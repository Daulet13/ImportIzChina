// ДТ#89566 id_list_client (т.е. Контрактодержатель, т.е. кто расплачивается - не знаю за товар или с таможней) <> Сармант 
// нужно создавать документы ПоступлениеТоваровУслуг, ОтчетКомитенту, ПередачаТоваров, ОтчетыАгента_ОтчетАгента 
// по мотивам задачи https://portal.canavara-group.ru/company/personal/user/716/tasks/task/view/120564/

//&НаКлиенте
//Перем MSWord; // COM-объект для создания документа Word, 
	// для ДобавитьОбработчик MSWord.Quit, ВыходИзWordОтправитьВПрисоединенныеФайлы и 
	// УдалитьОбработчик MSWord.Quit, ВыходИзWordОтправитьВПрисоединенныеФайлы;
	
// если ДТ отнесена более чем к одному заказу, то при обработка данных, полученных  из ОперацииВозмещенияТаможенныхПлатежейПоФайлуExcel	
// если точно, то около строки кода 1235, это в Процедура ЗаполнитьСписокДТФактическоеВыполнение(....)
// выполняемая с ВариантЗаполнения == "ЗакладкаПереданныеДанные"
// еще точнее - обработка запроса с текстом ПолучитьТекстЗапроса_СуммыИВалютаПоДТ(СтрокаID_ДТ) 
// для каждой ДТ будут получены из Логос суммы Таможенного сбора (1010), Пошлины (202), НДС (5010)
// а т.к. ДТ относится более чем к одному заказу, но ОтчетыАгента данной обработкой могут быть созданы строго для одного заказа, то
// для строк этой ДТ на закладке Главное не будут заполнены Валюта и указанные в Логос суммы таможенных платежей, т.к.
// если указать эти суммы таможенных платежей, то они будут отнесены ко всем (нескольким) отчетам агента по данной ДТ, т.е. учтены несколько раз.

#Область ИнтерфейсФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// не похоже, что в данном случае будет нужна МассаБрутто (за единицу) и СтрокаМассаБрутто (тоже за единицу)
	//ЭтаФорма.ОсновнойРеквизитНоменклатураМассаБрутто = НайтиДопРеквизитИлиДопСведение("МассаБрутто", Ложь, "Справочник.Номенклатура");	
	//ЭтаФорма.ОсновнойРеквизитНоменклатураСтрокаМассаБрутто = НайтиДопРеквизитИлиДопСведение("СтрокаМассаБрутто", Ложь, "Справочник.Номенклатура");		
	//Если НЕ ЗначениеЗаполнено(ЭтаФорма.ОсновнойРеквизитНоменклатураМассаБрутто) ИЛИ 
	//	НЕ ЗначениеЗаполнено(ЭтаФорма.ОсновнойРеквизитНоменклатураСтрокаМассаБрутто) Тогда
	//	Сообщить("Не найдены доп. реквизиты справочника Номенклатура МассаБрутто и СтрокаМассаБрутто");
	//	Отказ = Истина;
	//КонецЕсли;
	
	// переходный период изменений в учета по счету 002    
	Если ЗначениеЗаполнено(Справочники.Организации.НайтиПоРеквизиту("ИНН", "7805640197")) Тогда // т.е. база Сармант
		Объект.КонтролироватьСубсчет002 = Истина; // только субсчета 002 в проводках
	Иначе	
		Объект.КонтролироватьСубсчет002 = Ложь; // можно использовать в проводках счет 002 и его его субсчета
		Объект.СубСчет002 = ПланыСчетов.Хозрасчетный.НайтиПоКоду("002");		
	КонецЕсли;	
	
	ЭтаФорма.ОснСведениеНомераЗаказовНЛ_ПоступлениеТоваровУслуг = 
		НайтиДопРеквизитИлиДопСведение("НомераЗаказовНЛ", Истина, "Документ.ПоступлениеТоваровУслуг"); // значение строка
	ЭтаФорма.ОснСведениеНомерЗаказа_ОтчетКомитентуОПродажах = 
		НайтиДопРеквизитИлиДопСведение("НомерЗаказа", Истина, "Документ.ОтчетКомитентуОПродажах"); // значение число
	ЭтаФорма.ОснСведениеНомерЗаказа_ПередачаТоваров = 
		НайтиДопРеквизитИлиДопСведение("НомерЗаказа", Истина, "Документ.ПередачаТоваров"); // значение число 
	ЭтаФорма.ОснСведениеНомерЗаказа_ОперацияБух = 		
		НайтиДопРеквизитИлиДопСведение("НомерЗаказа", Истина, "Документ.ОперацияБух"); // значение число 		
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.ОснСведениеНомераЗаказовНЛ_ПоступлениеТоваровУслуг) ИЛИ 
		НЕ ЗначениеЗаполнено(ЭтаФорма.ОснСведениеНомерЗаказа_ОтчетКомитентуОПродажах) ИЛИ 
		НЕ ЗначениеЗаполнено(ЭтаФорма.ОснСведениеНомерЗаказа_ПередачаТоваров) ИЛИ 
		НЕ ЗначениеЗаполнено(ЭтаФорма.ОснСведениеНомерЗаказа_ОперацияБух) Тогда
		Сообщить("Должны быть зарегистрированы доп. сведения 'НомераЗаказовНЛ' (Документ.ПоступлениеТоваровУслуг), " + 
			"'НомерЗаказа' (Документ.ОтчетКомитентуОПродажах), 'НомерЗаказа' (Документ.ПередачаТоваров), 'НомерЗаказа' (Документ.ОперацияБух)");	
		Отказ = Истина;
	КонецЕсли;		
КонецПроцедуры  // ПриСозданииНаСервере(...)

&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	ЭтаФорма.ОтбиратьДТ = Истина;
	Объект.БазаЛогос = 1; // т.е. Сармант, по просьбе Ю.Куриленко 09.02.2024
	Если Объект.КонтролироватьСубсчет002 Тогда
		ЭтаФорма.Элементы.ОбъектСубСчет002.Заголовок = "СубСчет 002";
	Иначе
		ЭтаФорма.Элементы.ОбъектСубСчет002.Заголовок = "Счет 002";		
	КонецЕсли;	
	ЭтаФорма.СтрокаОписания = " 
|Добавлено 23.07.2024. Если существует вероятность, что даты создаваемых документов могут быть в закрытом для редактирования периоде, то
|рекомендуется указать параметр 'Дата док если период закрыт' на закладке 'Главное'. Если при записи нового документа возникнет ошибка, то будет 
|выполнена повторная попытка записать документ с датой 'Дата док если период закрыт', при этом курсы валют останутся от прежней, находящейся в 
|закрытом периоде, даты. Эта возможность работает только при создании пакета документов.   
|
|Добавлено 07.02.2024. Можно отбирать из Логос в таблицу ДТ как записи ДТ, так и записи заявлений на выпуск. Для номеров ДТ, являющихся 
|номерами заявлений, будет выполнен дополнительный поиск соответствующих им ДТ, номера и #таких ДТ будут выведены в столбцы ДТпоЗВ и #ДТпоЗВ
|в правой части таблицы ДТ.
|
|Добавлено 12.05.2023.	
|В данных Логос обнаружены в таблице ДТ записи для которых заполнено поле номеров заявлений и не
|заполнено поле номеров записей (##) этих заявлений, хотя заявления есть в базе (например для ДТ #162343 и 162342).
|Для таких ДТ дата заявления будет выделена из его номера.
|При загрузке наименований номенклатуры будут убраны слева строки 'ВДТ' и '(ВДТ)'.
|	
|Укажите организацию, базу Логос (Логос или Логос Сармант) интервал (период) дат, #клиента заказа, контрагента 1С, группу справочника 
|Контрагенты (для поиска поставщиков), контрагент и договор для таможенных платежей (для движений документа ОперацияБух) и нажмите кнопку 
|'1. Заполнить список ДТ'. #Марлин это 80241.
|
|В Логос будет выполнен поиск ДТ у которых дата выпуска или дата условного выпуска или дата выпуска под обеспечение
|попадает в указанный интервал и ДТ относятся к заказам, #клиента которых совпадает с указаным.
|Не будут показаны декларации, выпущенные по заявлениям. Будет выполнен отдельный поиск в Логос заявлений на выпуск 
|к найденным ранее ДТ.
|
|Если в НЛ не найден контрагент с указанным #, то выполнение будет приостановлено.
|
|АЛЬТЕРНАТИВНЫЙ ВАРИАНТ.
|Данная обработка должна быть открыта, можно не указывать в ней никаких параметров.
|В обработке 'Операции возмещения тамож платежей по файлу Excel' после завершения создания операций нажать на закладке
|'Создание/Проверка ОперацияБух' кнопку 'Передать данные'. В эту обработку будет переданы массивы данных  
|{НомерДТ, #ДТ, Контрагент, Договор, Организация, Операция}. После приема данных и дополнительных поисков в Логос в этой 
|обработке на закладке 'Переданные данные' будет сформирована обобщенная таблица (верхняя) с данными {Контрагент, Договор, 
|Организация, #КонтрагентаЗаказа, НаименованиеКонтрагентаНЛ, Строка##ДТ} для ускорения обработки переданных данных. 
|Если из обработки 'Операции возмещения тамож платежей по файлу Excel' переданы данные уже созданных документов ОперацияБух,
|то они будут отражены в таблице на закладке Главное и не будут созданы новые документы ОперацияБухг при создании пакетов документов. 
|Для обработки одной строки обобщенных переданных данных (верхняя таблица на закладке 'Переданные данные') на закладке 
|'Главное' нажмите кнопку 'ИЛИ 1.Обработать одного контрагента из переданных данных'. Затем нажмите кнопку '2. Дополнительная проверка' 
|и кнопку '3. Создать ОтчетыАгента'. Функционал кнопок '2. Дополнительная проверка' и '3. Создать ОтчетыАгента' описан ниже.
|Затем снова нажмите 'ИЛИ 1.Обработать одного контрагента из переданных данных', '2. Дополнительная проверка' и '3. Создать ОтчетыАгента' 
|для обработки следующей строки обобщенных переданных данных и т.д.            
|Если установлен признак 'ОтчетКомитенту и ОтчетАгента с датой ОперацияБух, даты ПТУ и ПередачаТоваров по заявлению на выпуск' (особое правило 
|дат документов):
|- если данные переданы из обработки ОперацииВозмещенияТаможенныхПлатежейПоФайлуExcel, то в таблице будут заполнены созданные ранее документы 
|  ОперацияБух и документы ОтчетКомитенту и ОтчетАгента будут сформированы с датой операции 
|- если найдено строго одно заявление на выпуск для ДТ строки, то ПоступлениеТоваровУслуг и ПередачаТоваров будут сформированы с датой 
|  заявления на выпуск
|- перед формированием документов будет выполнена проверка того, что дата/время ПоступлениеТоваровУслуг ранее чем дата/время ОтчетКомитенту и
|  выведено сообщения для каких #ДТ не применить особое правило дат документов
|Если НЕ установлен признак 'ОтчетКомитенту и ОтчетАгента с датой ОперацияБух, даты ПТУ и ПередачаТоваров по заявлению на выпуск', то документы 
|будут созданы со следующими датами:
|- ДатаПТУ = ДатаДокументов + 09:00			
|- ДатаОКОП = ДатаДокументов + 09:05
|- ДатаПередачаТоваров = ДатаДокументов + 09:10			
|- ДатаОтчетАгента = ДатаДокументов + 10.00
|- ДатаОперация = КонецДня(ДатаДокументов), 
|где ДатаДокументов выделена из средней части номера ДТ
|
|После нажатия кнопки '1. Заполнить список ДТ' или кнопки 'ИЛИ 1.Обработать переданные данные' будет заполнена таблица 
|подходящих ДТ на закладке 'Главное', при этом:
|- выделены из данных ДТ Логос:  
|  - валюта (графа 22), выполнен в 1с поиск валюты по коду 
|  - сумма в валюте 
|  - общая сумма таможенных платежей
|  - сумма таможенного сбора (1010)
|  - сумма пошлины (2010)
|  - сумма НДС (5010)
|  - сумма прочих таможенных платежей
|- из данных документов к ДТ (графа 44) в Логос будет выделен агентский договор (тип документа = '09023', наименование документа 
|  = 'АГЕНТСКИЙ ДОГОВОР'). Если в документах к ДТ найдено несколько агентских договоров, то об этом будет пометка в поле таблицы Примечания 
|- из данных документов к ДТ (графа 44) в Логос будет выделен инвойс (тип документа = '04021', наименование документа = 
|  'СЧЕТ-ФАКТУРА (ИНВОЙС) К ДОГОВОРУ'. Если в документах к ДТ найдено несколько инвойсов, то они буду перечислены через запятую. 
|- по данным полученных из Логос агентских договоров будет выполнен поиск договоров клиента в 1с с указанными номером и датой между 
|  организацией и контрагентом, тип договоров = СКомитентомНаЗакупку
|- будет выполнен поиск поставщика в 1с по подобию наименования (т.е. строка наименования из Логос является частью строки наименования 1С) в 
|  группе, указанной параметром 'Группа поиска поставщика'. Должен быть единственный не помеченный на удаление подходящий элемент справочника 1С.
|- будет выполнен поиск договора поставщика с указанной организацией в 1с по найденному выше поставщику и полученными из Логос номеру и дате, 
|  тип договора = СПоставщиком. Должен быть единственный  не помеченный на удаление подходящий элемент справочника 1С Договоры. 
|- для тех поставщиков которых не удалось найти описанным выше способом будет выполнен поиск по полученным из Логос номеру и дате договоров с 
|  указанной организацией, тип договора = СПоставщиком. Должен быть единственный  не помеченный на удаление подходящий элемент справочника 1С Договоры,  
|  причем контрагент договора должен быть в группе, указанной параметром 'Группа поиска поставщика'. Для отметки найденных таким способом поставщиков
|  и их договоров около поля Поставщик будет проставлен признак '???' для дополнительной проверки пользователем.
|
|Кроме того будет выполнено:
|- поиск в Логос грузовых единиц, входящих в ДТ 
|- получение из Логос данных строк ДТ
|- поиск в данных строк ДТ по полученным из Логос кодам единицы измерения и страны 
|- проверка полученных из Логос данных и результатов поиска, будет заполнено поле таблицы Примечания. Не пустое Примечание не позволит
|  пометить ДТ для создания покументов 1С
|- поиск документов ОтчетАгента (новый документ 1С, связыващий документы 1С, печатные формы, прикрепленные файлы, Логос, Битрикс, отправку 
|  по EMail и проч.)
|- будет определен номер отчета агента, т.е. выделен из референса заказа если референс имеет вид 'УТК ХХХ ...' (ХХХ - число) или, при 
|  другом формате референса, #заказа. Номер отчета агента может быть изменен интерактивно до формирования пакетов документов. 
|
|Допускается интерактивное (вручную) изменение значений полей таблиц:
|- поставщик и договор поставщика (верхняя таблица)
|- единица измерения, количество и страна (нижняя таблица)
|
|Если указать 'Договор по умолчанию' (связан с полями 'Организация' и 'Контрагент 1С', вид договора = 'С комитентом (принципалом) на закупку') и описанным
|выше способом не найден договор клиента, то будет подставлен 'Договор по умолчанию'.
|
|Если в данных строк ДТ не указаны количество и единица измерения и установлен признак 'Для строк ДТ: нетто-> количество, кг' и заполнено значение
|'Масса нетто', то будет подставлена единица измерения 'кг' и количество = 'Масса нетто'.
|
|После интерактивного изменения для обновления поля Примечания верхней таблицы и возможности создания документов нажмите кнопку 
|'2. Дополнительная проверка'.
|
|При создании печатной формы отчета агента будут использованы:
|- указанные банковские счета организации и контрагента 1С
|- данные из договоров контрагента (номер, дата, данные из раздела Подписи)
|
|Если установлен признак 'Комисс. вознаграждение' и заполнены поля комиссионного вознаграждения (услуга по вознаграждению, счет учета доходов,
|ставка НДС вознаграждения, счет учета НДС), то при создании пакета документов в табличной части 'Товары' документа ОтчетКомитентуОПродажах будет
|указано комиссионное вознаграждение.
|
|Если контрактодержатель по ДТ <> Сармант, то при создании пакета документов будет выполнена программная коррекция движений документа
|ПоступлениеТоваровУслуг, оставлены только движения у которых СчетДт.Родитель == 002, установлен признак ручной корректировки движений по бухгалтерскому и
|налоговому учету. Если установлен признак 'Оставлять все проводки (для ПП)', коррекция движений не выполняется — все проводки (включая Дт 76 / Кт 60) остаются.
|
|Для создания необходимых пакетов документов нужно поставить галки в левой колонке (создать) и нажать кнопку '3. Создать ОтчетыАгента'.
|Могут быть отмечены только строки ДТ для которых пуста колонка 'Примечания'.
|При этом будут созданы документы ПоступлениеТоваровУслуг, ОтчетКомитентаОПродажах, ПередачаТоваров, Операция и ОтчетАгента (новый, не входящий в 
|типовую конфигурацию документ), необходимые элементы справочников Номенклатура, НомераГТД, КодыТНВЭД. Для документов ПоступлениеТоваровУслуг, 
|ОтчетКомитентаОПродажах, ПередачаТоваров, Операция будут заполнены значения доп. сведений НомерЗаказа.
|
|ВАЖНОЕ УТОЧНЕНИЕ: Если контрактодержатель ДТ это Сармант (#Контрактодержателя = 3295 в верхней таблице в базе Логос), то будет создан полный описанный выше
|полный комплект документов. Если контрактодержатель - не Сармант, будет создан только документ Операция, печатная форма ОтчетаАгента (по кнопке
|на форме обработки) не будет содержать ПередачаТоваров, Инвойc №, Покупка товара (в таблице).
|
|После завершения создания пакетов документов будут сняты галки 'Создать' и установлены 'Создан'. Далее для успешно созданных пакетов документов 
|будут созданы печатные формы ОтчетаАгента и сохранены в присоединенные к ОтчетамАгента файлы. При возникновении ошибок будут установлены 
|галки 'Ошибка', подробное описание ошибок будет в поле 'Примечания'.
|
|В строках, помеченных 'Создан', щелкните на колонке 'Отчет агента'. В открывшейся форме документа ОтчетАгента:
|- вида основная информация по отчету агента (документы ПоступлениеТоваровУслуг, ОтчетКомитентуОПродажах, ПередачаТоваров, Операция, 
|  данные ДТ и грузовых единиц и проч) 
|- закладка 'Главное', нажмите кнопку 'Обновить данные из Логос'
|- закладка 'Главное', нижняя табличная часть, при создании ОтчетаАгента для документа ПередачаТоваров установлена галка Акт '(передача товаров)'
|- нажмите кнопку 'Печатные формы в присоединенные файлы (долго)'. Теперь в присоединенных к ОтчетАгента файлах (кроме сформированной при создании
|  пакета документов печатной формы ОтчетаАгента) есть и печатная форма акта (передача товаров).
|- закладка 'Файлы из Логос'. Отметьте в левой колонке ('Загрузить') файлы декларации (выпуск) и инвойсов. Выделить их автоматически невозможно. 
|  Нажмите кнопку 'Загрузить файлы из Логос в присоединенные к ОтчетАгента'. Отмеченные файлы будут добавлены к присоединенным к ОтчетуАгента файлам
|- закладка 'Отправка в Логос.' Нажмите кнопку 'Подготовить список файлов для отправки в Логос'. Отметьте в левой колонке ('Отправить') файл отчета агента.
|  Нажмите кнопку 'Отправить в Логос'. Файл отчета агента будет прикреплен к заказу Логос.
|- перейдите на закладку 'Отправка в Битрикс'. Должен быть указан #Партнера (для Марлин это 80241 в базе Логос). Нажмите кнопку 'Подготовить список файлов для 
|  отправки в Битрикс'. Отметьте в левой колонке ('Отправить') нужные файлы. Нажмите кнопку 'Отправить файлы в Битрикс'. 
|- если нужна отправка по E-Mail, то перейдите на закладку 'Отправка по E-mail'. Нажмите кнопку 'Подготовить письмо'. Укажите адреса получателей,
|  при необходимости исправьте тему и текст письма, отметьте файлы для отправки по E-Mail, нажмите кнопку 'Отправить письмо по E-Mail'
|
|Созданные таким способом документы ОтчетАгента с прикрепленными к ним файлами доступны по меню Расширения...Отчеты агента. Отправить в Логос и Битрикс
|можно в любой момент.
|
|Для замены присоединенного к ОтчетАгента файла новым файлом в форме документа ОтчетАгента: Присоединенные файлы..... открыть данные записи 
|в списке файлов, указать файл, поставить галку 'Перезаписать файл', записать. 
|
|";	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектБазаЛогосПриИзменении(Элемент)
	Объект.ПереданныеДанные.Очистить();
	Объект.ПереданныеДанныеИтоги.Очистить();
	Объект.ПереданныеНомерДТОперация.Очистить(); 
	ЭтаФорма.ДТДляГрупповогоСоздания.Очистить();
	ЭтаФорма.НоменклатураДТ.Очистить();
	ЭтаФорма.НоменклатураДТОтображение.Очистить();
	ЭтаФорма.НомераГрузовыхЕдиницПоДТ.Очистить();
	ЭтаФорма.IDКлиентаЗаказа = 0;
	ЭтаФорма.НаименованиеКонтрагентаНЛ = "";
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПериод(Команда)
	ВыбранныйПериод = Новый СтандартныйПериод;
	// Устанавливаем начальные данные
	ВыбранныйПериод.ДатаНачала = ЭтаФорма.НачальнаяДата;
	ВыбранныйПериод.ДатаОкончания = ЭтаФорма.КонечнаяДата;
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = ВыбранныйПериод;
	// Не модальный вызов диалога выбора периода
	Диалог.Показать(Новый ОписаниеОповещения("ВыборПериодаЗавершение", ЭтаФорма, Новый Структура("Диалог", Диалог)));
КонецПроцедуры
 
&НаКлиенте
Процедура ВыборПериодаЗавершение(Период, ДополнительныеПараметры) Экспорт
	Диалог = ДополнительныеПараметры.Диалог;
	Если ЗначениеЗаполнено(Период) Тогда 
		ВыбранныйПериод = Диалог.Период;
		ЭтаФорма.НачальнаяДата = ВыбранныйПериод.ДатаНачала;
		ЭтаФорма.КонечнаяДата = ВыбранныйПериод.ДатаОкончания;
		ЭтаФорма.ДТДляГрупповогоСоздания.Очистить();
		ЭтаФорма.НоменклатураДТ.Очистить();
		ЭтаФорма.НоменклатураДТОтображение.Очистить();
		ЭтаФорма.НаименованиеКонтрагентаНЛ = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДТДляГрупповогоСозданияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДТДляГрупповогоСозданияПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСписокДТ(Команда)
	ЭтаФорма.ДТДляГрупповогоСоздания.Очистить();
	ЭтаФорма.НоменклатураДТ.Очистить();
	ЭтаФорма.НоменклатураДТОтображение.Очистить();
	ЭтаФорма.НаименованиеКонтрагентаНЛ = "";	
КонецПроцедуры

&НаКлиенте
Процедура ДТДляГрупповогоСозданияПриАктивизацииСтроки(Элемент)
	ЭтаФорма.НоменклатураДТОтображение.Очистить();	
	ТекДанные = ЭтаФорма.Элементы.ДТДляГрупповогоСоздания.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	СтруктураПоискаПриАктивизации = Новый Структура("IDДТ", ТекДанные.IDДТ);
	НСПриАктивизации = ЭтаФорма.НоменклатураДТ.НайтиСтроки(СтруктураПоискаПриАктивизации);
	Для каждого СтрокаНСПриАктивизации из НСПриАктивизации Цикл
		СтрокаНомДТОтображение = ЭтаФорма.НоменклатураДТОтображение.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНомДТОтображение, СтрокаНСПриАктивизации);
	КонецЦикла;	
	ЭтаФорма.ОбновитьОтображениеДанных();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	ПараметрОповещения = Новый Структура("а, б", 12, 45);
	Оповестить("ВыполненоГрупповоеСозданиеОтчетовАгента", ПараметрОповещения);	
КонецПроцедуры

&НаКлиенте
Процедура IDКонтрагентаЗаказаПриИзменении(Элемент)
	ЭтаФорма.ДТДляГрупповогоСоздания.Очистить();
	ЭтаФорма.НоменклатураДТ.Очистить();
	ЭтаФорма.НоменклатураДТОтображение.Очистить();	
	ЭтаФорма.НаименованиеКонтрагентаНЛ = "";
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометку(Команда)
	Для каждого СтрокаДТ из ЭтаФорма.ДТДляГрупповогоСоздания Цикл
		Если ПустаяСтрока(СтрокаДТ.Примечания) Тогда 
			СтрокаДТ.Пометка = Истина;
		Иначе
			СтрокаДТ.Пометка = Ложь;			
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометку(Команда)
	Для каждого СтрокаДТ из ЭтаФорма.ДТДляГрупповогоСоздания Цикл
		СтрокаДТ.Пометка = Ложь;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура ДТДляГрупповогоСозданияПометкаПриИзменении(Элемент)
	ТекДанные = ЭтаФорма.Элементы.ДТДляГрупповогоСоздания.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	Если НЕ ПустаяСтрока(ТекДанные.Примечания) Тогда
		ТекДанные.Пометка = Ложь;					
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ЭтаФорма.ДТДляГрупповогоСоздания.Очистить();
	ЭтаФорма.НоменклатураДТ.Очистить();
	ЭтаФорма.НоменклатураДТОтображение.Очистить();	
	ЭтаФорма.НаименованиеКонтрагентаНЛ = "";
КонецПроцедуры

&НаКлиенте
Процедура Контрагент1CПриИзменении(Элемент)
	ЭтаФорма.ДТДляГрупповогоСоздания.Очистить();
	ЭтаФорма.НоменклатураДТ.Очистить();
	ЭтаФорма.НоменклатураДТОтображение.Очистить();	
	ЭтаФорма.НаименованиеКонтрагентаНЛ = "";
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураДТОтображениеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураДТОтображениеПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяПроверка(Команда)
	КилограммСсылка = НайтиКилограммНаСервере();
	Если ЭтаФорма.ЗаменаКолваЕдИзмерения Тогда
		Для каждого НомДТ из ЭтаФорма.НоменклатураДТ Цикл
			Если НЕ ЗначениеЗаполнено(НомДТ.ЕдИзм) И (НомДТ.Количество = 0) И (НомДТ.ВесНетто > 0) Тогда
				НомДТ.Количество = НомДТ.ВесНетто;
				НомДТ.ЕдИзм = КилограммСсылка;
			КонецЕсли;	
		КонецЦикла; 
	КонецЕсли;
	
	Для каждого СтрокаДТ из ЭтаФорма.ДТДляГрупповогоСоздания Цикл
		СтрокаДТ.Примечания = "";
		Если СтрокаДТ.ДатаДокументов = Дата(1, 1, 1) Тогда
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Дата ДТ;";
		КонецЕсли;
		Если СтрокаДТ.ВсегоПлатежей <> СтрокаДТ.Сумма1010 + СтрокаДТ.Сумма2010 + СтрокаДТ.Сумма5010 Тогда
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Всего тамож.платежей<>1010+2010+5010; ";
		КонецЕсли;	
		//Если НЕ ПустаяСтрока(СтрокаДТ.НомерДоговора) И (СтрНайти(СтрокаДТ.Примечания,"Несколько договоров; ")=0) Тогда
		//	СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Несколько договоров; ";
		//КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтрокаДТ.Договор) Тогда
			Если ЗначениеЗаполнено(ЭтаФорма.ДоговорКонтрагент1СПоУмолчанию) Тогда
				СтрокаДТ.Договор = ЭтаФорма.ДоговорКонтрагент1СПоУмолчанию;
			Иначе	
				СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Договор?; ";
			КонецЕсли;	
		КонецЕсли;	
		Если НЕ ЗначениеЗаполнено(СтрокаДТ.Валюта) Тогда
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Валюта?; ";
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтрокаДТ.Поставщик) Тогда
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Поставщик?; ";
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтрокаДТ.ДоговорПоставщика) Тогда
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Договора поставщика?; ";
		КонецЕсли;
	КонецЦикла;

	// доп проверки, все ли заполнено в таблице значений ЭтаФорма.НоменклатураДТ 
	Для Каждого СтрокаНом из ЭтаФорма.НоменклатураДТ Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаНом.Страна) Тогда
			СтруктураНеЗаполнено = Новый Структура("IDДТ", СтрокаНом.IDДТ);
			НСНеЗаполнено = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураНеЗаполнено);
			Если СтрНайти(НСНеЗаполнено[0].Примечания, "Страна в строках ДТ;") = 0 Тогда
				НСНеЗаполнено[0].Примечания = НСНеЗаполнено[0].Примечания + "Страна в строках ДТ; "; 
			КонецЕсли;
		КонецЕсли;	
		Если НЕ ЗначениеЗаполнено(СтрокаНом.ЕдИзм) Тогда
			СтруктураНеЗаполнено = Новый Структура("IDДТ", СтрокаНом.IDДТ);
			НСНеЗаполнено = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураНеЗаполнено);
			Если СтрНайти(НСНеЗаполнено[0].Примечания, "Ед.изм. в строках ДТ;") = 0 Тогда
				НСНеЗаполнено[0].Примечания = НСНеЗаполнено[0].Примечания + "Ед.изм. в строках ДТ; "; 
			КонецЕсли;
		КонецЕсли;	
		Если (СтрокаНом.Количество = 0) Тогда		
			СтруктураНеЗаполнено = Новый Структура("IDДТ", СтрокаНом.IDДТ);
			НСНеЗаполнено = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураНеЗаполнено);
			Если СтрНайти(НСНеЗаполнено[0].Примечания, "Количество в строках ДТ;") = 0 Тогда
				НСНеЗаполнено[0].Примечания = НСНеЗаполнено[0].Примечания + "Количество в строках ДТ; "; 
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;	

	ЭтаФорма.ОбновитьОтображениеДанных();	
	
КонецПроцедуры  // ДополнительнаяПроверка(Команда)

&НаКлиенте
Процедура ДТДляГрупповогоСозданияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекДанные = ЭтаФорма.Элементы.ДТДляГрупповогоСоздания.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если (Поле.Имя = "ДТДляГрупповогоСозданияОтчетАгента") И НЕ ПустаяСтрока(ТекДанные.ОтчетАгента) И 
		НЕ ПустаяСтрока(ТекДанные.ОтчетАгентаGUID) Тогда
		СтрукураКлюч = Новый Структура("Ключ", ПолучитьСсылку_ОтчетыАгента_ОтчетАгента(ТекДанные.ОтчетАгентаGUID));
		ФормаДокумента = ПолучитьФорму("Документ.ОтчетыАгента_ОтчетАгента.ФормаОбъекта", СтрукураКлюч);
		ФормаДокумента.Открыть();			
	ИначеЕсли (Поле.Имя = "ДТДляГрупповогоСозданияПоступлениеТоваровУслуг") И 
		ЗначениеЗаполнено(ТекДанные.ПоступлениеТоваровУслуг) Тогда		
		СтрукураКлюч = Новый Структура("Ключ", ТекДанные.ПоступлениеТоваровУслуг);
		ФормаДокумента = ПолучитьФорму("Документ.ПоступлениеТоваровУслуг.ФормаОбъекта", СтрукураКлюч);
		ФормаДокумента.Открыть();	
	ИначеЕсли (Поле.Имя = "ДТДляГрупповогоСозданияПередачаТоваров") И 
		ЗначениеЗаполнено(ТекДанные.ПередачаТоваров) Тогда		
		СтрукураКлюч = Новый Структура("Ключ", ТекДанные.ПередачаТоваров);
		ФормаДокумента = ПолучитьФорму("Документ.ПередачаТоваров.ФормаОбъекта", СтрукураКлюч);
		ФормаДокумента.Открыть();	
	ИначеЕсли (Поле.Имя = "ДТДляГрупповогоСозданияОтчетКомитентуОПродажах") И 
		ЗначениеЗаполнено(ТекДанные.ОтчетКомитентуОПродажах) Тогда		
		СтрукураКлюч = Новый Структура("Ключ", ТекДанные.ОтчетКомитентуОПродажах);
		ФормаДокумента = ПолучитьФорму("Документ.ОтчетКомитентуОПродажах.ФормаОбъекта", СтрукураКлюч);
		ФормаДокумента.Открыть();	
	ИначеЕсли (Поле.Имя = "ДТДляГрупповогоСозданияОперация") И 
		ЗначениеЗаполнено(ТекДанные.Операция) Тогда		
		СтрукураКлюч = Новый Структура("Ключ", ТекДанные.Операция);
		ФормаДокумента = ПолучитьФорму("Документ.ОперацияБух.ФормаОбъекта", СтрукураКлюч);
		ФормаДокумента.Открыть();	
	ИначеЕсли (Поле.Имя = "ДТДляГрупповогоСозданияДоговор") И ЗначениеЗаполнено(ТекДанные.Договор)	Тогда
		СтрукураКлюч = Новый Структура("Ключ", ТекДанные.Договор);		
		ФормаСпр = ПолучитьФорму("Справочник.ДоговорыКонтрагентов.ФормаОбъекта", СтрукураКлюч);
		ФормаСпр.Открыть();			
	ИначеЕсли (Поле.Имя = "ДТДляГрупповогоСозданияПоставщик") И ЗначениеЗаполнено(ТекДанные.Поставщик) Тогда
		СтрукураКлюч = Новый Структура("Ключ", ТекДанные.Поставщик);		
		ФормаСпр = ПолучитьФорму("Справочник.Контрагенты.ФормаОбъекта", СтрукураКлюч);
		ФормаСпр.Открыть();			
	ИначеЕсли (Поле.Имя = "ДТДляГрупповогоСозданияДоговорПоставщика") И ЗначениеЗаполнено(ТекДанные.ДоговорПоставщика) Тогда
		СтрукураКлюч = Новый Структура("Ключ", ТекДанные.ДоговорПоставщика);		
		ФормаСпр = ПолучитьФорму("Справочник.ДоговорыКонтрагентов.ФормаОбъекта", СтрукураКлюч);
		ФормаСпр.Открыть();			
	КонецЕсли;	
КонецПроцедуры  // ДТДляГрупповогоСозданияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

&НаКлиенте
Процедура ОбъектПереданныеНомерДТОперацияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекДанные = ЭтаФорма.Элементы.ОбъектПереданныеНомерДТОперация.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	Если ЗначениеЗаполнено(ТекДанные.Операция) Тогда		
		СтрукураКлюч = Новый Структура("Ключ", ТекДанные.Операция);
		ФормаДокумента = ПолучитьФорму("Документ.ОперацияБух.ФормаОбъекта", СтрукураКлюч);
		ФормаДокумента.Открыть();	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьСсылку_ОтчетыАгента_ОтчетАгента(СтрокаUID)
	// должно быть подключено расширение УчетЦенКлиентов
	Если ПустаяСтрока(СтрокаUID) Тогда
		Возврат Документы.ОтчетыАгента_ОтчетАгента.ПустаяСсылка();		
	Иначе	
		НовыйUID = Новый УникальныйИдентификатор(СтрокаUID);	
		Возврат Документы.ОтчетыАгента_ОтчетАгента.ПолучитьСсылку(НовыйUID);
	КонецЕсли;	
КонецФункции

&НаКлиенте
Процедура НоменклатураДТОтображениеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекДанные = ЭтаФорма.Элементы.НоменклатураДТОтображение.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если (Поле.Имя = "НоменклатураДТОтображениеНоменклатура") И ЗначениеЗаполнено(ТекДанные.Номенклатура) Тогда
		СтрукураКлюч = Новый Структура("Ключ", ТекДанные.Номенклатура);		
		ФормаСпр = ПолучитьФорму("Справочник.Номенклатура.ФормаОбъекта", СтрукураКлюч);
		ФормаСпр.Открыть();			
	КонецЕсли;	
КонецПроцедуры  // НоменклатураДТОтображениеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

&НаКлиенте
Процедура ОбъектСубСчет002ПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.СубСчет002) Тогда
		ОчиститьСообщения();
		ОбъектСубСчет002ПриИзмененииНаСервере();
		ЭтаФорма.ОбновитьОтображениеДанных();
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Функция Это002ИлиСубсчет002(СчетИзПлана)
	Если (СчетИзПлана.Код = "002") ИЛИ (СчетИзПлана.Код = "002.00") ИЛИ 
		(СчетИзПлана.Код = "002.01") ИЛИ (СчетИзПлана.Код = "002.02") Тогда
		// значение корректно, счет 002 или его субсчета
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;	
КонецФункции	

&НаСервере
Процедура ОбъектСубСчет002ПриИзмененииНаСервере() 
	Если Объект.КонтролироватьСубсчет002 Тогда
		Если Объект.СубСчет002.Родитель.Код <> "002" Тогда
			Объект.СубСчет002 = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
			Сообщить("Укажите субсчет счета 002");
		КонецЕсли;	
	Иначе 
		Если НЕ Это002ИлиСубсчет002(Объект.СубСчет002) Тогда
			Объект.СубСчет002 = ПланыСчетов.Хозрасчетный.НайтиПоКоду("002");
			Сообщить("Укажите счет 002 или его субсчет");
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры	

#КонецОбласти // ИнтерфейсФормы

#Область РаботаСДопРеквДопСведениями

&НаСервере
Функция НайтиДопРеквизитИлиДопСведение(ТиповоеНаименование, ЭтоДопСведение, СтрокаВладельца)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка
		|ИЗ
		|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
		|ГДЕ
		|	ДополнительныеРеквизитыИСведения.Наименование Подобно &Наименование
		|	И ДополнительныеРеквизитыИСведения.НаборСвойств = &НаборСвойств
		|	И (ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение = &ЭтоДополнительноеСведение)
		|	И НЕ ДополнительныеРеквизитыИСведения.ПометкаУдаления		";
	
	Запрос.УстановитьПараметр("Наименование", ТиповоеНаименование+"%");
	Запрос.УстановитьПараметр("ЭтоДополнительноеСведение", ЭтоДопСведение);
	Если СтрокаВладельца = "Справочник.Номенклатура" Тогда
		Запрос.УстановитьПараметр("НаборСвойств", Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Номенклатура);	
	ИначеЕсли СтрокаВладельца = "Документ.ПоступлениеТоваровУслуг" Тогда
		Запрос.УстановитьПараметр("НаборСвойств", Справочники.НаборыДополнительныхРеквизитовИСведений.Документ_ПоступлениеТоваровУслуг);	
	ИначеЕсли СтрокаВладельца = "Документ.ОтчетКомитентуОПродажах" Тогда
		Запрос.УстановитьПараметр("НаборСвойств", Справочники.НаборыДополнительныхРеквизитовИСведений.Документ_ОтчетКомитентуОПродажах);			
	ИначеЕсли СтрокаВладельца = "Документ.ПередачаТоваров" Тогда
		Запрос.УстановитьПараметр("НаборСвойств", Справочники.НаборыДополнительныхРеквизитовИСведений.Документ_ПередачаТоваров);			
	ИначеЕсли СтрокаВладельца = "Документ.ОперацияБух" Тогда
		Запрос.УстановитьПараметр("НаборСвойств", Справочники.НаборыДополнительныхРеквизитовИСведений.Документ_ОперацияБух);					
	КонецЕсли;	
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		Сообщить(?(ЭтоДопСведение,"Не найдено доп.сведение '", "Не найден доп.реквизит '") + ТиповоеНаименование + "' для " + СтрокаВладельца);		
		Возврат ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка();
	ИначеЕсли Выборка.Количество() = 1 Тогда	
		Выборка.Следующий();
		//Сообщить("Найдено единственное дополнительное сведение '" + ТиповоеНаименование + "' для документа РеализацияТоваровУслуг. ");				
		Возврат Выборка.Ссылка;
	ИначеЕсли Выборка.Количество() > 1 Тогда	
		Сообщить("Найдено несколько доп." + ?(ЭтоДопСведение,"сведений '", "реквизитов '") + ТиповоеНаименование + "' для " + СтрокаВладельца);		
		Возврат ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка();
	КонецЕсли;	
КонецФункции // НайтиДопРеквизитИлиДопСведение(...)	

&НаСервере
Функция ЗаписатьЗначениеДопСведения(Объект, Свойство, Значение)
//Объект Тип: Любая ссылка. Ссылка на объект для которого записывается сведение;
//Свойство Тип: План видов характеристик ДополнительныеРеквизитыИСведения. Дополнительное свойство;
//Значение Тип: Произвольный(Задается при создании (редактировании) свойства в плане видов 
// характеристик ДополнительныеРеквизитыИСведения). Значение, которое будет записано в дополнительные сведения.	

    Попытка
        НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
        НаборЗаписей.Отбор.Объект.Установить(Объект);
        НаборЗаписей.Отбор.Свойство.Установить(Свойство);
        НоваяСтрокаНабора = НаборЗаписей.Добавить();
        НоваяСтрокаНабора.Объект = Объект;
        НоваяСтрокаНабора.Свойство = Свойство;
        НоваяСтрокаНабора.Значение = Значение;
        НаборЗаписей.Записать();
        Возврат Истина;
    Исключение
        Возврат Ложь;
    КонецПопытки;
КонецФункции

#КонецОбласти // РаботаСДопРеквДопСведениями

#Область ИнтерфейсPostgres

&НаКлиенте
Процедура ЗакрытьПодключениеКPostgres(ConRSCom)
	ConRSCom.RecordSet = "";
	ConRSCom.Command = "";
	Если ТипЗНЧ(ConRSCom.Connection) <> Тип("ComОбъект")	Тогда //  Connection = ? Нет подключения к базе данных	
		Возврат;
	КонецЕсли;		
	ДействующееПодключение = Ложь;
	Попытка 
		Если ConRSCom.Connection.State = 1 Тогда // == подключено		
			ДействующееПодключение = Истина;
		КонецЕсли;	
	Исключение
	КонецПопытки;	
	Если НЕ ДействующееПодключение Тогда // Подключение не активно
		Возврат;
	КонецЕсли;	
	Попытка
		ConRSCom.Connection.Close(); // База PostgreSQL отключена		
		Connection = "";
	Исключение
		Сообщить("Ошибка отключения от базы Логос.");  
		Сообщить(ОписаниеОшибки());		
	КонецПопытки;	
КонецПроцедуры	// Процедура ЗакрытьПодключениеКPostgres(ConRSCom)

&НаКлиентеНаСервереБезКонтекста
Функция ВернутьПравильнуюСтрокуID(ИсходныйКод)
	// группы по 3 цифры могут быть разделены пробелом
	СтрокаID = СтрЗаменить(Строка(ИсходныйКод)," ",""); 
	// а могут - неразрывным пробелом (0160) 	
	СтрокаID = СтрЗаменить(СтрокаID,Символы.НПП,""); 
	Возврат СтрокаID;
КонецФункции	

&НаКлиенте
Функция ДатаВФорматеЗапросаPostgres(НачДатаДата) 
	// 23.08.2018 14:12:17-> "2018-08-23 14:12:17" 
	Возврат Формат(НачДатаДата, "ДФ='yyyy-MM-dd HH:mm:ss'");	
КонецФункции	

&НаКлиенте
Функция ВернутьСтруктуруConnectionRecordSetCommand()

	Connection = "";
	Попытка
		Connection = Новый COMОбъект("ADODB.CONNECTION");
		Connection.Provider	= "MSDASQL.1";    
	
		Если Объект.БазаЛогос = 0 Тогда
			Connection.ConnectionString = "Driver={PostgreSQL Unicode};data source=LogOs;STMT=utf8";
		ИначеЕсли Объект.БазаЛогос = 1 Тогда
			Connection.ConnectionString = "Driver={PostgreSQL Unicode};data source=LogOsSarmant;STMT=utf8";			
		ИначеЕсли Объект.БазаЛогос = 2 Тогда
			Connection.ConnectionString = "Driver={PostgreSQL Unicode};data source=LogOsSavelovo;STMT=utf8";						
		ИначеЕсли Объект.БазаЛогос = 3 Тогда
			Connection.ConnectionString = "Driver={PostgreSQL Unicode};data source=LogOsKL;STMT=utf8";
		КонецЕсли;	
		
		Connection.Open();	// База PostgreSQL подключена
	Исключение		    
		Сообщить("Ошибка подключения к базе Логос.");
		Сообщить(ОписаниеОшибки());			
		Connection = Неопределено;
		Возврат Неопределено;
	КонецПопытки;
	Если ТипЗНЧ(Connection) <> Тип("ComОбъект")	Тогда
		Сообщить("Нет подключения к базе Логос.");		
		Возврат Неопределено;
	КонецЕсли;		
	ДействующееПодключение = Ложь;
	Попытка 
		Если Connection.State = 1 Тогда // == подключено		
			ДействующееПодключение = Истина;
		КонецЕсли;	
	Исключение
	КонецПопытки;	
	Если НЕ ДействующееПодключение Тогда
		Сообщить("Подключение к базе Логос не активно");
		Возврат Неопределено;
	КонецЕсли;	
	
	RecordSet = "";
	Command = "";

	Попытка    
		RecordSet = Новый ComObject("ADODB.RecordSet");
		Command = Новый COMОбъект("ADODB.Command");
		Command.ActiveConnection = Connection;    
	Исключение    
		Сообщить("Ошибка создания ADODB.RecordSet или ADODB.Command"); 
		Сообщить(ОписаниеОшибки());				
		ЗакрытьПодключениеКPostgres(Новый Структура("Connection, RecordSet, Command", Connection, RecordSet, Command));				
		RecordSet = "";
		Команда = "";
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Новый Структура("Connection, RecordSet, Command", Connection, RecordSet, Command);
КонецФункции // ВернутьСтруктуруConnectionRecordSetCommand()	

#КонецОбласти // ИнтерфейсPostgres

#Область ПолучениеСпискаДТизНЛ

&НаКлиенте
Функция ПолучитьТекстЗапроса_ЗаполнитьСписокДТ(ВариантЗаполнения, СтрокаIDДТ)
	// ВариантЗаполнения = "ЗакладкаГлавное" - по данным полей закладки Главное, СтрокаIDДТ = Неопределено
	// ВариантЗаполнения = "ЗакладкаПереданныеДанные" - по данным полей закладки Переданные данные, СтрокаIDДТ - строка	ID ДТ для запроса
	
// при соединениях возможны повторы строк, добавлено distinct
// Для принципала: COALESCE — если id_company_principal пуст (NULL/0), берём id_company_client.
// Покрывает прямые контракты (ПП), где принципал = клиент заказа.
// ОТКЛЮЧЕНО 26.02.2026: галочка ИспользоватьПринципала мешает корректной работе при ПП,
// при прямых контрактах обработка работает нормально без неё (по обратной связи от бухгалтерии)
//Если ЭтаФорма.ИспользоватьПринципала Тогда
//	ВыражениеКомпании = "COALESCE(NULLIF(list_order.id_company_principal, 0), list_order.id_company_client)";
//Иначе
	ВыражениеКомпании = "list_order.id_company_client";
//КонецЕсли;
ТекстЗапроса = "
|SELECT distinct
|   list_gtd.id as gtd_id,
|   list_gtd.gtd_number,
|   COALESCE(list_gtd.date_clear + interval '3 hour','1000-01-01'::timestamp) as date_clear,  -- дата выпуска
|   COALESCE(list_gtd.date_conditional_clear + interval '3 hour','1000-01-01'::timestamp) as date_conditional_clear, -- дата условного выпуска
|   COALESCE(list_gtd.date_release_for_procuring + interval '3 hour','1000-01-01'::timestamp) as date_release_for_procuring, -- дата выпуска под обеспечение
|   list_order.id as order_id,
|   COALESCE(list_order.sys_serial_number, '') as sys_serial_number,
|   COALESCE(list_gtd.id_list_client, 0) as id_list_client,  -- контрактодержатель
|   COALESCE(LIST_GTD.sum_curs, 0) as sum_curs, -- курс валюты графа 23, курс взаиморасчетов
|   COALESCE(LIST_GTD.seller_name, '') as seller_name,  -- наименование контрагента для документа ПоступлениеТоваровУслуг
|   COALESCE(list_order.order_date + interval '3 hour','1000-01-01'::timestamp) as order_date,  -- дата заказа
|   COALESCE(LIST_GTD.contract_number, '') as contract_number,
|   COALESCE(LIST_GTD.contract_date + interval '3 hour','1000-01-01'::timestamp) as contract_date,
|   COALESCE(list_order.number_by_client, '') as number_by_client,
|   COALESCE(LIST_GTD.number_decl_clear_from_feaccs, '') as number_decl_clear_from_feaccs,  -- text, номера ЗВ к Дт через запятую
|   -- id деклараций-заявлений, после ODBC приходят как строка, пустой массив - как строка {}
|   COALESCE(LIST_GTD.ids_number_decl_clear_from_feaccs, ARRAY[]::bigint[]) as ids_number_decl_clear_from_feaccs,
|   coalesce(cardinality(LIST_GTD.ids_number_decl_clear_from_feaccs), 0) as cardinality_ids_number_decl_clear_from_feaccs
|FROM public.list_gtd list_gtd
| LEFT JOIN declaration.unit AS dec_unit ON dec_unit.id_list_gtd = list_gtd.id
| LEFT JOIN declaration.dec_unit_oper_unit_link AS link ON link.id_declaration_unit = dec_unit.id
| LEFT JOIN operation.unit unit ON link.id_operation_unit = unit.id
| LEFT JOIN public.order list_order ON unit.id_order = list_order.id
|WHERE
|   list_gtd.deleted = 0
|   and list_gtd.date_recall is NULL -- дата отзыва
|   and list_gtd.date_refuse is NULL -- дата отказа в выпуске
|   and list_gtd.gtd_number is not null
|   and " + ВыражениеКомпании + " = " + ВернутьПравильнуюСтрокуID(ЭтаФорма.IDКлиентаЗаказа) + " ";
Если ВариантЗаполнения = "ЗакладкаГлавное" Тогда // по данным полей закладки Главное
ТекстЗапроса = ТекстЗапроса + "			
|--and (list_gtd.date_clear is NOT NULL or list_gtd.date_conditional_clear is NOT NULL or list_gtd.date_release_for_procuring is NOT NULL)
|   and (LEAST(list_gtd.date_clear + interval '3 hour', list_gtd.date_conditional_clear + interval '3 hour', list_gtd.date_release_for_procuring + interval '3 hour') 
|      BETWEEN Cast('" + ДатаВФорматеЗапросаPostgres(ЭтаФорма.НачальнаяДата) + "' as timestamp) + interval '3 hour' 
|             and Cast('" + ДатаВФорматеЗапросаPostgres(КонецДня(ЭтаФорма.КонечнаяДата)) + "' as timestamp) + interval '3 hour')
| order by list_gtd.id ";
ИначеЕсли ВариантЗаполнения = "ЗакладкаПереданныеДанные" Тогда // по данным полей закладки Переданные данные	
//-- в декларации на закладке грузовые единицы могут быть грузовые единицы из декларации у которых не указан номер груз ед из заказа
//-- для таких запиcей list_order.id_company_client (или id_company_principal) будет NULL
ТекстЗапроса = ТекстЗапроса + "			
| and list_gtd.id in ( " + 
  СтрокаIDДТ + " )
|order by list_gtd.id ";
	
КонецЕсли;	

Возврат ТекстЗапроса;
КонецФункции // ПолучитьТекстЗапроса_ЗаполнитьСписокДТ()

&НаКлиенте
Функция ПолучитьТекстЗапроса_ДатыЗаявленийНаВыпуск(СтрокаID_ЗаявленияНаВыпуск)
ТекстЗапроса = "	
|SELECT distinct
|   list_gtd.id as gtd_id, 
|   list_gtd.gtd_number,
|   COALESCE(list_gtd.gtd_date + interval '3 hour','1000-01-01'::timestamp) as gtd_date  -- в данном случае - дата заявления на выпуск
|FROM public.list_gtd list_gtd
|WHERE 
|   list_gtd.deleted = 0
|   and list_gtd.date_recall is NULL -- дата отзыва
|   and list_gtd.date_refuse is NULL -- дата отказа в выпуске 
|   and list_gtd.gtd_number is not null  
|   and list_gtd.id in ( " + 
  СтрокаID_ЗаявленияНаВыпуск + " ) ;";
Возврат ТекстЗапроса;  

КонецФункции // ПолучитьТекстЗапроса_ДатыЗаявленийНаВыпуск(СтрокаID_ЗаявленияНаВыпуск)

&НаКлиенте
Функция ПолучитьТекстЗапроса_СуммыИВалютаПоДТ(СтрокаID_ДТ)
Возврат "	
|SELECT LIST_FEACC_GTD.id_list_gtd, 
|   COALESCE(LIST_GTD.currency, '') as currency, -- валюта графа 22 (валюта поступления) 'EUR', 'USD', 'RUB'
|   COALESCE(SUM(LIST_FEACC_GTD.price_statistical), 0) as sum_price_statistical,  -- покупка товара в валюте
|   COALESCE(SUM(LIST_FEACC_GTD.payment_ex), 0) as sum_payment_ex, -- сумма всего платежей в рублях
|   -- payment_ex = payment100 + payment200 + payment300   (возможно еще и + other_payments)
|   COALESCE(SUM(LIST_FEACC_GTD.payment100), 0) as sum_payment100, -- таможенный сбор 1010
|   COALESCE(SUM(LIST_FEACC_GTD.payment200), 0) as sum_payment200, -- пошлина 2010
|   COALESCE(SUM(LIST_FEACC_GTD.payment300), 0) as sum_payment300, -- НДС 5010
|   COALESCE(SUM(LIST_FEACC_GTD.other_payments), 0) as sum_other_payments 
|FROM public.list_feacc_gtd 
|left join PUBLIC.LIST_GTD on LIST_FEACC_GTD.ID_LIST_GTD = LIST_GTD.ID
|where LIST_FEACC_GTD.id_list_gtd IN (" + СтрокаID_ДТ + ") and LIST_FEACC_GTD.deleted = 0
|  and (LIST_FEACC_GTD.feacc_status is null or LIST_FEACC_GTD.feacc_status <> 'Отказано в выпуске')
|GROUP BY LIST_FEACC_GTD.id_list_gtd, COALESCE(LIST_GTD.currency, '') 
| ";

// поля, которые могут пригодиться
//--COALESCE(LIST_GTD.sum_curs, 0) as sum_curs, -- курс валюты графа 23, курс взаиморасчетов
//-- LIST_FEACC_GTD.price_customs, 
//--LIST_FEACC_GTD.price,  -- покупка товара, должна совпадать с sum_price_statistical
//--SUM(LIST_FEACC_GTD.payment) as sum_payment, -- сумма всего платежей в рублях, должна совпадать с sum_payment_ex
//-- price_one_kg, price_one_unit, -- цена за 1 кг или 1 единицу
//-- LIST_FEACC_GTD.count_unit, LIST_FEACC_GTD.unit, -- кол-во и ед измерения
//--LIST_FEACC_GTD.price_customs_ex, -- сумма покупки (стоимость товаров) в рублях
//--LIST_FEACC_GTD.invoice_number, 
//--LIST_FEACC_GTD.contract_number,
КонецФункции	// ПолучитьТекстЗапроса_СуммыИВалютаПоДТ(СтрокаID_ДТ)

&НаКлиенте
Функция ПолучитьТекстЗапроса_ДокументыПоДТ(СтрокаID_ДТ)
Возврат "	
|SELECT distinct --id, id_list_feacc_gtd, -- id строки ДТ
|id_list_gtd, 
|id_list_type_feacc_documents, 
|feacc_document_name,  
|COALESCE(feacc_document_number, '') as feacc_document_number,  
|COALESCE(feacc_document_start,'1000-01-01'::date) as feacc_document_start 
|FROM public.list_feacc_documents
|where id_list_gtd in (" + СтрокаID_ДТ + ") and deleted = 0
| and ( 
|        ((id_list_type_feacc_documents = '09023') and (feacc_document_name = 'АГЕНТСКИЙ ДОГОВОР')) 
|     or 
|        ((id_list_type_feacc_documents = '04021') and (feacc_document_name = 'СЧЕТ-ФАКТУРА (ИНВОЙС) К ДОГОВОРУ')) 
|     ) ";
	
КонецФункции // ПолучитьТекстЗапроса_ДокументыПоДТ(СтрокаID_ДТ)	

&НаКлиенте
Функция ПолучитьТекстЗапроса_НаименованиеКонтрагентаНЛ()
Возврат "
|SELECT COALESCE(case
|	        when list_company_name IS NULL then list_company_name_small
|	        else list_company_name
|	        end,'') as contractor_name,
|       COALESCE(inn,'') as contractor_inn
|from public.list_company
|where id = " + ВернутьПравильнуюСтрокуID(ЭтаФорма.IDКлиентаЗаказа) + " ";

КонецФункции // ПолучитьТекстЗапроса_НаименованиеКонтрагентаНЛ()

&НаКлиенте
Функция ПолучитьТекстЗапроса_ДанныеСтрокДТ(СтрокаID_ДТ)
	Возврат "
|SELECT 
|  LIST_FEACC_GTD.id_list_gtd, 
|  COALESCE(list_feacc_gtd.feacc_count,0) AS FECC_COUNT, -- Номер строки в ДТ
|  COALESCE(LIST_FEACC_GTD.FEACC_NO, '') AS FEACC_NO, -- код товара графа 33 == код ТНВЭД
|  COALESCE(LIST_FEACC_GTD.FEACC_TEXT,'') AS FEACC_TEXT, -- наименование номенклатуры
|  COALESCE(LIST_FEACC_GTD.PRICE, 0) as PRICE,-- double, стоимость товара == графа 42  (комментарий G-Soft: это фактурная стоимость)
|  COALESCE(LIST_FEACC_GTD.ORIGIN_COUNTRY_CODE,'') AS ORIGINCOUNTRYCODE, -- два символа (н-р 'CN', 'KR'), похоже на КодАльфа2
|  COALESCE(list_feacc_gtd.price_one_unit, 0) as price_one_unit,-- цена за 1 единицу измерения
|  COALESCE(list_feacc_gtd.unit, '') as unit, -- код правильной единицы измерения, строка, н-р '796', может быть NULL,'000',''
|  COALESCE(LIST_FEACC_GTD.COUNT_UNIT, 0) as COUNT_UNIT, -- double, количество в правильных единицах измерения, а не в КГ
|  COALESCE(LIST_FEACC_GTD.net_weight, 0) as net_weight, -- double, вес нетто
|  COALESCE(LIST_FEACC_GTD.GOODS_MARKING, '') as GOODS_MARKING,   -- артикул из дополнения к декларации,
|  COALESCE(LIST_FEACC_GTD.price_customs_ex, 0) AS price_customs_ex,  -- double, Таможенная стоимость
|  COALESCE(LIST_FEACC_GTD.payment200, 0) AS payment200,  -- double, Сумма пошлины
|  COALESCE(LIST_FEACC_GTD.payment300, 0) AS payment300,  -- double, Сумма НДС
|  COALESCE(LIST_FEACC_GTD.payment100, 0) AS payment100   -- double, Таможенный сбор
|FROM public.list_feacc_gtd list_feacc_gtd
|where LIST_FEACC_GTD.id_list_gtd in (" + СтрокаID_ДТ + ")
|   and LIST_FEACC_GTD.deleted = 0
|   and (LIST_FEACC_GTD.feacc_status is null or LIST_FEACC_GTD.feacc_status <> 'Отказано в выпуске')
|order by LIST_FEACC_GTD.id_list_gtd, list_feacc_gtd.feacc_count; 
| ";
КонецФункции // ПолучитьТекстЗапроса_ДанныеСтрокДТ(СтрокаID_ДТ)	

&НаКлиенте
Функция ПолучитьТекстЗапроса_ГрузовыеЕдиницыПоНомеруДТ(Строка_Номеров_ДТ)
Возврат " with raw_dt_numbers(one_dt) as (values " + Символы.ПС + Строка_Номеров_ДТ	+ "),
| raw_dt_numbers_array(one_dt_array) as (select string_to_array(one_dt, ',') from raw_dt_numbers) 
|
|SELECT 
|  raw_dt_numbers_array.one_dt_array, -- массив из одной строки
|  unit.deleted, 
|  unit.id,
|  unit.gtd_numbers_arr,
|  COALESCE(unit.unit_number, '') as unit_number, 
|  COALESCE(unit.number_by_client, '') as number_by_client
|  FROM operation.unit
|  right join raw_dt_numbers_array on unit.gtd_numbers_arr @> raw_dt_numbers_array.one_dt_array ; ";
КонецФункции

&НаСервере
Функция НайтиКилограммНаСервере()
	Возврат Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду("166");
КонецФункции	

&НаКлиенте
Процедура ЗаполнитьСписокДТ(Команда)
	// нужен поиск поставщика по полному наименованию в группа контарагентов Поставщики....Марлин
	ОчиститьСообщения();
	Если (ЭтаФорма.НачальнаяДата = Дата(1,1,1)) ИЛИ (ЭтаФорма.КонечнаяДата = Дата(1,1,1)) ИЛИ
		(ЭтаФорма.НачальнаяДата > ЭтаФорма.КонечнаяДата) Тогда
		Сообщить("Проверьте даты");
		Возврат;
	КонецЕсли;	
	Если ЭтаФорма.IDКлиентаЗаказа = 0 Тогда
		Сообщить("Укажите #клиента заказа");
		Возврат;
	КонецЕсли;	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Организация) ИЛИ НЕ ЗначениеЗаполнено(ЭтаФорма.Контрагент1C) Тогда
		Сообщить("Укажите организацию и контрагента 1С");
		Возврат;
	КонецЕсли;	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.ГруппаПоискаПоставщика) Тогда
		Сообщить("Укажите группу контрагентов для поиска поставщика в 1С");
		Возврат;
	КонецЕсли;              
	
	КилограммСсылка = НайтиКилограммНаСервере();			
	Если НЕ ЗначениеЗаполнено(КилограммСсылка) Тогда
		Сообщить("В базе 1С не найдена единица измерения с кодом 166 (килограмм)");
		Возврат;
	КонецЕсли;
	Если НЕ ОтбиратьДТ И НЕ ОтбиратьЗаявления Тогда
		Сообщить("Не указано что отбирать: ДТ/Заявления");
		Возврат;		
	КонецЕсли;	
	
	ЗаполнитьСписокДТФактическоеВыполнение("ЗакладкаГлавное", КилограммСсылка);
	
КонецПроцедуры // ЗаполнитьСписокДТ(Команда)

&НаКлиенте
Функция УдалитьНежелательныеСимволыСтрокиPostgres(ИсходнаяСтрока)
// для кодовой страницы cp866 нужно чистить 160, 159, 9, 10, 13	
РезСтрока = СтрЗаменить(ИсходнаяСтрока, "'" ,"");
РезСтрока = СтрЗаменить(РезСтрока, Символ(34) ,"");
Возврат РезСтрока;
КонецФункции

&НаКлиенте
Процедура ЗаполнитьСписокДТФактическоеВыполнение(ВариантЗаполнения, КилограммСсылка, СтрокаIDДТ = Неопределено)
	// ВариантЗаполнения = "ЗакладкаГлавное" - по данным полей закладки Главное, СтрокаIDДТ = Неопределено
	// ВариантЗаполнения = "ЗакладкаПереданныеДанные" - по данным полей закладки Переданные данные, СтрокаIDДТ - строка	ID ДТ для запроса
	
	ЭтаФорма.ДТДляГрупповогоСоздания.Очистить();
	ЭтаФорма.НоменклатураДТ.Очистить();
	ЭтаФорма.НоменклатураДТОтображение.Очистить();
	ЭтаФорма.НомераГрузовыхЕдиницПоДТ.Очистить();
	ЭтаФорма.НаименованиеКонтрагентаНЛ = "";
	
	ConRSCom = ВернутьСтруктуруConnectionRecordSetCommand();  // Connection, RecordSet, Command;
	Если ConRSCom = Неопределено Тогда
		Возврат; // сообщения об ошибках уже выведены
	КонецЕсли;	
	
	Если ВариантЗаполнения = "ЗакладкаГлавное" Тогда
		// поиск контрагента по ID
		Попытка    
			ConRSCom.Command.CommandText = ПолучитьТекстЗапроса_НаименованиеКонтрагентаНЛ(); 
			ConRSCom.RecordSet = ConRSCom.Command.Execute();    //  Данные получены
		Исключение    
			Сообщить("Ошибка поиска контрагента #" + ВернутьПравильнуюСтрокуID(ЭтаФорма.IDКлиентаЗаказа) + " в базе Логос"); 
			Сообщить(ОписаниеОшибки());				
			ЗакрытьПодключениеКPostgres(ConRSCom);				
			Возврат;
		КонецПопытки;
	
		Пока НЕ ConRSCom.RecordSet.EOF Цикл // если контрагент найден по ID, то будет один оборот цикла
			ЭтаФорма.НаименованиеКонтрагентаНЛ = ConRSCom.RecordSet.fields("contractor_name").Value + 
				" ИНН " + ConRSCom.RecordSet.fields("contractor_inn").Value;		
			ConRSCom.RecordSet.MoveNext();		
		КонецЦикла;	
		ConRSCom.RecordSet.Close();    // RecordSet.State = 1 == Open	
		
		Если ПустаяСтрока(ЭтаФорма.НаименованиеКонтрагентаНЛ) Тогда
			Сообщить("Не найден контрагент #" + ВернутьПравильнуюСтрокуID(ЭтаФорма.IDКлиентаЗаказа) + " в базе Логос"); 
			ЗакрытьПодключениеКPostgres(ConRSCom);				
			Возврат;
		КонецЕсли;	
	КонецЕсли; // 	Если ВариантЗаполнения = "ЗакладкаГлавное" Тогда
		
	// получение списка ДТ
	Попытка    
		ConRSCom.Command.CommandText = ПолучитьТекстЗапроса_ЗаполнитьСписокДТ(ВариантЗаполнения, СтрокаIDДТ); 
		ConRSCom.RecordSet = ConRSCom.Command.Execute();    //  Данные получены
	Исключение    
		Сообщить("Ошибка получения списка ДТ из базы Логос"); 
		Сообщить(ОписаниеОшибки());				
		ЗакрытьПодключениеКPostgres(ConRSCom);				
		Возврат;
	КонецПопытки;
	
	Дата111 = Дата(1,1,1);
	Дата100011 = Дата(1000,1,1); 
	СтрокаWithToSearch = "";
	КолвоToSearch = 0;
	
	СтрокаID_ДТ = "";
	СтрокаID_ЗаявленияНаВыпуск = "";	
	КолвоЗаявленийНаВыпуск = 0;
	Строка_Номеров_ДТ = "";
	КолвоДТ = 0;            
	
	Пока НЕ ConRSCom.RecordSet.EOF Цикл 
		НачальныйНомерДТ = СокрЛП(ConRSCom.RecordSet.fields("gtd_number").Value);
		// возможно не нужны декларации, выпущенные по заявлениям, русское и англ 'В'		
		ЭтоДТ = Ложь;
		ЭтоЗаявление = Ложь;
		Если (СтрНайти(НачальныйНомерДТ, "В") = 0) И (СтрНайти(НачальныйНомерДТ, "B") = 0) Тогда
			ЭтоДТ = Истина;			
		Иначе	            
			ЭтоЗаявление = Истина;		
		КонецЕсли;	
		Если (ЭтоДТ И ОтбиратьДТ) ИЛИ (ЭтоЗаявление И ОтбиратьЗаявления) Тогда		
			Если ЭтоЗаявление Тогда
				КолвоToSearch = КолвоToSearch + 1;
				СтрокаWithToSearch = СтрокаWithToSearch + ", ('%" + УдалитьНежелательныеСимволыСтрокиPostgres(НачальныйНомерДТ) + "%')";
				Если КолвоToSearch / 3 = Цел(КолвоToSearch / 3 ) Тогда
					СтрокаWithToSearch = СтрокаWithToSearch + Символы.ПС;
				КонецЕсли;	
			КонецЕсли;	
			
			СтрокаДТ = ЭтаФорма.ДТДляГрупповогоСоздания.Добавить();
			СтрокаДТ.IDДТ  = ConRSCom.RecordSet.fields("gtd_id").Value;
			СтрокаID_ДТ = СтрокаID_ДТ + "," + ВернутьПравильнуюСтрокуID(СтрокаДТ.IDДТ);
			Строка_Номеров_ДТ = Строка_Номеров_ДТ + ", ('" + НачальныйНомерДТ + "')";
			КолвоДТ = КолвоДТ + 1;		
			Если КолвоДТ/7 = Цел(КолвоДТ/7) Тогда
				СтрокаID_ДТ = СтрокаID_ДТ + Символы.ПС;
				Строка_Номеров_ДТ = Строка_Номеров_ДТ + Символы.ПС ;
			КонецЕсли;	
			СтрокаДТ.НомерДТ = НачальныйНомерДТ;
			
			СтрокаДТ.НомераЗВ = ConRSCom.RecordSet.fields("number_decl_clear_from_feaccs").Value;  // text, номера ЗВ к Дт через запятую
			Если ConRSCom.RecordSet.fields("cardinality_ids_number_decl_clear_from_feaccs").Value = 1 Тогда			
				// id деклараций-заявлений, []BigInt, после ODBC приходят как строка, пустой массив - как строка {}				
				ПромСтрока = ConRSCom.RecordSet.fields("ids_number_decl_clear_from_feaccs").Value;
				ПромСтрока = СтрЗаменить(СтрЗаменить(ПромСтрока, "{", ""), "}", "");
				Попытка
					СтрокаДТ.IDЗВ = Число(ПромСтрока);					
					СтрокаID_ЗаявленияНаВыпуск = СтрокаID_ЗаявленияНаВыпуск + ", " + ВернутьПравильнуюСтрокуID(СтрокаДТ.IDЗВ);	
					КолвоЗаявленийНаВыпуск = КолвоЗаявленийНаВыпуск + 1;
					Если КолвоЗаявленийНаВыпуск / 7 = Цел(КолвоЗаявленийНаВыпуск / 7) Тогда
						СтрокаID_ЗаявленияНаВыпуск = СтрокаID_ЗаявленияНаВыпуск + Символы.ПС;		
					КонецЕсли;	
				Исключение
					// не буду ничего сообщать					
				КонецПопытки;	
				// СтрокаДТ.ДатаЗВ будет заполнена ниже по результатам еще одного запроса
			КонецЕсли; // 	Если ConRSCom.RecordSet.fields("cardinality_ids_number_decl_clear_from_feaccs").Value = 1 Тогда			

			СтрокаДТ.ДатаВыпуска = ConRSCom.RecordSet.fields("date_clear").Value;
			Если НачалоДня(СтрокаДТ.ДатаВыпуска) = Дата100011 Тогда
				СтрокаДТ.ДатаВыпуска = Дата111;
			КонецЕсли;	
			СтрокаДТ.ДатаУсловногоВыпуска = ConRSCom.RecordSet.fields("date_conditional_clear").Value;
			Если НачалоДня(СтрокаДТ.ДатаУсловногоВыпуска) = Дата100011 Тогда
				СтрокаДТ.ДатаУсловногоВыпуска = Дата111;
			КонецЕсли;	
			СтрокаДТ.ДатаВыпускаПодОбеспечение = ConRSCom.RecordSet.fields("date_release_for_procuring").Value;
			Если НачалоДня(СтрокаДТ.ДатаВыпускаПодОбеспечение) = Дата100011 Тогда
				СтрокаДТ.ДатаВыпускаПодОбеспечение = Дата111;
			КонецЕсли;	
			СтрокаДТ.IDЗаказа = ConRSCom.RecordSet.fields("order_id").Value; 
			СтрокаДТ.НомерЗаказа = ConRSCom.RecordSet.fields("sys_serial_number").Value;
			
			СтрокаДТ.ДатаЗаказа = НачалоДня(ConRSCom.RecordSet.fields("order_date").Value); // NULL=='1000-01-01', дата заказа, 
			Если СтрокаДТ.ДатаЗаказа = Дата100011 Тогда
				СтрокаДТ.ДатаЗаказа = Дата111;			
			КонецЕсли;	
			
			СтрокаДТ.IDКонтрактодержателя = ConRSCom.RecordSet.fields("id_list_client").Value;
			СтрокаДТ.КурсВалюты = ConRSCom.RecordSet.fields("sum_curs").Value; // курс валюты графа 23, курс взаиморасчетов
			СтрокаДТ.НаименованиеПоставщика = ConRSCom.RecordSet.fields("seller_name").Value; // наименование контрагента для докум	ПТУ	
			
			СтрокаДТ.НомерДоговораПоставщика = ConRSCom.RecordSet.fields("contract_number").Value; 
			СтрокаДТ.ДатаДоговораПоставщика = НачалоДня(ConRSCom.RecordSet.fields("contract_date").Value); 
			Если СтрокаДТ.ДатаДоговораПоставщика = Дата100011 Тогда
				СтрокаДТ.ДатаДоговораПоставщика = Дата111;			
			КонецЕсли;	
			
			СтрокаДТ.РеференсЗаказа = ConRSCom.RecordSet.fields("number_by_client").Value;			
			
			Попытка
				// например НачальныйНомерДТ = "10317120/040322/3035158";
				ДеньДД = Число(Сред(НачальныйНомерДТ, 10, 2));
				МесяцДД = Число(Сред(НачальныйНомерДТ, 12, 2));
				ГодДД = 2000 + Число(Сред(НачальныйНомерДТ, 14, 2));
				СтрокаДТ.ДатаДокументов	= Дата(ГодДД, МесяцДД, ДеньДД);
			Исключение
				СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Дата ДТ;";
			КонецПопытки;	
			
			// если референс заказа имеет вид УТК ХХХ ..., то выделить ХХХ
			СтрокаДТ.НомерОтчетаАгента = ВернутьПравильнуюСтрокуID(СтрокаДТ.IDЗаказа);
			ТехНомер = СтрЗаменить(ВРег(СтрокаДТ.РеференсЗаказа), "  ", " ");
			ТехНомер = СтрЗаменить(СтрЗаменить(СтрЗаменить(ТехНомер, "Y", "У"), "T", "Т"), "K", "К");
			СтрокаЦифр = "0123456789";
			Если Лев(ТехНомер, 4) = "УТК " Тогда
				ТехНомер = Сред(ТехНомер, 5);
				НакопленныйНомер = "";
				Для Поз = 1 по СтрДлина(ТехНомер) Цикл
					СледСимвол = Сред(ТехНомер, Поз, 1);
					Если СледСимвол = " " Тогда
						Продолжить;
					ИначеЕсли СтрНайти(СтрокаЦифр, СледСимвол) > 0 Тогда
						НакопленныйНомер = НакопленныйНомер + СледСимвол;
					Иначе
						Прервать;
					КонецЕсли;	
				КонецЦикла;	
				Если НЕ ПустаяСтрока(НакопленныйНомер) Тогда
					СтрокаДТ.НомерОтчетаАгента = НакопленныйНомер;
				КонецЕсли;	
			КонецЕсли;			
		КонецЕсли; // (СтрНайти(НачальныйНомерДТ, "В") = 0) И (СтрНайти(НачальныйНомерДТ, "B") = 0) Тогда
		ConRSCom.RecordSet.MoveNext();
	КонецЦикла; 
	ConRSCom.RecordSet.Close();    // RecordSet.State = 1 == Open
	
	Если НЕ ПустаяСтрока(СтрокаID_ЗаявленияНаВыпуск) Тогда // получение дат заявлений на выпуск
		Попытка    
			ConRSCom.Command.CommandText = ПолучитьТекстЗапроса_ДатыЗаявленийНаВыпуск(Сред(СтрокаID_ЗаявленияНаВыпуск, 2)); 
			ConRSCom.RecordSet = ConRSCom.Command.Execute();    //  Данные получены
		Исключение    
			Сообщить("Ошибка получения дат заявлений на выпуск из базы Логос"); 
			Сообщить(ОписаниеОшибки());				
			ЗакрытьПодключениеКPostgres(ConRSCom);				
			Возврат;
		КонецПопытки;
		
		Пока НЕ ConRSCom.RecordSet.EOF Цикл 		
			IDДТ  = ConRSCom.RecordSet.fields("gtd_id").Value;
			gtd_date = ConRSCom.RecordSet.fields("gtd_date").Value;
			Если НачалоДня(gtd_date) = Дата100011 Тогда
				gtd_date = Дата111;
			КонецЕсли;	
			Если gtd_date <> Дата111 Тогда
				СтруктураПоиска = Новый Структура("IDЗВ", IDДТ);
				НайденныеСтрокиЗаявлений = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураПоиска);
				Для каждого НС_Заявления из НайденныеСтрокиЗаявлений Цикл
					НС_Заявления.ДатаЗВ	= gtd_date;
				КонецЦикла;	
			КонецЕсли; 
			ConRSCom.RecordSet.MoveNext();
		КонецЦикла; 
		ConRSCom.RecordSet.Close();    // RecordSet.State = 1 == Open
	КонецЕсли;	// 	Если НЕ ПустаяСтрока(СтрокаID_ЗаявленияНаВыпуск) Тогда // получение дат заявлений на выпуск
	
	// ДОБАВЛЕНО 12.05.2023
	// к сожалению, не особо качественные данные Логос. Есть ДТ 
	// COALESCE(LIST_GTD.number_decl_clear_from_feaccs, '') as number_decl_clear_from_feaccs НЕ ПУСТО
    // COALESCE(LIST_GTD.ids_number_decl_clear_from_feaccs, ARRAY[]::bigint[]) ПУСТО, хотя заявления есть в базе
	// например для ДТ #162343 и 162342
	// Для тех строк ДТДляГрупповогоСоздания у которых заполнено поле НомераЗВ и не заполнено поле ДатаЗВ выделить 
	// дату заявления из его номера
	Для каждого	СтрокаДТ из ЭтаФорма.ДТДляГрупповогоСоздания Цикл
		Если НЕ ПустаяСтрока(СтрокаДТ.НомераЗВ) И (СтрокаДТ.ДатаЗВ = Дата111) Тогда
			СтрокаДТ.ДатаЗВ = ВернутьДатуПоСтрокеИзСреднейЧастиНомераДТ(Сред(СтрокаДТ.НомераЗВ, 10, 6));
		КонецЕсли;	
	КонецЦикла;	
	
	Если КолвоToSearch > 0 Тогда // выполнить поиск номеров и ID ДТ, соответствующих исходным номерам заявлений 
		// начальный вид СтрокаWithToSearch примерно такой
      	//,('%10228010/280124/В003868%') ,('%10228010/240124/В003421%') ,('%10228010/210124/В003057%')
		// в поле number_decl_clear_from_feacc может быть указано через запятую несколько номеров заявлений
		СтрокаWithToSearch = "with tosearch (pattern) as (values " + Символы.ПС + Сред(СтрокаWithToSearch, 2) + "
|     )
|SELECT list_gtd.id, Coalesce(list_gtd.gtd_number, '') as gtd_number, list_gtd.number_decl_clear_from_feaccs, tosearch.pattern
|  FROM public.list_gtd
|INNER JOIN tosearch on list_gtd.number_decl_clear_from_feaccs like tosearch.pattern
|where list_gtd.deleted = 0  ";
	
		Попытка    
			ConRSCom.Command.CommandText = СтрокаWithToSearch; 
			ConRSCom.RecordSet = ConRSCom.Command.Execute();    //  Данные получены
		Исключение    
			Сообщить("Ошибка получения из базы Логос номеров и #ДТ, соответствующих исходным номерам заявлений "); 
			Сообщить(ОписаниеОшибки());				
			ЗакрытьПодключениеКPostgres(ConRSCom);				
			Возврат;
		КонецПопытки;
		
		Пока НЕ ConRSCom.RecordSet.EOF Цикл 		
			IDДТДополнит  = ConRSCom.RecordSet.fields("id").Value;
			НомДТДополнит = СокрЛП(ConRSCom.RecordSet.fields("gtd_number").Value);
			НомЗаявления = СтрЗаменить(СокрЛП(ConRSCom.RecordSet.fields("pattern").Value), "%", "");
			СтруктураПоиска = Новый Структура("НомерДТ", НомЗаявления);
			НайденныеСтрокиЗаявлений = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураПоиска);
			Для каждого НС_Заявления из НайденныеСтрокиЗаявлений Цикл
				НС_Заявления.IDДТпоЗВ = IDДТДополнит;
				НС_Заявления.ДТпоЗВ = НомДТДополнит;				
			КонецЦикла;	
			ConRSCom.RecordSet.MoveNext();
		КонецЦикла; 
		ConRSCom.RecordSet.Close();    // RecordSet.State = 1 == Open
				
	КонецЕсли; // Если КолвоToSearch > 0 Тогда // выполнить поиск номеров и ID ДТ, соответствующих исходям номерам заявлений
	
	СтрокаID_ДТ = Сред(СтрокаID_ДТ, 2);	
	Строка_Номеров_ДТ = Сред(Строка_Номеров_ДТ, 2);
	
	Если КолвоДТ > 0 Тогда
		// получение строк ДТ
		Попытка    
			ConRSCom.Command.CommandText = ПолучитьТекстЗапроса_ДанныеСтрокДТ(СтрокаID_ДТ); 
			ConRSCom.RecordSet = ConRSCom.Command.Execute();    //  Данные получены
		Исключение    
			Сообщить("Ошибка получения данных строк ДТ из базы Логос"); 
			Сообщить(ОписаниеОшибки());				
			ЗакрытьПодключениеКPostgres(ConRSCom);				
			Возврат;
		КонецПопытки;
		
		Пока НЕ ConRSCom.RecordSet.EOF Цикл 
			НомДТ = ЭтаФорма.НоменклатураДТ.Добавить();

			НомДТ.IDДТ = ConRSCom.RecordSet.fields("id_list_gtd").Value; 
			НомДТ.ПорядковыйНомер = ConRSCom.RecordSet.fields("FECC_COUNT").Value; // Номер строки в ДТ
			НомДТ.Наименование = ConRSCom.RecordSet.fields("FEACC_TEXT").Value; // наименование номенклатуры 
			
			// ДОБАВЛЕНО 12.05.2023			
			Если ВРег(Лев(НомДТ.Наименование, 3)) = "ВДТ" Тогда
				НомДТ.Наименование = СокрЛП(Сред(НомДТ.Наименование, 4));
			КонецЕсли;	
			Если ВРег(Лев(НомДТ.Наименование, 5)) = "(ВДТ)" Тогда
				НомДТ.Наименование = СокрЛП(Сред(НомДТ.Наименование, 6));
			КонецЕсли;	
			
			НомДТ.Количество = ConRSCom.RecordSet.fields("COUNT_UNIT").Value; // double, количество в правильных единицах измерения
			НомДТ.Цена = ConRSCom.RecordSet.fields("price_one_unit").Value; // цена за 1 единицу измерения
			НомДТ.Сумма = ConRSCom.RecordSet.fields("PRICE").Value; // double, стоимость товара == графа 42  (комментарий G-Soft: это фактурная стоимость)
			НомДТ.Артикул = ConRSCom.RecordSet.fields("GOODS_MARKING").Value; // артикул из дополнения к декларации
			НомДТ.КодТНВЭД = ConRSCom.RecordSet.fields("FEACC_NO").Value; // код товара графа 33 == код ТНВЭД   Справочники.КлассификатовТНВЭД 
			НомДТ.КодСтраны = ConRSCom.RecordSet.fields("ORIGINCOUNTRYCODE").Value; // два символа (н-р 'CN', 'KR'), похоже на КодАльфа2	
			НомДТ.КодЕдИзм = ConRSCom.RecordSet.fields("unit").Value; // код правильной единицы измерения, строка, н-р '796', может быть NULL,'000',''
			НомДТ.ТаможеннаяСтоимость = ConRSCom.RecordSet.fields("price_customs_ex").Value;
			НомДТ.Пошлина = ConRSCom.RecordSet.fields("payment200").Value;
			НомДТ.СуммаНДС = ConRSCom.RecordSet.fields("payment300").Value;
			НомДТ.ТаможенныйСбор = ConRSCom.RecordSet.fields("payment100").Value;
			НомДТ.ВесНетто = ConRSCom.RecordSet.fields("net_weight").Value;
			//Если НЕ ЗначениеЗаполнено(НомДТ.ЕдИзм) И (НомДТ.Количество = 0) И (НомДТ.ВесНетто > 0) И ЭтаФорма.ЗаменаКолваЕдИзмерения Тогда
			Если (НомДТ.Количество = 0) И (НомДТ.ВесНетто > 0) И ЭтаФорма.ЗаменаКолваЕдИзмерения Тогда				
				НомДТ.Количество = НомДТ.ВесНетто;
				НомДТ.ЕдИзм = КилограммСсылка;
			КонецЕсли;	
			ConRSCom.RecordSet.MoveNext();
		КонецЦикла; 
		ConRSCom.RecordSet.Close();    // RecordSet.State = 1 == Open		
		
		// получение сумм по строкам ДТ на основе строки ID ДТ
		ОшибкаПоискаПоID_ДТ = Ложь;		
		Попытка    
			ConRSCom.Command.CommandText = ПолучитьТекстЗапроса_СуммыИВалютаПоДТ(СтрокаID_ДТ); 
			ConRSCom.RecordSet = ConRSCom.Command.Execute();    //  Данные получены
		Исключение    
			Сообщить("Ошибка получения списка сумм по ДТ из базы Логос"); 
			Сообщить(ОписаниеОшибки());				
			ЗакрытьПодключениеКPostgres(ConRSCom);				
			Возврат;
		КонецПопытки;
	
		Пока НЕ ConRSCom.RecordSet.EOF Цикл 
			IDДТ = ConRSCom.RecordSet.fields("id_list_gtd").Value; 
			СтруктураПоискаIDДТ = Новый Структура("IDДТ", IDДТ);
			НайденныеСтрокиГС = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураПоискаIDДТ);
			Если НайденныеСтрокиГС.Количество() = 1 Тогда
				СтрокаДТ = НайденныеСтрокиГС[0];
				СтрокаДТ.ВалютаПокупки = ConRSCom.RecordSet.fields("currency").Value; //  валюта графа 22 (валюта поступления) 'EUR', 'USD', 'RUB'
				СтрокаДТ.СуммаПокупки = ConRSCom.RecordSet.fields("sum_price_statistical").Value; //   покупка товара в валюте
				СтрокаДТ.ВсегоПлатежей = ConRSCom.RecordSet.fields("sum_payment_ex").Value; //   сумма всего платежей в рублях
				//  -- payment_ex = payment100 + payment200 + payment300   (возможно еще и + other_payments)
				СтрокаДТ.Сумма1010 = ConRSCom.RecordSet.fields("sum_payment100").Value; // таможенный сбор 1010
				СтрокаДТ.Сумма2010 = ConRSCom.RecordSet.fields("sum_payment200").Value; // пошлина 2010
				СтрокаДТ.Сумма5010 = ConRSCom.RecordSet.fields("sum_payment300").Value; // НДС 5010
				СтрокаДТ.ПрочПлатежи = ConRSCom.RecordSet.fields("sum_other_payments").Value; 
				Если СтрокаДТ.ВсегоПлатежей <> СтрокаДТ.Сумма1010 + СтрокаДТ.Сумма2010 + СтрокаДТ.Сумма5010 Тогда
					СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Всего тамож.платежей<>1010+2010+5010; ";
				КонецЕсли;	
			Иначе
				ОшибкаПоискаПоID_ДТ = Истина;
			КонецЕсли;	
			
			ConRSCom.RecordSet.MoveNext();
		КонецЦикла; 
		ConRSCom.RecordSet.Close();    // RecordSet.State = 1 == Open
		
		// поиск договора и инвойса в данных документов ДТ
		Попытка    
			ConRSCom.Command.CommandText = ПолучитьТекстЗапроса_ДокументыПоДТ(СтрокаID_ДТ); 
			ConRSCom.RecordSet = ConRSCom.Command.Execute();    //  Данные получены
		Исключение    
			Сообщить("Ошибка поиска договоров/инвойсов в документах к ДТ в базе Логос"); 
			Сообщить(ОписаниеОшибки());				
			ЗакрытьПодключениеКPostgres(ConRSCom);				
			Возврат;
		КонецПопытки;

		Пока НЕ ConRSCom.RecordSet.EOF Цикл 
			IDДТ = ConRSCom.RecordSet.fields("id_list_gtd").Value; 
			СтруктураПоискаIDДТ = Новый Структура("IDДТ", IDДТ);
			НайденныеСтрокиГС = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураПоискаIDДТ);
			Если НайденныеСтрокиГС.Количество() = 1 Тогда
				СтрокаДТ = НайденныеСтрокиГС[0];
				id_list_type_feacc_documents = ConRSCom.RecordSet.fields("id_list_type_feacc_documents").Value; // 
				feacc_document_name = ConRSCom.RecordSet.fields("feacc_document_name").Value; //  
				feacc_document_number = ConRSCom.RecordSet.fields("feacc_document_number").Value; //  
				feacc_document_start = ConRSCom.RecordSet.fields("feacc_document_start").Value; // 
				Если НачалоДня(feacc_document_start) = Дата100011 Тогда
					feacc_document_start = Дата111;
				КонецЕсли;	
				Если (id_list_type_feacc_documents = "09023") И (feacc_document_name = "АГЕНТСКИЙ ДОГОВОР") Тогда
					Если НЕ ПустаяСтрока(СтрокаДТ.НомерДоговора) И (СтрНайти(СтрокаДТ.Примечания,"Несколько договоров; ")=0) Тогда
						СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Несколько договоров; ";
					КонецЕсли;	
					СтрокаДТ.НомерДоговора = feacc_document_number; 
					СтрокаДТ.ДатаДоговора = feacc_document_start;
				КонецЕсли;	
				Если (id_list_type_feacc_documents = "04021") И (feacc_document_name = "СЧЕТ-ФАКТУРА (ИНВОЙС) К ДОГОВОРУ") Тогда
					// может быть несколько инвойсов, это нормально
					СтрокаДТ.СтрокаИнвойсов = СтрокаДТ.СтрокаИнвойсов + feacc_document_number + " от " + 
						Формат(feacc_document_start, "ДФ=dd.MM.yyyy") + "; ";
				КонецЕсли;	
			Иначе
				ОшибкаПоискаПоID_ДТ = Истина;
			КонецЕсли;	
			ConRSCom.RecordSet.MoveNext();
		КонецЦикла; 
		ConRSCom.RecordSet.Close();    // RecordSet.State = 1 == Open	
		
		// поиск # грузовых единиц по номерам ДТ
		// может быть несколько грузовых единиц в одной ДТ
		Попытка    
			ConRSCom.Command.CommandText = ПолучитьТекстЗапроса_ГрузовыеЕдиницыПоНомеруДТ(Строка_Номеров_ДТ); 
			ConRSCom.RecordSet = ConRSCom.Command.Execute();    //  Данные получены
		Исключение    
			Сообщить("Ошибка поиска грузовых единиц к ДТ в базе Логос"); 
			Сообщить(ОписаниеОшибки());				
			ЗакрытьПодключениеКPostgres(ConRSCom);				
			Возврат;
		КонецПопытки;		
		
		Пока НЕ ConRSCom.RecordSet.EOF Цикл 
			// массив приходит через ODBC как строка вида "{10317120/220122/В000503,10317120/230122/3011553}"
			one_dt_array = ConRSCom.RecordSet.fields("one_dt_array").Value; // массив из одной (искомой) строки
			one_dt_array = СтрЗаменить(one_dt_array, "{", "");
			one_dt_array = СтрЗаменить(one_dt_array, "}", "");			
			
			//deleted = ConRSCom.RecordSet.fields("deleted").Value; 
			uint_id = ConRSCom.RecordSet.fields("id").Value;
			unit_number = ConRSCom.RecordSet.fields("unit_number").Value; 
			number_by_client = ConRSCom.RecordSet.fields("number_by_client").Value;	
			
			СтрокаНГЕ = ЭтаФорма.НомераГрузовыхЕдиницПоДТ.Добавить();	
			СтрокаНГЕ.НомерДТ = one_dt_array;
			СтрокаНГЕ.IDГрузовойЕдиницы = uint_id;
			СтрокаНГЕ.РеференсГрузовойЕдиницы = number_by_client;
			СтрокаНГЕ.НомерГрузовойЕдиницы = unit_number;
			
			//gtd_numbers_arr = ConRSCom.RecordSet.fields("gtd_numbers_arr").Value; // массив возможно из нескольких строк
			
			СтруктураПоискаНомерДТ = Новый Структура("НомерДТ", one_dt_array);
			НайденныеСтрокиГС = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураПоискаНомерДТ);		
			Для каждого НСГС из НайденныеСтрокиГС Цикл
				Если ПустаяСтрока(НСГС.СтрокаIDГрузовыхЕдиниц) Тогда
					НСГС.СтрокаIDГрузовыхЕдиниц = ВернутьПравильнуюСтрокуID(uint_id);
				Иначе
					НСГС.СтрокаIDГрузовыхЕдиниц = НСГС.СтрокаIDГрузовыхЕдиниц + "," + ВернутьПравильнуюСтрокуID(uint_id);					
				КонецЕсли;	
			КонецЦикла;
			ConRSCom.RecordSet.MoveNext();
		КонецЦикла; 
		ConRSCom.RecordSet.Close();    // RecordSet.State = 1 == Open	

		Если ОшибкаПоискаПоID_ДТ Тогда
			Сообщить("Внутренняя ошибка поиска. Продолжение работы в обработке НЕ РЕКОМЕНДУЕТСЯ.");
		КонецЕсли;
	КонецЕсли; // Если КолвоДТ > 0 Тогда
	ЗакрытьПодключениеКPostgres(ConRSCom);
	
	ПоискДоговоровИСтранИВалютИЕдИзмНаСервере();
	ПоискПоставщиковИДоговоровПоставщиков();
	ПоискДокументовОтчетАгентаНаСервере();
	
	Если ЭтаФорма.ЗаменаКолваЕдИзмерения Тогда
		Для каждого НомДТ из ЭтаФорма.НоменклатураДТ Цикл
			Если НЕ ЗначениеЗаполнено(НомДТ.ЕдИзм) И (НомДТ.Количество = 0) И (НомДТ.ВесНетто > 0) Тогда
				НомДТ.Количество = НомДТ.ВесНетто;
				НомДТ.ЕдИзм = КилограммСсылка;
			КонецЕсли;	
		КонецЦикла; 
	КонецЕсли;
	
	// есть еще один вариант проверок - см. ДополнительнаяПроверка(Команда)
	// доп проверки, все ли заполнено в таблице значений ЭтаФорма.ДТДляГрупповогоСоздания
	Для каждого СтрокаДТ из ЭтаФорма.ДТДляГрупповогоСоздания Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаДТ.Договор) Тогда
			Если ЗначениеЗаполнено(ЭтаФорма.ДоговорКонтрагент1СПоУмолчанию) Тогда
				СтрокаДТ.Договор = ЭтаФорма.ДоговорКонтрагент1СПоУмолчанию;
			Иначе	
				СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Договор?; ";
			КонецЕсли;	
		КонецЕсли;	
		Если НЕ ЗначениеЗаполнено(СтрокаДТ.Валюта) Тогда
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Валюта?; ";
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаДТ.Поставщик) Тогда
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Поставщик?; ";
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(СтрокаДТ.ДоговорПоставщика) Тогда
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Договор поставщика?; ";
		КонецЕсли; 
		Если ЗначениеЗаполнено(СтрокаДТ.НомерДТ) Тогда  // добавлено 23.01.2023
			СтруктураПоиска = Новый Структура("НомерДТ", СтрокаДТ.НомерДТ);
			НС_ПереданныеНомерДТОперация = Объект.ПереданныеНомерДТОперация.НайтиСтроки(СтруктураПоиска);
			Для каждого НС_ПД из НС_ПереданныеНомерДТОперация Цикл
				СтрокаДТ.Операция = НС_ПД.Операция; 
			КонецЦикла;	
		КонецЕсли;	
	КонецЦикла;	
	
	// доп проверки, все ли заполнено в таблице значений ЭтаФорма.НоменклатураДТ 
	Для Каждого СтрокаНом из ЭтаФорма.НоменклатураДТ Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаНом.Страна) Тогда
			СтруктураНеЗаполнено = Новый Структура("IDДТ", СтрокаНом.IDДТ);
			НСНеЗаполнено = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураНеЗаполнено);
			Если СтрНайти(НСНеЗаполнено[0].Примечания, "Страна в строках ДТ;") = 0 Тогда
				НСНеЗаполнено[0].Примечания = НСНеЗаполнено[0].Примечания + "Страна в строках ДТ; "; 
			КонецЕсли;
		КонецЕсли;	
		Если НЕ ЗначениеЗаполнено(СтрокаНом.ЕдИзм) Тогда
			СтруктураНеЗаполнено = Новый Структура("IDДТ", СтрокаНом.IDДТ);
			НСНеЗаполнено = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураНеЗаполнено);
			Если СтрНайти(НСНеЗаполнено[0].Примечания, "Ед.изм. в строках ДТ;") = 0 Тогда
				НСНеЗаполнено[0].Примечания = НСНеЗаполнено[0].Примечания + "Ед.изм. в строках ДТ; "; 
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;	
	
	ЭтаФорма.ОбновитьОтображениеДанных();	
КонецПроцедуры  // ЗаполнитьСписокДТ(Команда)

&НаКлиенте
Функция ВернутьДатуПоСтрокеИзСреднейЧастиНомераДТ(СтрДаты)
	Попытка                                                               
	    День = Число(Лев(СтрДаты, 2));
		Месяц = Число((Сред(СтрДаты,3,2)));
		Год = 2000 + Число(Прав(СтрДаты, 2));
		Возврат Дата(Год, Месяц, День);
	Исключение
		Сообщить("Невозможно преобразовать строку '" + СтрДаты + "' из средней части номера заявления в дату.");				
		Возврат Дата(1,1,1);			
	КонецПопытки;	
КонецФункции	

&НаСервере
Процедура ПоискДокументовОтчетАгентаНаСервере()
	// заполнить по номеру ДТ поля ДТДляГрупповогоСоздания.ОтчетАгента (Строка) и ДТДляГрупповогоСоздания.ОтчетАгентаGUID (Строка)
	МассивДТ = Новый Массив;
	Для каждого СтрокаДТ из ЭтаФорма.ДТДляГрупповогоСоздания Цикл
		МассивДТ.Добавить(СтрокаДТ.НомерДТ);
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтчетыАгента_ОтчетАгента.IDДТ КАК IDДТ,
	|	ОтчетыАгента_ОтчетАгента.НомерДТ КАК НомерДТ,
	|	ОтчетыАгента_ОтчетАгента.Ссылка КАК Ссылка,
	|	ОтчетыАгента_ОтчетАгента.Номер КАК Номер,
	|	ОтчетыАгента_ОтчетАгента.Дата КАК Дата,
	|	ОтчетыАгента_ОтчетАгента.НомерОтчетаАгента КАК НомерОтчетаАгента,
	|	ОтчетыАгента_ОтчетАгента.ДатаОтчетаАгента КАК ДатаОтчетаАгента
	|ИЗ
	|	Документ.ОтчетыАгента_ОтчетАгента КАК ОтчетыАгента_ОтчетАгента
	|ГДЕ
	|	НЕ ОтчетыАгента_ОтчетАгента.ПометкаУдаления
	|	И ОтчетыАгента_ОтчетАгента.НомерДТ В (&МассивДТ) ";	
	// возможно нужно добавить условия 
	// ОтчетыАгента_ОтчетАгента.Организация = &Организация И ОтчетыАгента_ОтчетАгента.Контрагент = &Контрагент
	
	Запрос.УстановитьПараметр("МассивДТ", МассивДТ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураПоискаНомерДТ = Новый Структура("НомерДТ", Выборка.НомерДТ);		
		СтрокиНайденныеПоНомерДТ = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураПоискаНомерДТ);
		Для каждого СНПНДТ из СтрокиНайденныеПоНомерДТ Цикл
			// теоретически может быть несколько отчетов агента по одной ДТ
			СНПНДТ.ОтчетАгента = СНПНДТ.ОтчетАгента + "№ " + СокрЛП(Выборка.НомерОтчетаАгента) + ", " +
				Формат(Выборка.ДатаОтчетаАгента, "ДФ=dd.MM.yyyy") + " (№ " +
				Строка(Выборка.Номер) + ", " + Формат(Выборка.Дата, "ДФ=dd.MM.yyyy") + ");";
			СНПНДТ.ОтчетАгентаGUID = Выборка.Ссылка.УникальныйИдентификатор();		
		КонецЦикла;	
	КонецЦикла;
КонецПроцедуры	// ПоискДокументовОтчетАгентаНаСервере()

&НаСервере
Процедура ПоискПоставщиковИДоговоровПоставщиков()
	// поиск контрагентов-поставщиков
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	НЕ Контрагенты.ПометкаУдаления
		|	И НЕ Контрагенты.ЭтоГруппа		
		|	И Контрагенты.Ссылка В ИЕРАРХИИ(&РодительскаяГруппа)
		|	И Контрагенты.Наименование ПОДОБНО &Наименование";
	Запрос.УстановитьПараметр("РодительскаяГруппа", ЭтаФорма.ГруппаПоискаПоставщика);	
	
	// например	
	//ENTUG NAKLIYAT INSAAT TURIZM AMBALAJLAMA PLASTIK SANAYI TEMIZLIK VE YEMEK HIZMETLERI TICARET LIMITED  в 1c	
	//ENTUG NAKLIYAT INSAAT TURIZM AMBALAJLAMA PLASTIK SANAYI TEMIZLIK VE YE	пришло из НЛ
	
	//AS STAR TARIM URUN.NAK.VE TIC.LTD.STI.       в 1С
	//AS STAR TARIM URUNLERI NAKLIYAT VE TIC. LTD. STI. из НЛ	из НЛ не будет найден в 1С	
		
	ТехТЗ = "";
	ТехТЗ = ЭтаФорма.ДТДляГрупповогоСоздания.Выгрузить(,"НаименованиеПоставщика");
	ТехТЗ.Свернуть("НаименованиеПоставщика");
	Для каждого СтрокаТЗ из ТехТЗ Цикл
		Если ПустаяСтрока(СтрокаТЗ.НаименованиеПоставщика) Тогда
			Продолжить;
		КонецЕсли;	
		Запрос.УстановитьПараметр("Наименование", "%" + СтрокаТЗ.НаименованиеПоставщика + "%");	
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() = 1 Тогда
			Выборка.Следующий();	
			ПотенциальныйКонтрагент = Выборка.Ссылка;
			СтруктураПоискаГрупповоеСоздание = Новый Структура("НаименованиеПоставщика", СтрокаТЗ.НаименованиеПоставщика);
			НайденныеСтрокиДТ = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураПоискаГрупповоеСоздание);
			Для каждого НСДТ из НайденныеСтрокиДТ Цикл
				НСДТ.Поставщик = ПотенциальныйКонтрагент;
			КонецЦикла;	
		КонецЕсли;
	КонецЦикла;	
	
	// поиск договоров контрагентов-поставщиков (там где уже найден поставщик) 
	Запрос = "";
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	НЕ ДоговорыКонтрагентов.ПометкаУдаления
		|	И ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
		|	И ДоговорыКонтрагентов.Организация = &Организация
		|	И ДоговорыКонтрагентов.Дата = &Дата
		|	И ДоговорыКонтрагентов.Номер = &Номер
		|	И ДоговорыКонтрагентов.Владелец = &Владелец ";
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	Запрос.УстановитьПараметр("Организация", ЭтаФорма.Организация);
	
	ТехТЗ = "";
	ТехТЗ = ЭтаФорма.ДТДляГрупповогоСоздания.Выгрузить(,"Поставщик,НомерДоговораПоставщика,ДатаДоговораПоставщика");
	ТехТЗ.Свернуть("Поставщик,НомерДоговораПоставщика,ДатаДоговораПоставщика");	
	Для каждого СтрокаТЗ из ТехТЗ Цикл	
		Если НЕ ЗначениеЗаполнено(СтрокаТЗ.Поставщик) ИЛИ ПустаяСтрока(СтрокаТЗ.НомерДоговораПоставщика) ИЛИ 
			(СтрокаТЗ.ДатаДоговораПоставщика = Дата(1,1,1)) Тогда
			Продолжить;
		КонецЕсли;	
		Запрос.УстановитьПараметр("Владелец", СтрокаТЗ.Поставщик);	
		Запрос.УстановитьПараметр("Дата", СтрокаТЗ.ДатаДоговораПоставщика);
		Запрос.УстановитьПараметр("Номер", СтрокаТЗ.НомерДоговораПоставщика);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() = 1 Тогда
			Выборка.Следующий();
			ДогПоставщика = Выборка.Ссылка;
			СтруктураПоискаГрупповоеСоздание = Новый Структура("Поставщик,НомерДоговораПоставщика,ДатаДоговораПоставщика", 
				СтрокаТЗ.Поставщик, СтрокаТЗ.НомерДоговораПоставщика, СтрокаТЗ.ДатаДоговораПоставщика);
			НайденныеСтрокиДТ = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураПоискаГрупповоеСоздание);
			Для каждого НСДТ из НайденныеСтрокиДТ Цикл
				НСДТ.ДоговорПоставщика = ДогПоставщика;
			КонецЦикла;	
		КонецЕсли;
	КонецЦикла;		
	
	// если выше не найден поставщик, то попытка поиска поставщика и договора по номеру и дате договора
	Запрос = "";
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
		|	ДоговорыКонтрагентов.Владелец КАК Владелец		
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	НЕ ДоговорыКонтрагентов.ПометкаУдаления
		|	И ДоговорыКонтрагентов.Организация = &Организация
		|	И ДоговорыКонтрагентов.Владелец В ИЕРАРХИИ(&РодительскаяГруппа)		
		|	И ДоговорыКонтрагентов.Дата = &Дата
		|	И ДоговорыКонтрагентов.Номер = &Номер
		|	И ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора";
	
	Запрос.УстановитьПараметр("РодительскаяГруппа", ЭтаФорма.ГруппаПоискаПоставщика);		
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	Запрос.УстановитьПараметр("Организация", ЭтаФорма.Организация);
	
	ТехТЗ = "";
	ТехТЗ = ЭтаФорма.ДТДляГрупповогоСоздания.Выгрузить(,"Поставщик,НомерДоговораПоставщика,ДатаДоговораПоставщика,ДоговорПоставщика");
	ТехТЗ.Свернуть("Поставщик,НомерДоговораПоставщика,ДатаДоговораПоставщика,ДоговорПоставщика");	
	Для каждого СтрокаТЗ из ТехТЗ Цикл	
		Если ЗначениеЗаполнено(СтрокаТЗ.Поставщик) ИЛИ ПустаяСтрока(СтрокаТЗ.НомерДоговораПоставщика) ИЛИ 
			(СтрокаТЗ.ДатаДоговораПоставщика = Дата(1,1,1)) ИЛИ ЗначениеЗаполнено(СтрокаТЗ.ДоговорПоставщика) Тогда
			Продолжить;
		КонецЕсли;	
		Запрос.УстановитьПараметр("Дата", СтрокаТЗ.ДатаДоговораПоставщика);
		Запрос.УстановитьПараметр("Номер", СтрокаТЗ.НомерДоговораПоставщика);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() = 1 Тогда
			Выборка.Следующий();
			ПоставщикСпр = Выборка.Владелец;
			ДогПоставщика = Выборка.Ссылка;
			// в данной точке СтрокаТЗ.Поставщик и СтрокаТЗ.ДоговорПоставщика пусты
			СтруктураПоискаГрупповоеСоздание = Новый Структура("Поставщик,НомерДоговораПоставщика,ДатаДоговораПоставщика,ДоговорПоставщика", 
				СтрокаТЗ.Поставщик, СтрокаТЗ.НомерДоговораПоставщика, СтрокаТЗ.ДатаДоговораПоставщика, СтрокаТЗ.ДоговорПоставщика);
			НайденныеСтрокиДТ = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураПоискаГрупповоеСоздание);
			Для каждого НСДТ из НайденныеСтрокиДТ Цикл
				НСДТ.Поставщик = ПоставщикСпр;
				НСДТ.ДоговорПоставщика = ДогПоставщика;
				НСДТ.ПроверитьПоставщикаИЕгоДоговор = Истина;
				// потенциально таким способом может быть найден совершенно посторонний поставщик и договор
			КонецЦикла;	
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры  // ПоискПоставщиковИДоговоровПоставщиков()

&НаСервере
Процедура ПоискДоговоровИСтранИВалютИЕдИзмНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,		
		|	ДоговорыКонтрагентов.Наименование КАК Наименование,
		|	ДоговорыКонтрагентов.Дата КАК Дата,
		|	ДоговорыКонтрагентов.Номер КАК Номер
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Организация = &Организация
		|	И ДоговорыКонтрагентов.Владелец = &Владелец
		|	И ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора";
	
	Запрос.УстановитьПараметр("Организация", ЭтаФорма.Организация);
	Запрос.УстановитьПараметр("Владелец", ЭтаФорма.Контрагент1C);	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СКомитентомНаЗакупку);		
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл  // не будет много таких договоров в 1С
		СтруктураПоискаГрупповоеСоздание = Новый Структура("НомерДоговора, ДатаДоговора", Выборка.Номер, Выборка.Дата);
		НайденныеСтрокиГрупповоеСоздание = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураПоискаГрупповоеСоздание);
		Для каждого НСГрупповоеСоздание из НайденныеСтрокиГрупповоеСоздание Цикл
			НСГрупповоеСоздание.Договор = Выборка.Ссылка;
		КонецЦикла;	
	КонецЦикла;
	
	ТехТЗ = ЭтаФорма.ДТДляГрупповогоСоздания.Выгрузить(,"ВалютаПокупки");
	ТехТЗ.Свернуть("ВалютаПокупки");
	Для каждого СтрокаТЗ из ТехТЗ Цикл
		ВалютаПокупкиСпр = НайтиВалюту(СтрокаТЗ.ВалютаПокупки);  
		Если ЗначениеЗаполнено(ВалютаПокупкиСпр) Тогда
			СтруктураПоискаГрупповоеСоздание = Новый Структура("ВалютаПокупки", СтрокаТЗ.ВалютаПокупки);
			НайденныеСтрокиГрупповоеСоздание = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиСтроки(СтруктураПоискаГрупповоеСоздание);
			Для каждого НСГрупповоеСоздание из НайденныеСтрокиГрупповоеСоздание Цикл
				НСГрупповоеСоздание.Валюта = ВалютаПокупкиСпр;
			КонецЦикла;	
		КонецЕсли;	
	КонецЦикла;	
	
	ТехТЗ = "";
	ТехТЗ = ЭтаФорма.НоменклатураДТ.Выгрузить(,"КодСтраны");
	ТехТЗ.Свернуть("КодСтраны");
	Для каждого СтрокаТЗ из ТехТЗ Цикл
		СтранаСпр = НайтиСтрану(СтрокаТЗ.КодСтраны);  
		Если ЗначениеЗаполнено(СтранаСпр) Тогда
			СтруктураПоискаГрупповоеСоздание = Новый Структура("КодСтраны", СтрокаТЗ.КодСтраны);
			НайденныеСтрокиНомДТ = ЭтаФорма.НоменклатураДТ.НайтиСтроки(СтруктураПоискаГрупповоеСоздание);
			Для каждого НСНомДТ из НайденныеСтрокиНомДТ Цикл
				НСНомДТ.Страна = СтранаСпр;
			КонецЦикла;	
		КонецЕсли;	
	КонецЦикла;	
	
	ТехТЗ = "";
	ТехТЗ = ЭтаФорма.НоменклатураДТ.Выгрузить(,"КодЕдИзм");
	ТехТЗ.Свернуть("КодЕдИзм");
	Для каждого СтрокаТЗ из ТехТЗ Цикл
		ЕдИзмСпр = Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду(СтрокаТЗ.КодЕдИзм);  
		Если ЗначениеЗаполнено(ЕдИзмСпр) Тогда
			СтруктураПоискаГрупповоеСоздание = Новый Структура("КодЕдИзм", СтрокаТЗ.КодЕдИзм);
			НайденныеСтрокиНомДТ = ЭтаФорма.НоменклатураДТ.НайтиСтроки(СтруктураПоискаГрупповоеСоздание);
			Для каждого НСНомДТ из НайденныеСтрокиНомДТ Цикл
				НСНомДТ.ЕдИзм = ЕдИзмСпр;
			КонецЦикла;	
		КонецЕсли;	
	КонецЦикла;	
		
КонецПроцедуры  //	ПоискДоговоровИСтранИВалютИЕдИзмНаСервере()

&НаСервереБезКонтекста
Функция НайтиВалюту(ИмяВалюты)  
	ПотенциальнаяВалюта = Справочники.Валюты.ПустаяСсылка();
	Если (ВРег(ИмяВалюты) = "RUB") ИЛИ (ВРег(ИмяВалюты) = "RUR") Тогда
		ПотенциальнаяВалюта = Справочники.Валюты.НайтиПоКоду("643");
	ИначеЕсли (ВРег(ИмяВалюты) = "EUR") ИЛИ (ВРег(ИмяВалюты) = "EURO") Тогда
		ПотенциальнаяВалюта = Справочники.Валюты.НайтиПоКоду("987");		
	ИначеЕсли ВРег(ИмяВалюты) = "USD" Тогда
		ПотенциальнаяВалюта = Справочники.Валюты.НайтиПоКоду("840");
	КонецЕсли;	
	Если ЗначениеЗаполнено(ПотенциальнаяВалюта) Тогда
		Возврат ПотенциальнаяВалюта;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", ИмяВалюты);
	Запрос.Текст=
	"ВЫБРАТЬ
	|	Валюты.Ссылка
	|ИЗ
	|	Справочник.Валюты КАК Валюты
	|ГДЕ
	|	Валюты.Наименование = &Наименование";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	Возврат ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");
КонецФункции  // Функция НайтиВалюту(ИмяВалюты)

&НаСервереБезКонтекста
Функция НайтиСтрану(КодСтраны)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодАльфа2", КодСтраны);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтраныМира.Ссылка
	|ИЗ
	|	Справочник.СтраныМира КАК СтраныМира
	|ГДЕ
	|	СтраныМира.КодАльфа2 = &КодАльфа2";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	Возврат Справочники.СтраныМира.ПустаяСсылка(); 
КонецФункции  // Функция НайтиСтрану(КодСтраны)

#КонецОбласти // ПолучениеСпискаДТизНЛ

#Область СозданиеПакетовДокументов
&НаСервере
Функция ВернутьДатуДокумента(Док)
	Возврат Док.Дата;
КонецФункции	

&НаКлиенте
Процедура СоздатьПакетыДокументов(Команда)
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.КонтрагентБанковскийСчет) Тогда
		Сообщить("Не указан банковский счет контрагента.");
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.СубСчет002) Тогда
		Если Объект.КонтролироватьСубсчет002 Тогда
			Сообщить("Укажите субсчет 002.");
		Иначе
			Сообщить("Укажите счет 002.");			
		КонецЕсли;	
		Возврат;
	КонецЕсли;
		
	КолвоДляСоздания = 0;	
	КолвоДляПовторногоСоздания = 0;		
	НеУказанНомерОтчетаАгента = Ложь;
	IDДТДляКоторыхНевозможноПрименитьПравилоДатДокументов = "";
	Дата111 = Дата(1,1,1);
	Для каждого СтрокаДТ из ЭтаФорма.ДТДляГрупповогоСоздания Цикл
		Если НЕ СтрокаДТ.Пометка Тогда
			Продолжить;			
		КонецЕсли;
		Если ПустаяСтрока(СтрокаДТ.НомерОтчетаАгента) Тогда
			НеУказанНомерОтчетаАгента = Истина;
		КонецЕсли;
		Если ОсобыеДатыДокументов Тогда
			// ОтчетКомитенту и ОтчетАгента - с датой ОперацияБух, даты ПТУ и ПередачаТоваров по заявлению на выпуск
			
			// если НЕ установлен признак ОсобыеДатыДокументов, то документы будут созданы со следующими датами 
			// где СтрокаДТ.ДатаДокументов выделена из средней части номера ДТ
			//ДатаПТУ = СтрокаДТ.ДатаДокументов + 9 * 60 * 60;	// 09:00			
			//ДатаОКОП = СтрокаДТ.ДатаДокументов + 9 * 60 * 60 + 5 * 60;	// 09:05
			//ДатаПередачаТоваров = СтрокаДТ.ДатаДокументов + 9 * 60 * 60 + 10 * 60;	// 09:10			
			//ДатаОтчетАгента = СтрокаДТ.ДатаДокументов + 10 * 60 * 60; // время документа = 10.00
			//ДатаОперация = КонецДня(СтрокаДТ.ДатаДокументов);
			// СтрокаДТ.ДатаДокументов и СтрокаДТ.ДатаЗВ - дата без времени

			ДатаПТУ = СтрокаДТ.ДатаДокументов; // + 9 * 60 * 60;	// 09:00			
			ДатаОКОП = СтрокаДТ.ДатаДокументов; //  + 9 * 60 * 60 + 5 * 60;	// 09:05
			
			Если ЗначениеЗаполнено(СтрокаДТ.Операция) Тогда
				ДатаОКОП = НачалоДня(ВернутьДатуДокумента(СтрокаДТ.Операция));
			КонецЕсли;	
			Если СтрокаДТ.ДатаЗВ <> Дата111 Тогда
				ДатаПТУ = СтрокаДТ.ДатаЗВ;
			КонецЕсли;
			Если ДатаПТУ > ДатаОКОП Тогда
				IDДТДляКоторыхНевозможноПрименитьПравилоДатДокументов = IDДТДляКоторыхНевозможноПрименитьПравилоДатДокументов + ", #" +
					ВернутьПравильнуюСтрокуID(СтрокаДТ.IDДТ);
			КонецЕсли;	
		КонецЕсли;	// 		Если ОсобыеДатыДокументов Тогда
		КолвоДляСоздания = КолвоДляСоздания + 1;			
		Если СтрокаДТ.Создан ИЛИ НЕ ПустаяСтрока(СтрокаДТ.ОтчетАгента) Тогда
			КолвоДляПовторногоСоздания = КолвоДляПовторногоСоздания + 1;							
		КонецЕсли;
	КонецЦикла;     
	Если НЕ ПустаяСтрока(IDДТДляКоторыхНевозможноПрименитьПравилоДатДокументов) Тогда
		Сообщить("Правило 'ОтчетКомитенту и ОтчетАгента - с датой ОперацияБух, даты ПТУ и ПередачаТоваров по заявлению на выпуск' не может быть");
		Сообщить("использовано для #ДТ " + Сред(IDДТДляКоторыхНевозможноПрименитьПравилоДатДокументов,2));
		Сообщить("Пакеты документов НЕ созданы. Снимите пометку с указанных строк ДТ или признак особых дат документов.");		
		Возврат;
	КонецЕсли;	
	Если КолвоДляСоздания = 0 Тогда
		Сообщить("Не отмечены ДТ для создания документов.");
		Возврат;
	КонецЕсли;	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.Склад) Тогда
		Сообщить("Не указан склад.");
		Возврат;
	КонецЕсли;	
	Если НеУказанНомерОтчетаАгента Тогда
		Сообщить("Не во всех отмеченных строках указан номер отчета агента");
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Этаформа.КонтрагентТаможенныеПлатежи) ИЛИ
		НЕ ЗначениеЗаполнено(ЭтаФорма.ДоговорТаможенныеПлатежи) Тогда
		Сообщить("Должны быть указаны контрагент и договор для таможенных платежей (для движений документа ОперацияБух)");
		Возврат;
	КонецЕсли;	
	
	Если ЭтаФорма.ВтчКомиссионноеВознаграждение Тогда
		Если (ЭтаФорма.КомиссионноеВознагражениеСумма = 0) ИЛИ НЕ ЗначениеЗаполнено(ЭтаФорма.УслугаПоВознаграждению) ИЛИ
			НЕ ЗначениеЗаполнено(ЭтаФорма.СчетУчетаДоходов) ИЛИ НЕ ЗначениеЗаполнено(ЭтаФорма.СчетУчетаНДС) ИЛИ 
			НЕ ЗначениеЗаполнено(ЭтаФорма.СтавкаНДСВознагражения) Тогда
			Сообщить("Заполните все поля для комисс. вознаграждения");
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	
	СтрокаВопроса = "Создать пакеты документов для отмеченных ДТ (" + Строка(КолвоДляСоздания) + ") ?";
	Если КолвоДляПовторногоСоздания > 0 Тогда
		СтрокаВопроса = СтрокаВопроса  + Символы.ПС + 
			"Будут ПОВТОРНО созданы пакеты документов для " + Строка(КолвоДляПовторногоСоздания) + " ДТ.";
	КонецЕсли;	
    Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаСоздатьПакетыДокументов",
      ЭтотОбъект);	
 
    ПоказатьВопрос(Оповещение,
        СтрокаВопроса,
        РежимДиалогаВопрос.ДаНетОтмена,
        0, // таймаут в секундах
        КодВозвратаДиалога.Нет, // (необ.) кнопка по умолчанию
        "Неоходимо подтверждение" // (необ.) заголовок
    );    
КонецПроцедуры //  СоздатьОтчетыАгента(Команда)
 
&НаКлиенте
Процедура ПослеЗакрытияВопросаСоздатьПакетыДокументов(Результат, Параметры) Экспорт
     Если Результат <> КодВозвратаДиалога.Да Тогда
        Сообщить("Пакеты документов не созданы");
		Возврат;
	КонецЕсли;
	Сообщить("Создание пакетов документов....");	
	Если НЕ СоздатьПакетыДокументовНаСервере() Тогда
		Сообщить("Создание пакетов документов прервано.");
	Иначе	
		// Сообщить("Создание пакетов документов успешно завершено.");		
		Сообщить("Создание печатных форм ОтчетовАгента....");							
		Если СозданиеИЗаписьВПрисоединенныеФайлыОтчетовАгента()	Тогда
			// Создание печатных форм ОтчетовАгента успешно завершено 
			Сообщить("Обработка завершена.");					
		Иначе	
			Сообщить("Создание печатных форм ОтчетовАгента завершено с ошибками.");		
		КонецЕсли;
	КонецЕсли; 
КонецПроцедуры  // ПослеЗакрытияВопросаСоздатьПакетыДокументов(Результат, Параметры) Экспорт

&НаСервере
Функция СоздатьПакетыДокументовНаСервере()
	// поля документов по релизу 3.0.106.101
	Валюта_Рубль = Константы.ВалютаРегламентированногоУчета.Получить();
	Если НЕ ЗначениеЗаполнено(Валюта_Рубль) Тогда
		Сообщить("Не найдена валюта: Рубль");
		Возврат Ложь;		
	КонецЕсли;	
	
	ВидНоменклатуры_Товары = НайтиВидНоменклатуры_Товары();
	Если НЕ ЗначениеЗаполнено(ВидНоменклатуры_Товары) Тогда
		Сообщить("Не найден ВидНоменклатуры = 'Товары'");		
		Возврат Ложь;				
	КонецЕсли;	
	
	СтавкиНДС_БезНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
	//СтавкиНДС_НДС20	= ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20");	
	//СтавкиНДС_НДС18	= ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18");
	//СтавкиНДС_НДС10	= ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10");	
	//СтавкиНДС_НДС0	= ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0");		
	//ДопустимаяПогрешностьНДС = 0.09;  // эмпирически, аналогично применяемой при загрузке ДТ
	
	ТипЦенНоменклатуры_ОсновнаяЦенаПокупки = НайтиТипыЦенНоменклатуры_ОсновнаяЦенаПокупки();

	// Счет_002 = ПланыСчетов.Хозрасчетный.НайтиПоКоду("002");	
	Счет_002 = Объект.СубСчет002;	
	Счет_19_03 = ПланыСчетов.Хозрасчетный.НайтиПоКоду("19.03");
	Счет_19_05 = ПланыСчетов.Хозрасчетный.НайтиПоКоду("19.05");
	Счет_41_01 = ПланыСчетов.Хозрасчетный.НайтиПоКоду("41.01");
	Счет_76_05 = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.05");			
	Счет_76_06 = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.06");		
	Счет_76_09 = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.09");	
	Счет_62_01 = ПланыСчетов.Хозрасчетный.НайтиПоКоду("62.01");		
	Счет_62_02 = ПланыСчетов.Хозрасчетный.НайтиПоКоду("62.02");			
	
	СпособыЗачетаАвансов_Автоматически = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.Автоматически");
	СпособыЗачетаАвансов_НеЗачитывать = ПредопределенноеЗначение("Перечисление.СпособыЗачетаАвансов.НеЗачитывать");	
	
	СчетУчетаРасчетовСКонтрагентом = Неопределено;
	СчетУчетаРасчетовПоАвансам = Неопределено; 
	
	Дата111 = Дата(1,1,1);
	
	СозданоОтчетовАгента = 0;
	Для каждого СтрокаДТ из ЭтаФорма.ДТДляГрупповогоСоздания Цикл  // перебор ЭтаФорма.ДТДляГрупповогоСоздания для создания документов
		Если НЕ СтрокаДТ.Пометка Тогда
			Продолжить;			
		КонецЕсли;
		СтрокаДТ.Примечания	= ""; // предыдущая попытка могла закончится ошибкой	
		// КонтрактодержательЭтоСармант = Ложь, то создавать только ОперацияБух
		// т.е. НЕ БУДУТ ЗАПИСАНЫ ПоступлениеТоваровУслуг, ОтчетКомитентуОПродажахЮ ПередачаТоваров, ОтчетАгента 		
		КонтрактодержательЭтоСармант = Ложь; 
		Если СтрокаДТ.IDКонтрактодержателя = 3295 Тогда
			КонтрактодержательЭтоСармант = Истина; // создавать полный комплект документов
		КонецЕсли;	
		
		// СтрокаДТ.НомерДТ	нужно добавлять в номенклатуру	
		СтрокаДТ.Ошибка = Ложь;		
		Попытка
			CсылкаНаНомерДТ = НайтиСоздатьДТ(СтрокаДТ.НомерДТ);
			Если НЕ ЗначениеЗаполнено(CсылкаНаНомерДТ) Тогда
				СтрокаДТ.Ошибка = Истина;
				СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Ошибка поиcка/создания элемента в справочнике ДТ (" + СтрокаДТ.НомерДТ + "). ";
				Возврат Ложь;	
			КонецЕсли;	
			
			// поиск/создание группы номенклатуры с наименованием СтрокаДТ.НомерДТ
			СсылкаГруппаНоменклатуры = Справочники.Номенклатура.ПустаяСсылка();
			СсылкаГруппаНоменклатуры = НайтиСоздатьГруппуНоменклатурыДТ(СтрокаДТ.НомерДТ, Ложь);
			Если НЕ ЗначениеЗаполнено(СсылкаГруппаНоменклатуры) Тогда
				СтрокаДТ.Ошибка = Истина;
				СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Ошибка создания группы номенклатуры (" + СтрокаДТ.НомерДТ + "). ";
				Возврат Ложь;
			КонецЕсли;                                         
			
			// если НЕ установлен признак ОсобыеДатыДокументов, то документы будут созданы со следующими датами 
			// где СтрокаДТ.ДатаДокументов выделена из средней части номера ДТ
			ДатаПТУ = СтрокаДТ.ДатаДокументов + 9 * 60 * 60;	// 09:00			
			ДатаОКОП = СтрокаДТ.ДатаДокументов + 9 * 60 * 60 + 5 * 60;	// 09:05
			ДатаПередачаТоваров = СтрокаДТ.ДатаДокументов + 9 * 60 * 60 + 10 * 60;	// 09:10			
			ДатаОперация = КонецДня(СтрокаДТ.ДатаДокументов);			
			ДатаОтчетАгента = СтрокаДТ.ДатаДокументов + 10 * 60 * 60; // время документа = 10.00
			
			// подготовить резервные даты если заполнена ДатаДокЕслиПериодЗакрыт
			//РезервнаяДатаПТУ = Дата111;				
			//РезервнаяДатаОКОП = Дата111;	
			//РезервнаяДатаПередачаТоваров = Дата111;	
			//РезервнаяДатаОперация = Дата111;			
			//РезервнаяДатаОтчетАгента = Дата111; 
			
			РезервнаяДатаПТУ = ДатаДокЕслиПериодЗакрыт + 9 * 60 * 60;	// 09:00			
			РезервнаяДатаОКОП = ДатаДокЕслиПериодЗакрыт + 9 * 60 * 60 + 5 * 60;	// 09:05
			РезервнаяДатаПередачаТоваров = ДатаДокЕслиПериодЗакрыт + 9 * 60 * 60 + 10 * 60;	// 09:10			
			РезервнаяДатаОперация = КонецДня(ДатаДокЕслиПериодЗакрыт);			
			РезервнаяДатаОтчетАгента = ДатаДокЕслиПериодЗакрыт + 10 * 60 * 60; // время документа = 10.00

			Если ОсобыеДатыДокументов Тогда
				// ОтчетКомитенту и ОтчетАгента - с датой ОперацияБух, даты ПТУ и ПередачаТоваров по заявлению на выпуск
				// СтрокаДТ.ДатаДокументов и СтрокаДТ.ДатаЗВ - дата без времени

			//	ДатаПТУ = СтрокаДТ.ДатаДокументов; // + 9 * 60 * 60;	// 09:00			
			//	ДатаОКОП = СтрокаДТ.ДатаДокументов; //  + 9 * 60 * 60 + 5 * 60;	// 09:05
			//	
				Если ЗначениеЗаполнено(СтрокаДТ.Операция) Тогда                                                     
					НачДатаОКОП = НачалоДня(ВернутьДатуДокумента(СтрокаДТ.Операция));	// 09:05				
					ДатаОКОП = НачДатаОКОП + 9 * 60 * 60 + 5 * 60;	// 09:05
					ДатаОперация = КонецДня(НачДатаОКОП);
					ДатаОтчетАгента = НачДатаОКОП + 10 * 60 * 60; // время документа = 10.00
				КонецЕсли;	
				Если СтрокаДТ.ДатаЗВ <> Дата111 Тогда
					ДатаПТУ = СтрокаДТ.ДатаЗВ + 9 * 60 * 60;	// 09:00
					ДатаПередачаТоваров = СтрокаДТ.ДатаЗВ + 9 * 60 * 60 + 10 * 60;	// 09:10					
				КонецЕсли;                   
				// выше проверено, что НачалоДня(ДатаПТУ) <= НачалоДня(ДатаОКОП)
				// даже если НачалоДня(ВернутьДатуДокумента(СтрокаДТ.Операция)) == СтрокаДТ.ДатаЗВ, то будет соблюден порядок документов внутри дня
			КонецЕсли;	// Если ОсобыеДатыДокументов Тогда			
			

			//** Создание ПоступлениеТоваровУслуг **************************************************************************************
			ПоступлениеТоваровУслуг = Документы.ПоступлениеТоваровУслуг.СоздатьДокумент();
			ПоступлениеТоваровУслуг.ВидОперации	= ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия");
			ПоступлениеТоваровУслуг.НомерВходящегоДокумента	= СтрокаДТ.НомерДТ;
			Если (СтрокаДТ.ДатаЗВ <> Дата(1,1,1)) И НЕ ПустаяСтрока(СтрокаДТ.НомераЗВ) Тогда
				// просьба от 25/01/2023 - если есть заявление, то писать его в качестве номера ПТУ
				ПоступлениеТоваровУслуг.НомерВходящегоДокумента	= СтрокаДТ.НомераЗВ;
			КонецЕсли;	
			//ДатаДляДокументов = СтрокаДТ.ДатаДокументов + 9 * 60 * 60;	// 09:00
			ДатаДляДокументов = ДатаПТУ;
			ПоступлениеТоваровУслуг.ДатаВходящегоДокумента = ДатаДляДокументов; 
			ПоступлениеТоваровУслуг.Дата = ДатаДляДокументов; 
			ПоступлениеТоваровУслуг.НДСВключенВСтоимость = Истина;
			ПоступлениеТоваровУслуг.СуммаВключаетНДС = Истина;
			СтрокаКомментарияДокументов = "Отчет № " + СтрокаДТ.НомерОтчетаАгента + ", " + 
				СокрЛП(ЭтаФорма.Контрагент1C.Наименование) + ", гр.ед." + СтрокаДТ.СтрокаIDГрузовыхЕдиниц +
				", заказ " + ВернутьПравильнуюСтрокуID(СтрокаДТ.IDЗаказа) + 
				", ДТ " + СтрокаДТ.НомерДТ;
			ПоступлениеТоваровУслуг.Комментарий	= СтрокаКомментарияДокументов;
			ПоступлениеТоваровУслуг.Организация = ЭтаФорма.Организация;
			ПоступлениеТоваровУслуг.Контрагент = СтрокаДТ.Поставщик;
			ПоступлениеТоваровУслуг.ДоговорКонтрагента = СтрокаДТ.ДоговорПоставщика;
			ПоступлениеТоваровУслуг.ТипЦен = ТипЦенНоменклатуры_ОсновнаяЦенаПокупки;
			ПоступлениеТоваровУслуг.ВалютаДокумента	= СтрокаДТ.Валюта;
			ПоступлениеТоваровУслуг.КурсВзаиморасчетов = СтрокаДТ.КурсВалюты;
			ПоступлениеТоваровУслуг.КратностьВзаиморасчетов	= 1;
			
			Если СтрокаДТ.Валюта = Валюта_Рубль Тогда
				СчетУчетаРасчетовСКонтрагентом		= ПланыСчетов.Хозрасчетный.НайтиПоКоду("60.01");
				СчетУчетаРасчетовПоАвансам			= ПланыСчетов.Хозрасчетный.НайтиПоКоду("60.02");
			Иначе
				СчетУчетаРасчетовСКонтрагентом		= ПланыСчетов.Хозрасчетный.НайтиПоКоду("60.21");
				СчетУчетаРасчетовПоАвансам			= ПланыСчетов.Хозрасчетный.НайтиПоКоду("60.22");
			КонецЕсли;

			ПоступлениеТоваровУслуг.СчетУчетаРасчетовСКонтрагентом	= СчетУчетаРасчетовСКонтрагентом;
			ПоступлениеТоваровУслуг.СчетУчетаРасчетовПоАвансам		= СчетУчетаРасчетовПоАвансам;
			ПоступлениеТоваровУслуг.Склад = ЭтаФорма.Склад;
			//ПоступлениеТоваровУслуг.СпособЗачетаАвансов	= СпособыЗачетаАвансов_Автоматически;
			ПоступлениеТоваровУслуг.СпособЗачетаАвансов	= СпособыЗачетаАвансов_НеЗачитывать;
	
			ПоступлениеТоваровУслуг.Ответственный = Пользователи.ТекущийПользователь();
			
			СтруктураПоискаНоменклатураДТ = Новый Структура("IDДТ", СтрокаДТ.IDДТ);	
			
			Если НЕ НайтиСоздатьНоменклатуру(СтруктураПоискаНоменклатураДТ, CсылкаНаНомерДТ,
				СсылкаГруппаНоменклатуры, ВидНоменклатуры_Товары, СтрокаДТ.НомераЗВ, СтрокаДТ.ДатаЗВ) Тогда
				// не удалось создать номенклатуру хотя бы для одной строки, сообщения уже выведены
				Возврат Ложь;				
			КонецЕсли;	
					
			СтрокиТекущейДТ = ЭтаФорма.НоменклатураДТ.НайтиСтроки(СтруктураПоискаНоменклатураДТ);
			
			Для каждого НомДТ из СтрокиТекущейДТ Цикл  // создание строк ПоступлениеТоваровУслуг
				НоваяСтрока = ПоступлениеТоваровУслуг.Товары.Добавить();
				НоваяСтрока.Номенклатура = НомДТ.Номенклатура;
				НоваяСтрока.СтранаПроисхождения	= НомДТ.Страна;
				НоваяСтрока.НомерГТД			= CсылкаНаНомерДТ;
				// номер ГТД - из номенклатуры, может быть обычным, может быть дополненным справа "/"+номер строки ГТД
				// НоваяСтрока.НомерГТД			= НоваяСтрока.Номенклатура.НомерГТД;			
				НоваяСтрока.Количество			= НомДТ.Количество;	
				НоваяСтрока.Сумма				= НомДТ.Сумма;  // стоимость товара == цена товара графа 42	
				Если НоваяСтрока.Количество = 0 Тогда
					НоваяСтрока.Цена			= 0;					
				Иначе	
					НоваяСтрока.Цена			= Окр(НоваяСтрока.Сумма / НоваяСтрока.Количество, 2);		
				КонецЕсли;	
				Если НоваяСтрока.Цена = 0.00 Тогда
					Сообщить("Для ДТ " + СтрокаДТ.НомерДТ + ": проверьте количества, цены, суммы");
					НоваяСтрока.Цена = 0.01;
				КонецЕсли;	
				НоваяСтрока.СтавкаНДС = СтавкиНДС_БезНДС;
				НоваяСтрока.СчетУчета = Счет_002;
				НоваяСтрока.СчетУчетаНДС		= Счет_19_03;
				НоваяСтрока.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
				НоваяСтрока.Контрагент = ЭтаФорма.Контрагент1C;
				НоваяСтрока.ДоговорКонтрагента = СтрокаДТ.Договор;
				НоваяСтрока.СчетРасчетов = Счет_76_09;
				НоваяСтрока.СпособУчетаНДС = Перечисления.СпособыУчетаНДС.ПринимаетсяКВычету; 
			КонецЦикла; // создание строк ПоступлениеТоваровУслуг	

			СохраненнаяДатаПТУ = ПоступлениеТоваровУслуг.Дата;
			Попытка 
				ПоступлениеТоваровУслуг.Записать(РежимЗаписиДокумента.Запись);
			Исключение   
				Если ЗначениеЗаполнено(ДатаДокЕслиПериодЗакрыт) Тогда
					// если период закрыт, то пробуем записать документ с датой ДатаДокЕслиПериодЗакрыт
					// если не получится и с новой датой, то будет исключение в расположенном выше Попытка ... 
					ПоступлениеТоваровУслуг.Дата = РезервнаяДатаПТУ;
					ПоступлениеТоваровУслуг.Записать(РежимЗаписиДокумента.Запись);
					Сообщить(Строка(ПоступлениеТоваровУслуг) +  ": установлена дата документа = " + Строка(РезервнаяДатаПТУ) + 
						" вместо " + Строка(СохраненнаяДатаПТУ));
				КонецЕсли;	
			КонецПопытки;	
				
			СтрокаДТ.ПоступлениеТоваровУслуг = ПоступлениеТоваровУслуг.Ссылка;
			ПоступлениеТоваровУслуг.Записать(РежимЗаписиДокумента.Проведение);				
			ЗаписатьЗначениеДопСведения(ПоступлениеТоваровУслуг.Ссылка, 
				ЭтаФорма.ОснСведениеНомераЗаказовНЛ_ПоступлениеТоваровУслуг, ВернутьПравильнуюСтрокуID(СтрокаДТ.IDЗаказа)); // значение строка	

			Если НЕ КонтрактодержательЭтоСармант И НЕ ЭтаФорма.ОставлятьВсеПроводки Тогда // новый кусок с начала октября 2022; добавлено 26.02.2026: при ПП галочка ОставлятьВсеПроводки пропускает удаление
				// программная коррекция движений документа ПоступлениеТоваровУслуг (на примере хотя бы https://helpf.pro/faq83/view/1610.html)
				// потом ПоступлениеТоваровУслуг.РучнаяКорректировка = Истина;
				
				 // прочитать набор записей регистра по документу	
 				НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
 				НаборЗаписей.Отбор.Регистратор.Установить(ПоступлениеТоваровУслуг.Ссылка);
 				НаборЗаписей.Прочитать();    

				БылиЗаписиСчетДТНеРавен002 = Истина;
				Пока БылиЗаписиСчетДТНеРавен002 Цикл  // при другом варианте перебора часть не нужных записей не будет удалена
					БылиЗаписиСчетДТНеРавен002 = Ложь;
					Для каждого Запись из НаборЗаписей Цикл
						// старый вариант, до субсчетов 002
						//Если Запись.СчетДт <> Счет_002 Тогда
						//	БылиЗаписиСчетДТНеРавен002 = Истина;
						//	НаборЗаписей.Удалить(Запись);
						//КонецЕсли;                                    
						
						//Если Запись.СчетДт.Родитель <> Счет_002.Родитель Тогда
						//	БылиЗаписиСчетДТНеРавен002 = Истина;
						//	НаборЗаписей.Удалить(Запись);
  						//КонецЕсли;
					
						Если НЕ Это002ИлиСубсчет002(Запись.СчетДт) Тогда // уточненный вариант
							БылиЗаписиСчетДТНеРавен002 = Истина;
							НаборЗаписей.Удалить(Запись);
  						КонецЕсли;
					КонецЦикла;
				КонецЦикла;	
 				НаборЗаписей.Записать();
				
				ПоступлениеТоваровУслуг.РучнаяКорректировка = Истина;
				ПоступлениеТоваровУслуг.Записать(РежимЗаписиДокумента.Проведение);
			КонецЕсли;	//	Если НЕ КонтрактодержательЭтоСармант Тогда // новый кусок с начала октября 2022 

		Исключение
			СтрокаДТ.Ошибка = Истина;
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Ошибка создания/проведения документа ПоступлениеТоваровУслуг на основе данных ДТ. ";
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + ОписаниеОшибки();					
			Возврат Ложь;
		КонецПопытки;	//** Создание ПоступлениеТоваровУслуг
		
		//** Создание ОтчетКомитентуОПродажах **************************************************************************************		
		Попытка
			ОКОП = Документы.ОтчетКомитентуОПродажах.СоздатьДокумент();		
			ОКОП.Дата = ДатаОКОП; // СтрокаДТ.ДатаДокументов + 9 * 60 * 60 + 5 * 60;	// 09:05
			ОКОП.ВидОперации = Перечисления.ВидыОперацийОтчетКомитентуОПродажах.ОтчетОЗакупках;
			ОКОП.Организация = ЭтаФорма.Организация;
			// ОКОП.ПодразделениеОрганизации 
			ОКОП.СпособРасчетаКомиссионногоВознаграждения = Перечисления.СпособыРасчетаКомиссионногоВознаграждения.НеРассчитывается;
			ОКОП.Контрагент = ЭтаФорма.Контрагент1C;
			ОКОП.ДоговорКонтрагента = СтрокаДТ.Договор;
			ОКОП.СтавкаНДСВознаграждения = Перечисления.СтавкиНДС.НДС22;
			ОКОП.СчетУчетаРасчетовСКонтрагентом = Счет_62_01;
			ОКОП.СчетУчетаРасчетовПоАвансам = Счет_62_02;
			ОКОП.УдержатьВознаграждение = Ложь;
			ОКОП.Ответственный = Пользователи.ТекущийПользователь();
			ОКОП.Комментарий = СтрокаКомментарияДокументов;
			ОКОП.СуммаДокумента = Окр(СтрокаДТ.СуммаПокупки * СтрокаДТ.КурсВалюты, 2); // ??
			ОКОП.ВалютаДокумента = Валюта_Рубль;
			ОКОП.КурсВзаиморасчетов = СтрокаДТ.КурсВалюты;
			ОКОП.КратностьВзаиморасчетов = 1.0;
			ОКОП.ТипЦен = ТипЦенНоменклатуры_ОсновнаяЦенаПокупки;
			ОКОП.СуммаВключаетНДС = Истина;
			ОКОП.ВыписыватьСчетаФактурыСводно = Истина;
			
			СтрокаПоставщики = ОКОП.Поставщики.Добавить();
			СтрокаПоставщики.Поставщик = СтрокаДТ.Поставщик;
			СтрокаПоставщики.Партия = СтрокаДТ.ПоступлениеТоваровУслуг;
			СтрокаПоставщики.ДатаСФ = НачалоДня(ОКОП.Дата);
			Если ЭтаФорма.ВтчКомиссионноеВознаграждение Тогда
				ОКОП.УслугаПоВознаграждению	= ЭтаФорма.УслугаПоВознаграждению;
				ОКОП.СчетДоходов = ЭтаФорма.СчетУчетаДоходов;
				ОКОП.СчетУчетаНДСПоРеализации = ЭтаФорма.СчетУчетаНДС;
				ОКОП.СтавкаНДСВознаграждения = ЭтаФорма.СтавкаНДСВознагражения;				
			КонецЕсли;	// Если ЭтаФорма.ВтчКомиссионноеВознаграждение Тогда
			
			КолвоСтрокПТУТовары = ПоступлениеТоваровУслуг.Товары.Количество();
			НомерОбрабатываемойСтроки = 0;
			РаспределеннаяСуммаАгентскогоВознаграждения = 0;
			
			Для каждого СтрокаТоварыПТУ из ПоступлениеТоваровУслуг.Товары Цикл
				НомерОбрабатываемойСтроки = НомерОбрабатываемойСтроки + 1;
				СтрокаТоварыОКОП = ОКОП.Товары.Добавить();
				СтрокаТоварыОКОП.Номенклатура = СтрокаТоварыПТУ.Номенклатура;
				СтрокаТоварыОКОП.КоличествоМест = СтрокаТоварыПТУ.Количество;
				СтрокаТоварыОКОП.ЕдиницаИзмерения = СтрокаТоварыПТУ.Номенклатура.ЕдиницаИзмерения;
				СтрокаТоварыОКОП.Коэффициент = 1;
				СтрокаТоварыОКОП.Количество = СтрокаТоварыПТУ.Количество;
				СтрокаТоварыОКОП.Цена = Окр(СтрокаТоварыПТУ.Цена * СтрокаДТ.КурсВалюты, 2);
				СтрокаТоварыОКОП.Сумма = Окр(СтрокаТоварыПТУ.Сумма * СтрокаДТ.КурсВалюты, 2);
				СтрокаТоварыОКОП.СтавкаНДС = СтрокаТоварыПТУ.СтавкаНДС;
				СтрокаТоварыОКОП.СтранаПроисхождения = СтрокаТоварыПТУ.СтранаПроисхождения;
				СтрокаТоварыОКОП.ИдентификаторСтроки = Новый УникальныйИдентификатор; // так в форме документа
				СтрокаТоварыОКОП.НомерГТД = СтрокаТоварыПТУ.НомерГТД;
				ОчереднаяСуммаВознаграждения = Окр(ЭтаФорма.КомиссионноеВознагражениеСумма / КолвоСтрокПТУТовары, 2);
				Если НомерОбрабатываемойСтроки = КолвоСтрокПТУТовары Тогда
					СтрокаТоварыОКОП.СуммаВознаграждения = ЭтаФорма.КомиссионноеВознагражениеСумма - РаспределеннаяСуммаАгентскогоВознаграждения;
				Иначе
					СтрокаТоварыОКОП.СуммаВознаграждения = ОчереднаяСуммаВознаграждения;
					РаспределеннаяСуммаАгентскогоВознаграждения = РаспределеннаяСуммаАгентскогоВознаграждения + СтрокаТоварыОКОП.СуммаВознаграждения;
				КонецЕсли;	
				СтрокаТоварыОКОП.СуммаНДСВознаграждения = УчетНДСКлиентСервер.РассчитатьСуммуНДС(СтрокаТоварыОКОП.СуммаВознаграждения,
					ОКОП.СуммаВключаетНДС, УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(ОКОП.СтавкаНДСВознаграждения));

				//СтрокаТоварыОКОП.ВсегоВознаграждение - поле формы	
				//СтрокаТоварыОКОП.ВсегоВознаграждение = СтрокаТоварыОКОП.СуммаВознаграждения + 
				//	?(ОКОП.СуммаВключаетНДС, 0, СтрокаТоварыОКОП.СуммаНДСВознаграждения);				
				
			КонецЦикла;	
			
			СохраненнаяДатаОКОП = ОКОП.Дата;
			Попытка		
				ОКОП.Записать(РежимЗаписиДокумента.Запись);
			Исключение
				Если ЗначениеЗаполнено(ДатаДокЕслиПериодЗакрыт) Тогда
					// если период закрыт, то пробуем записать документ с датой ДатаДокЕслиПериодЗакрыт
					// если не получится и с новой датой, то будет исключение в расположенном выше Попытка ... 
                    ОКОП.Дата = РезервнаяДатаОКОП;
					ОКОП.Записать(РежимЗаписиДокумента.Запись);					
					Сообщить(Строка(ОКОП) +  ": установлена дата документа = " + Строка(РезервнаяДатаОКОП) + 
						" вместо " + Строка(СохраненнаяДатаОКОП));
				КонецЕсли;
			КонецПопытки;
			
			СтрокаДТ.ОтчетКомитентуОПродажах = ОКОП.Ссылка;
			ОКОП.Записать(РежимЗаписиДокумента.Проведение);				
			ЗаписатьЗначениеДопСведения(ОКОП.Ссылка, ЭтаФорма.ОснСведениеНомерЗаказа_ОтчетКомитентуОПродажах, СтрокаДТ.IDЗаказа);  // значение число
	
		Исключение
			СтрокаДТ.Ошибка = Истина;
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Ошибка создания/проведения документа ОтчетКомитентуОПродажах. ";
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + ОписаниеОшибки();					
			Возврат Ложь;
		КонецПопытки;	//** Создание ОтчетКомитентуОПродажах
		
		//** Создание ПередачаТоваров **************************************************************************************
		Попытка 
			ПередачаТоваров = Документы.ПередачаТоваров.СоздатьДокумент();
			ПередачаТоваров.Дата = ДатаПередачаТоваров; // СтрокаДТ.ДатаДокументов + 9 * 60 * 60 + 10 * 60;	// 09:10
			ПередачаТоваров.ВидОперации = Перечисления.ВидыОперацийПередачаТоваров.ПередачаТоваровКомитенту;
			ПередачаТоваров.Организация = ЭтаФорма.Организация;
			ПередачаТоваров.Склад = ЭтаФорма.Склад; 
			ПередачаТоваров.Контрагент = ЭтаФорма.Контрагент1C;
			ПередачаТоваров.ДоговорКонтрагента = СтрокаДТ.Договор;
			ПередачаТоваров.ВалютаДокумента = Валюта_Рубль;
			ПередачаТоваров.СчетУчетаРасчетовПоТаре = Счет_76_06;
			ПередачаТоваров.Комментарий = СтрокаКомментарияДокументов;
			ПередачаТоваров.Ответственный = Пользователи.ТекущийПользователь();
			ПередачаТоваров.ТипЦен = ТипЦенНоменклатуры_ОсновнаяЦенаПокупки;
			ПередачаТоваров.КурсВзаиморасчетов = 1.0;			
			ПередачаТоваров.КратностьВзаиморасчетов = 1.0;
			ПередачаТоваров.СуммаВключаетНДС = Истина;
			// ПередечаТоваров.СуммаДокумента = Окр(СтрокаДТ.СуммаПокупки * СтрокаДТ.КурсВалюты, 2); // = 0
			Для каждого СтрокаТоварыПТУ из ПоступлениеТоваровУслуг.Товары Цикл
				СтрокаТоварыПередачаТоваров = ПередачаТоваров.Товары.Добавить();
				СтрокаТоварыПередачаТоваров.Номенклатура = СтрокаТоварыПТУ.Номенклатура;
				СтрокаТоварыПередачаТоваров.Количество = СтрокаТоварыПТУ.Количество;
				СтрокаТоварыПередачаТоваров.СчетУчета = Счет_002;
				СтрокаТоварыПередачаТоваров.ИдентификаторСтроки = Новый УникальныйИдентификатор; // так в форме документа				
			КонецЦикла;	                                                                 
			
			СохраненнаяДатаПередачаТоваров = ПередачаТоваров.Дата;
  			Попытка
				ПередачаТоваров.Записать(РежимЗаписиДокумента.Запись);
			Исключение	
				Если ЗначениеЗаполнено(ДатаДокЕслиПериодЗакрыт) Тогда
					// если период закрыт, то пробуем записать документ с датой ДатаДокЕслиПериодЗакрыт
					// если не получится и с новой датой, то будет исключение в расположенном выше Попытка ... 
                    ПередачаТоваров.Дата = РезервнаяДатаПередачаТоваров;
					ПередачаТоваров.Записать(РежимЗаписиДокумента.Запись);					
					Сообщить(Строка(ПередачаТоваров) +  ": установлена дата документа = " + Строка(РезервнаяДатаПередачаТоваров) + 
						" вместо " + Строка(СохраненнаяДатаПередачаТоваров));
				КонецЕсли;
			КонецПопытки;	
				
			СтрокаДТ.ПередачаТоваров = ПередачаТоваров.Ссылка;
			ПередачаТоваров.Записать(РежимЗаписиДокумента.Проведение);				
			ЗаписатьЗначениеДопСведения(ПередачаТоваров.Ссылка, 
				ЭтаФорма.ОснСведениеНомерЗаказа_ПередачаТоваров, СтрокаДТ.IDЗаказа); // значение число 			
		
		Исключение
			СтрокаДТ.Ошибка = Истина;
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Ошибка создания документа ПередачаТоваров.";
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + ОписаниеОшибки();
			Возврат Ложь;
		КонецПопытки;	// создание документа ПередачаТоваров

		//** Создание ОперацияБух **************************************************************************************				
		// операция может уже существовать, она может быть передана из обработки ОперацииВозмещенияТаможПлатежейПоФайлуExcel
		// см. ОбработкаОповещения(...)
		Если НЕ ЗначениеЗаполнено(СтрокаДТ.Операция) Тогда
			Попытка
				// ОперацияБух создается в любом случае, независимо от того контрактодержатель это Сармант или не Сармант
				ДокОперация = Документы.ОперацияБух.СоздатьДокумент();
				ДокОперация.Дата = ДатаОперация; // КонецДня(СтрокаДТ.ДатаДокументов);
				ДокОперация.Организация = ЭтаФорма.Организация;
				// ДокОперация.СуммаОперации будет заполнено ниже 
				ДокОперация.Содержание = "Возмещение таможенных платежей " + СтрокаКомментарияДокументов;
				ДокОперация.СпособЗаполнения = "Вручную";
				ДокОперация.Ответственный = Пользователи.ТекущийПользователь();
				ДокОперация.Комментарий = "заказ НЛ " + ВернутьПравильнуюСтрокуID(СтрокаДТ.IDЗаказа);
				// в первый раз записать ДокОперация без проводок  
				
				СохраненнаяДатаДокОперация = ДокОперация.Дата;
				Попытка
					ДокОперация.Записать(РежимЗаписиДокумента.Запись);
				Исключение
					Если ЗначениеЗаполнено(ДатаДокЕслиПериодЗакрыт) Тогда
						// если период закрыт, то пробуем записать документ с датой ДатаДокЕслиПериодЗакрыт
						// если не получится и с новой датой, то будет исключение в расположенном выше Попытка ... 
	                    ДокОперация.Дата = РезервнаяДатаОперация;
						ДокОперация.Записать(РежимЗаписиДокумента.Запись);
						Сообщить(Строка(ДокОперация) +  ": установлена дата документа = " + Строка(РезервнаяДатаОперация) + 
							" вместо " + Строка(СохраненнаяДатаДокОперация));
					КонецЕсли;
				КонецПопытки;	

				// н-р в Сармант ОперацияБух № 721 от 09.03.2022
				// создание и заполнение ОперацияБух на примере обработки ЗагрузкаГТДНоваяСтараяЛогистика
				// не заполнять ДокСписаниеСРасчетногоСчета в субконто счетов строк операции
				//ДокСписаниеСРасчетногоСчета = ВернутьСписаниеСРасчетногоСчетаДляОперацияБух(СтруктураТП.НомерПП, СтруктураТП.ДатаПП);
				//Если ДокСписаниеСРасчетногоСчета = Неопределено Тогда
				//	ЕстьОшибкиПоискаДокументаСписаниеСРасчетногоСчета = Истина;
				//ИначеЕсли ДокСписаниеСРасчетногоСчета = ДокСписаниеСРасчетногоСчетаПустаяСсылка	Тогда
				//	ЕстьОшибкиПоискаДокументаСписаниеСРасчетногоСчета = Истина;
				//Иначе	
				//	БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетКт, НоваяЗапись.СубконтоКт, 		
				//		"ДокументыРасчетовСКонтрагентами", ДокСписаниеСРасчетногоСчета);		
				//КонецЕсли;  	
				
				СуммаОперации = 0;
				Регистр = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
				Регистр.Отбор.Регистратор.Значение = ДокОперация.Ссылка;
				Если СтрокаДТ.Сумма1010 > 0 Тогда
					НоваяЗапись = Регистр.Добавить();
					НоваяЗапись.Период = ДокОперация.Дата;			
					НоваяЗапись.Регистратор = ДокОперация.Ссылка;			
					НоваяЗапись.Активность = Истина;			
					НоваяЗапись.СчетДт = Счет_76_09;
					НоваяЗапись.СчетКт = Счет_76_05;			
					НоваяЗапись.Организация = ЭтаФорма.Организация;
					НоваяЗапись.Сумма = СтрокаДТ.Сумма1010;    // СтрокаДТ.2010, СтрокаДТ.5010
					НоваяЗапись.СуммаНУДт = СтрокаДТ.Сумма1010;
					НоваяЗапись.СуммаНУКт = СтрокаДТ.Сумма1010;
					СуммаОперации = СуммаОперации + СтрокаДТ.Сумма1010;									
					БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетДт, НоваяЗапись.СубконтоДт,
						"Контрагенты", ЭтаФорма.Контрагент1C);
					БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетДт, НоваяЗапись.СубконтоДт,
						"Договоры", СтрокаДТ.Договор);
					БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетКт, НоваяЗапись.СубконтоКт, 
						"Контрагенты", ЭтаФорма.КонтрагентТаможенныеПлатежи);
					БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетКт, НоваяЗапись.СубконтоКт, 
						"Договоры", ЭтаФорма.ДоговорТаможенныеПлатежи);
					НоваяЗапись.Содержание = "Таможенный сбор (1010)"; // Пошлина (2010), НДС (5010)
					Регистр.Записать();			
				КонецЕсли; // Если СтрокаДТ.Сумма1010 > 0 Тогда					
				Если СтрокаДТ.Сумма2010 > 0 Тогда
					//Регистр = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
					//Регистр.Отбор.Регистратор.Значение = ДокОперация.Ссылка;
					НоваяЗапись = Регистр.Добавить();
					НоваяЗапись.Период = ДокОперация.Дата;			
					НоваяЗапись.Регистратор = ДокОперация.Ссылка;			
					НоваяЗапись.Активность = Истина;			
					НоваяЗапись.СчетДт = Счет_76_09;
					НоваяЗапись.СчетКт = Счет_76_05;			
					НоваяЗапись.Организация = ЭтаФорма.Организация;
					НоваяЗапись.Сумма = СтрокаДТ.Сумма2010;    // СтрокаДТ.2010, СтрокаДТ.5010
					НоваяЗапись.СуммаНУДт = СтрокаДТ.Сумма2010;
					НоваяЗапись.СуммаНУКт = СтрокаДТ.Сумма2010;
					СуммаОперации = СуммаОперации + СтрокаДТ.Сумма2010;									
					БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетДт, НоваяЗапись.СубконтоДт,
						"Контрагенты", ЭтаФорма.Контрагент1C);
					БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетДт, НоваяЗапись.СубконтоДт,
						"Договоры", СтрокаДТ.Договор);
					БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетКт, НоваяЗапись.СубконтоКт, 
						"Контрагенты", ЭтаФорма.КонтрагентТаможенныеПлатежи);
					БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетКт, НоваяЗапись.СубконтоКт, 
						"Договоры", ЭтаФорма.ДоговорТаможенныеПлатежи);
					НоваяЗапись.Содержание = "Пошлина (2010)"; // Пошлина (2010), НДС (5010)
					Регистр.Записать();			
				КонецЕсли; // Если СтрокаДТ.Сумма2010 > 0 Тогда	
				Если СтрокаДТ.Сумма5010 > 0 Тогда
					//Регистр = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
					//Регистр.Отбор.Регистратор.Значение = ДокОперация.Ссылка;
					НоваяЗапись = Регистр.Добавить();
					НоваяЗапись.Период = ДокОперация.Дата;			
					НоваяЗапись.Регистратор = ДокОперация.Ссылка;			
					НоваяЗапись.Активность = Истина;			
					НоваяЗапись.СчетДт = Счет_76_09;
					НоваяЗапись.СчетКт = Счет_76_05;			
					НоваяЗапись.Организация = ЭтаФорма.Организация;
					НоваяЗапись.Сумма = СтрокаДТ.Сумма5010;    // СтрокаДТ.2010, СтрокаДТ.5010
					НоваяЗапись.СуммаНУДт = СтрокаДТ.Сумма5010;
					НоваяЗапись.СуммаНУКт = СтрокаДТ.Сумма5010;
					СуммаОперации = СуммаОперации + СтрокаДТ.Сумма5010;									
					БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетДт, НоваяЗапись.СубконтоДт,
						"Контрагенты", ЭтаФорма.Контрагент1C);
					БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетДт, НоваяЗапись.СубконтоДт,
						"Договоры", СтрокаДТ.Договор);
					БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетКт, НоваяЗапись.СубконтоКт, 
						"Контрагенты", ЭтаФорма.КонтрагентТаможенныеПлатежи);
					БухгалтерскийУчет.УстановитьСубконто(НоваяЗапись.СчетКт, НоваяЗапись.СубконтоКт, 
						"Договоры", ЭтаФорма.ДоговорТаможенныеПлатежи);
					НоваяЗапись.Содержание = "НДС (5010)"; // Пошлина (2010), НДС (5010)
					Регистр.Записать();			
				КонецЕсли; // Если СтрокаДТ.Сумма5010 > 0 Тогда				
							
				ДокОперация.СуммаОперации = СуммаОперации;
				ДокОперация.Записать(РежимЗаписиДокумента.Запись); 			
				// ДокОперация не проводится, при проведении возникает ошибка
				СтрокаДТ.Операция = ДокОперация.Ссылка;			
				ЗаписатьЗначениеДопСведения(ДокОперация.Ссылка, ЭтаФорма.ОснСведениеНомерЗаказа_ОперацияБух, СтрокаДТ.IDЗаказа); // значение число 						
			Исключение
				СтрокаДТ.Ошибка = Истина;
				СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Ошибка создания документа ОперацияБух.";
				СтрокаДТ.Примечания = СтрокаДТ.Примечания + ОписаниеОшибки();
				Возврат Ложь;
			КонецПопытки;	// создание документа ОперацияБух
		КонецЕсли; // Если НЕ ЗначениеЗаполнено(СтрокаДТ.Операция) Тогда			
				
		//** Создание ОтчетАгента **************************************************************************************
		Попытка // создание документа ОтчетАгента	
			ДокОтчетАгента = Документы.ОтчетыАгента_ОтчетАгента.СоздатьДокумент();
			ДокОтчетАгента.ВариантОтчетаАгента = Перечисления.ВариантОтчетаАгента.Марлин;
			ДокОтчетАгента.Дата = ДатаОтчетАгента; // СтрокаДТ.ДатаДокументов + 10 * 60 * 60; // время документа = 10.00
			ДокОтчетАгента.НомерОтчетаАгента = СтрокаДТ.НомерОтчетаАгента;
			ДокОтчетАгента.ДатаОтчетаАгента = ДокОтчетАгента.Дата;
			ДокОтчетАгента.IDДТ = СтрокаДТ.IDДТ;
			ДокОтчетАгента.НомерДТ = СтрокаДТ.НомерДТ;     
			Попытка
				ДокОтчетАгента.БазаЛогос = Объект.БазаЛогос + 1;
			Исключение
			КонецПопытки;	
			ДокОтчетАгента.ГлавныйНомерЗаказа = СтрокаДТ.IDЗаказа;
			ДокОтчетАгента.РеференсЗаказа = СтрокаДТ.НомерЗаказа;
			ДокОтчетАгента.ГлавныйДатаЗаказа = СтрокаДТ.ДатаЗаказа;
			//СтрокаДТ.СуммаПокупки
			//СтрокаДТ.ВалютаПокупки
			//СтрокаДТ.ВсегоПлатежей
			ДокОтчетАгента.Сумма1010 = СтрокаДТ.Сумма1010;
			ДокОтчетАгента.Сумма2010 = СтрокаДТ.Сумма2010;
			ДокОтчетАгента.Сумма5010 = СтрокаДТ.Сумма5010;
			//СтрокаДТ.ПрочПлатежи
			//СтрокаДТ.Примечания
			//СтрокаДТ.НомерДоговора
			//СтрокаДТ.ДатаДоговора
			//СтрокаДТ.СтрокаИнвойсов
			//СтрокаДТ.IDКонтрактодержателя
			ДокОтчетАгента.Контрагент = ЭтаФорма.Контрагент1C;
			ДокОтчетАгента.Организация = ЭтаФорма.Организация;
			ДокОтчетАгента.Договор = СтрокаДТ.Договор;
			//СтрокаДТ.НаименованиеПоставщика
			//СтрокаДТ.КурсВалюты
			//СтрокаДТ.Валюта
			
			// не добавляю в табличную часть Документы документ ПоступлениеТоваровУслуг
			СтрокаДокументы = ДокОтчетАгента.Документы.Добавить();
			СтрокаДокументы.Документ = ОКОП.Ссылка;
			СтрокаДокументы.НомерДокумента = ОКОП.Номер;
			СтрокаДокументы.ДатаДокумента = ОКОП.Дата;
			СтрокаДокументы.КонтрагентВДокументе = ЭтаФорма.Контрагент1C;
			СтрокаДокументы.ДоговорВДокументе = СтрокаДТ.Договор;
			//СтрокаДокументы.ДокументСчетФактураВыданный // в этом варианте нет СФВыданных
			// СтрокаДокументы.СуммаВознагражденияАктОбОказанииУслугОКОП // в этом варианте 0
			СтрокаДокументы.СуммаДокумента = ОКОП.СуммаДокумента;
			СтрокаДокументы.СуммаПередачаТоваров = 0;
			//СтрокаДокументы.Сумма1010, СтрокаДокументы.Сумма2010, СтрокаДокументы.Сумма5010
			//СтрокаДокументы.УслугиБанков, СтрокаДокументы.КурсовыеРазницы
			СтрокаДокументы.СтрокаПечатныхФорм = "";
			
			СтрокаДокументы = ДокОтчетАгента.Документы.Добавить();
			СтрокаДокументы.Документ = ПередачаТоваров.Ссылка;
			СтрокаДокументы.НомерДокумента = ПередачаТоваров.Номер;
			СтрокаДокументы.ДатаДокумента = ПередачаТоваров.Дата;
			СтрокаДокументы.КонтрагентВДокументе = ЭтаФорма.Контрагент1C;
			СтрокаДокументы.ДоговорВДокументе = СтрокаДТ.Договор;
			//СтрокаДокументы.ДокументСчетФактураВыданный // в этом варианте нет СФВыданных
			// СтрокаДокументы.СуммаВознагражденияАктОбОказанииУслугОКОП // в этом варианте 0
			СтрокаДокументы.СуммаДокумента = 0;
			СтрокаДокументы.СуммаПередачаТоваров = 0; // ??
			//СтрокаДокументы.Сумма1010, СтрокаДокументы.Сумма2010, СтрокаДокументы.Сумма5010
			//СтрокаДокументы.УслугиБанков, СтрокаДокументы.КурсовыеРазницы
			СтрокаДокументы.АктПередачаТоваров = Истина;
			СтрокаДокументы.СтрокаПечатныхФорм = "Акт (передача товаров);";
			
			СтрокаГТДВБазе = ДокОтчетАгента.ГТДВБазе.Добавить();
			СтрокаГТДВБазе.ГТД = СтрокаДТ.НомерДТ;
			СтрокаГТДВБазе.ПоступлениеТоваровУслуг = ПоступлениеТоваровУслуг.Ссылка;
			
			СтруктураПоискаГрузЕдПоДТ = Новый Структура("НомерДТ", СтрокаДТ.НомерДТ);
			НайденныеСтрокиГЕПоДТ = ЭтаФорма.НомераГрузовыхЕдиницПоДТ.НайтиСтроки(СтруктураПоискаГрузЕдПоДТ); 
			// может быть несколько грузовых единиц в одной ДТ
			Для каждого СтрокаНГЕ из НайденныеСтрокиГЕПоДТ Цикл  // заполнение в Документе ОтчетАгента табличных частей ГТДИзНЛ и НомераГрузовыхЕдиниц	
				СтрокаГТДИзНЛ = ДокОтчетАгента.ГТДИзНЛ.Добавить();
				СтрокаГТДИзНЛ.Пометка = Истина;
				СтрокаГТДИзНЛ.ГТД = СтрокаДТ.НомерДТ;
				СтрокаГТДИзНЛ.ГрузоваяЕдиница = СтрокаНГЕ.НомерГрузовойЕдиницы;  // строка			
			
				СтрокаНомераГЕ = ДокОтчетАгента.НомераГрузовыхЕдиниц.Добавить();
				СтрокаНомераГЕ.Пометка = Истина;
				СтрокаНомераГЕ.IDГруза = СтрокаНГЕ.IDГрузовойЕдиницы;
				СтрокаНомераГЕ.ГрузоваяЕдиница = СтрокаНГЕ.НомерГрузовойЕдиницы;   // строка
				СтрокаНомераГЕ.РеференсГруза = СтрокаНГЕ.РеференсГрузовойЕдиницы;
			КонецЦикла; // заполнение в Документе ОтчетАгента табличных частей ГТДИзНЛ и НомераГрузовыхЕдиниц	
			
			СохраненнаяДатаДокОтчетАгента = ДокОтчетАгента.Дата;
			Попытка
				ДокОтчетАгента.Записать(РежимЗаписиДокумента.Запись);
			Исключение
				Если ЗначениеЗаполнено(ДатаДокЕслиПериодЗакрыт) Тогда
					// если период закрыт, то пробуем записать документ с датой ДатаДокЕслиПериодЗакрыт
					// если не получится и с новой датой, то будет исключение в расположенном выше Попытка ... 
                    ДокОтчетАгента.Дата = РезервнаяДатаОтчетАгента;
					ДокОтчетАгента.Записать(РежимЗаписиДокумента.Запись);
					Сообщить(Строка(ДокОтчетАгента) +  ": установлена дата документа = " + Строка(РезервнаяДатаОтчетАгента) + 
						" вместо " + Строка(СохраненнаяДатаДокОтчетАгента));
				КонецЕсли;
			КонецПопытки;
			
			// Сообщить("Создан документ " + Строка(ДокОтчетАгента.Ссылка));
			СтрокаДТ.ОтчетАгента = СтрокаДТ.ОтчетАгента + "№ " + 
				СокрЛП(ДокОтчетАгента.НомерОтчетаАгента) + ", " + Формат(ДокОтчетАгента.ДатаОтчетаАгента, "ДФ=dd.MM.yyyy") + " (№ " +			
				Строка(ДокОтчетАгента.Номер) + ", " + Формат(ДокОтчетАгента.Дата, "ДФ=dd.MM.yyyy") + ");";
			СтрокаДТ.ОтчетАгентаGUID = ДокОтчетАгента.Ссылка.УникальныйИдентификатор();
			// Пометка будет снята в СозданиеИЗаписьВПрисоединенныеФайлыОтчетовАгента()
			//СтрокаДТ.Пометка = Ложь;  
			СтрокаДТ.Создан = Истина;

		Исключение
			СтрокаДТ.Ошибка = Истина;
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Ошибка создания документа ОтчетАгента на основе данных ДТ.";
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + ОписаниеОшибки();
			Возврат Ложь;
		КонецПопытки;	// создание документа ОтчетАгента
	КонецЦикла;	  // перебор ЭтаФорма.ДТДляГрупповогоСоздания для создания документов
	Возврат Истина;
КонецФункции  // СоздатьПакетыДокументовНаСервере()

//&НаСервереБезКонтекста
&НаСервере
Функция НайтиСоздатьНоменклатуру(СтруктураПоискаНоменклатураДТ, CсылкаНаНомерДТ, СсылкаГруппаНоменклатуры, ВидНоменклатуры_Товары, НомерЗВ = "", ДатаЗВ = Неопределено)
	// СтруктураПоискаНоменклатураДТ = Новый Структура("IDДТ", СтрокаДТ.IDДТ);
	
	ДопустимаяПогрешностьНДС = 0.09;  // эмпирически, аналогично применяемой при загрузке ДТ	
	
	СозданиеНоменклатурыЗавершеноУспешно = Истина;
	СтрокиТекущейДТ = ЭтаФорма.НоменклатураДТ.НайтиСтроки(СтруктураПоискаНоменклатураДТ);
	Для каждого НомДТ из СтрокиТекущейДТ Цикл  // создать номенклатуру, заполнить поле ЭтаФорма.НоменклатураДТ.Номенклатура
		Попытка
			ИспользоватьРеквизитСтавкаНДС = Ложь; // сейчас используется реквизит ВидСтавкиНДС, ранее - СтавкаНДС

			НоменклатураОбъект = Справочники.Номенклатура.СоздатьЭлемент();	
			НоменклатураОбъект.ВидНоменклатуры = ВидНоменклатуры_Товары;
			НоменклатураОбъект.Родитель	= СсылкаГруппаНоменклатуры;
			НоменклатураОбъект.Наименование	= НомДТ.Наименование;
			НоменклатураОбъект.НаименованиеПолное = НомДТ.Наименование;
		    		
			ПроцентНДС = 20;
			Если НомДТ.СуммаНДС > 0 Тогда
				Попытка
					РасчетныйПроцентНДС = 100.00 * НомДТ.СуммаНДС / (НомДТ.ТаможеннаяСтоимость + НомДТ.Пошлина);
				Исключение   // деление на ноль
					РасчетныйПроцентНДС = 20;  // маловероятно
				КонецПопытки;	
				Если (РасчетныйПроцентНДС > 10 - ДопустимаяПогрешностьНДС ) И (РасчетныйПроцентНДС < 10 + ДопустимаяПогрешностьНДС) Тогда				
					ПроцентНДС = 10;
				ИначеЕсли (РасчетныйПроцентНДС > 18 - ДопустимаяПогрешностьНДС ) И (РасчетныйПроцентНДС < 18 + ДопустимаяПогрешностьНДС) Тогда				
					ПроцентНДС = 18;				
				ИначеЕсли (РасчетныйПроцентНДС > 20 - ДопустимаяПогрешностьНДС ) И (РасчетныйПроцентНДС < 20 + ДопустимаяПогрешностьНДС) Тогда				
					ПроцентНДС = 20;				
				Иначе 	
					ПроцентНДС = 20;
				КонецЕсли;	
			Иначе
				ПроцентНДС = 0;
			КонецЕсли;
		
			Если ПроцентНДС = 0 Тогда
				НоменклатураОбъект.ВидСтавкиНДС = Перечисления.ВидыСтавокНДС.БезНДС;
			ИначеЕсли ПроцентНДС = 10 Тогда	
				НоменклатураОбъект.ВидСтавкиНДС = Перечисления.ВидыСтавокНДС.Пониженная;				
			ИначеЕсли (ПроцентНДС = 18) ИЛИ (ПроцентНДС = 20) Тогда	
				НоменклатураОбъект.ВидСтавкиНДС = Перечисления.ВидыСтавокНДС.Общая;			
			КонецЕсли;	
			
			НоменклатураОбъект.ЕдиницаИзмерения		= НомДТ.ЕдИзм;
			// СтатьяЗатрат должна быть пустой (у нас не производство!!)
			//	НоменклатураОбъект.СтатьяЗатрат			= СтруктураДляПоискаНоменклатуры.СтатьяЗатрат_МатериальныеРасходы;
			НоменклатураОбъект.СтранаПроисхождения	= НомДТ.Страна;
		
			// не используется дополненный номер ГТД, т.е. номер ГТД + "/" + Строка(НомерСтрокиВДокументе)
			НоменклатураОбъект.КодТНВЭД = НайтиСоздатьКодТНВЭД(НомДТ.КодТНВЭД);
			НоменклатураОбъект.Артикул = НомДТ.Артикул; 
			НоменклатураОбъект.Комментарий = НомДТ.Артикул;             
			Попытка  // добавлено 17.02.2023
				НоменклатураОбъект.НомерГТД = CсылкаНаНомерДТ;
			Исключение
			КонецПопытки;
			// заявление на выпуск (ЗВ): номер и дата — добавлено 26.02.2026
			Если НЕ ПустаяСтрока(НомерЗВ) Тогда
				Попытка
					НоменклатураОбъект.ЗаявлениеНаВыпуск = НомерЗВ;
				Исключение
				КонецПопытки;
			КонецЕсли;
			Если ДатаЗВ <> Неопределено И ДатаЗВ <> Дата(1,1,1) Тогда
				Попытка
					НоменклатураОбъект.ДатаЗаявленияНаВыпуск = ДатаЗВ;
				Исключение
				КонецПопытки;
			КонецЕсли;
			НоменклатураОбъект.Записать();
			НомДТ.Номенклатура = НоменклатураОбъект.Ссылка;

			//		Если (СтруктураДляПоискаНоменклатуры.МассаБрутто > 0) Тогда
			//			// т.к. создана новая номенклатура, не нужно искать в табличной части ДополнительныеРеквизиты
			//			// значение Объект.ОсновнойРеквизитМассаБрутто
			//			СтрокаДополнительныеРеквизиты = НоменклатураОбъект.ДополнительныеРеквизиты.Добавить();
			//			СтрокаДополнительныеРеквизиты.Свойство = Объект.ОсновнойРеквизитНоменклатураМассаБрутто;
			//			Колво = ?(СтруктураДляПоискаНоменклатуры.Количество=0, 1.000, СтруктураДляПоискаНоменклатуры.Количество);
			//			СтрокаДополнительныеРеквизиты.Значение = Окр(СтруктураДляПоискаНоменклатуры.МассаБрутто / Колво,3); 
			//		
			//			// добавить в табличную часть ДополнительныеРеквизиты значение Объект.ОсновнойРеквизитСтрокаМассаБрутто
			//			СтрокаДополнительныеРеквизиты = НоменклатураОбъект.ДополнительныеРеквизиты.Добавить();
			//			СтрокаДополнительныеРеквизиты.Свойство = Объект.ОсновнойРеквизитНоменклатураСтрокаМассаБрутто;
			//			// изображение числа 
			//			СтрокаЗначения = Строка(Окр(СтруктураДляПоискаНоменклатуры.МассаБрутто / Колво,14));
			//			// группы по 3 цифры могут быть разделены пробелом
			//			СтрокаЗначения = СтрЗаменить(Строка(СтрокаЗначения)," ",""); 
			//			// а могут - неразрывным пробелом (0160) 	
			//			СтрокаЗначения = СтрЗаменить(СтрокаЗначения,Символы.НПП,""); 					
			//			СтрокаДополнительныеРеквизиты.Значение = СтрокаЗначения;
			//			Попытка // еще раз записать 
			//				НоменклатураОбъект.Записать();	
			//			Исключение
			//				Объект.ОшибкаЗагрузкиМассаБрутто = Истина;
			//			КонецПопытки;	
			//		КонецЕсли;	// 	Если (СтруктураДляПоискаНоменклатуры.МассаБрутто > 0) Тогда

		Исключение
			Сообщить("Для ДТ " + Строка(CсылкаНаНомерДТ) + ": ошибка создания номенклатуры " + НомДТ.Наименование); 
			Сообщить(ОписаниеОшибки());
			СозданиеНоменклатурыЗавершеноУспешно = Ложь;
			Прервать;
		КонецПопытки;
	КонецЦикла;  	

	Возврат	СозданиеНоменклатурыЗавершеноУспешно;
КонецФункции  // Функция НайтиСоздатьНоменклатуру(...)

&НаСервереБезКонтекста
Функция НайтиТипыЦенНоменклатуры_ОсновнаяЦенаПокупки()
	// не будем проверять пометку на удаление				
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", "Основная цена покупки");
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ТипыЦенНоменклатуры.Ссылка
	|ИЗ
	|	Справочник.ТипыЦенНоменклатуры КАК ТипыЦенНоменклатуры
	|ГДЕ
	|	ТипыЦенНоменклатуры.Наименование = &Наименование";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	Возврат ПредопределенноеЗначение("Справочник.ТипыЦенНоменклатуры.ПустаяСсылка");
КонецФункции  // НайтиТипыЦенНоменклатуры_ОсновнаяЦенаПокупки()

&НаСервереБезКонтекста
Функция НайтиСоздатьДТ(НомерДекларации)
	// здесь не нужна проверка на отсутсвие пометки на удаление	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НомерДекларации", НомерДекларации);
	Запрос.Текст=
	"ВЫБРАТЬ
	|	НомераГТД.Ссылка
	|ИЗ
	|	Справочник.НомераГТД КАК НомераГТД
	|ГДЕ
	|	НомераГТД.Код = &НомерДекларации";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Попытка
		НовыйНомерГТД = Справочники.НомераГТД.СоздатьЭлемент();
		НовыйНомерГТД.Код = НомерДекларации;
		//	НовыйНомерГТД.ОбменДанными.Загрузка	= Истина;
		НовыйНомерГТД.Записать();
		Возврат НовыйНомерГТД.Ссылка;
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
	Возврат ПредопределенноеЗначение("Справочник.НомераГТД.ПустаяСсылка");
КонецФункции  // Функция НайтиСоздатьДТ(НомерДекларации)

&НаСервереБезКонтекста
Функция НайтиСоздатьГруппуНоменклатурыДТ(Наименование, ТолькоНайти)
	// если ТолькоНайти = Истина, то не создавать новую группу
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Наименование = &Наименование
	|	И НЕ Номенклатура.ПометкаУдаления
	|	И Номенклатура.ЭтоГруппа ";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Если ТолькоНайти Тогда
		Возврат ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");		
	КонецЕсли;	
	
	Попытка
		НоваяГруппа = Справочники.Номенклатура.СоздатьГруппу();	
		НоваяГруппа.Родитель				= НайтиГруппуНоменклатуры_ПрочиеТовары();
		НоваяГруппа.Наименование			= Наименование;
//		НоваяГруппа.ОбменДанными.Загрузка	= Истина;
		НоваяГруппа.Записать();
		Возврат НоваяГруппа.Ссылка;
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
	Возврат ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
	
КонецФункции  // Функция НайтиСоздатьГруппуНоменклатурыДТ(Наименование, ТолькоНайти)

&НаСервереБезКонтекста
Функция НайтиГруппуНоменклатуры_ПрочиеТовары()
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", "Прочие товары");
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Наименование = &Наименование
	|	И Номенклатура.ЭтоГруппа
	|	И НЕ Номенклатура.ПометкаУдаления";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
КонецФункции  // НайтиГруппуНоменклатуры_ПрочиеТовары()

&НаСервереБезКонтекста
Функция НайтиВидНоменклатуры_Товары()
	// не будем проверять пометку на удаление				
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Наименование", "Товары");
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ВидыНоменклатуры.Ссылка
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|ГДЕ
	|	ВидыНоменклатуры.Наименование = &Наименование";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	Возврат ПредопределенноеЗначение("Справочник.ВидыНоменклатуры.ПустаяСсылка");
КонецФункции  // НайтиВидНоменклатуры_Товары()

&НаСервереБезКонтекста
Функция НайтиСоздатьКодТНВЭД(КодТНВЭД)
	// не будем проверять пометку на удаление		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодТНВЭД", КодТНВЭД);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КлассификаторТНВЭД.Ссылка
	|ИЗ
	|	Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|ГДЕ
	|	КлассификаторТНВЭД.Код = &КодТНВЭД";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Попытка
		ЭлементТНВЭД = Справочники.КлассификаторТНВЭД.СоздатьЭлемент();
		ЭлементТНВЭД.Код					= КодТНВЭД;
		ЭлементТНВЭД.Наименование			= КодТНВЭД;
		ЭлементТНВЭД.НаименованиеПолное		= КодТНВЭД;
//		ЭлементТНВЭД.ОбменДанными.Загрузка	= Истина;
		ЭлементТНВЭД.Записать();
		Возврат ЭлементТНВЭД.Ссылка;
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	Возврат ПредопределенноеЗначение("Справочник.КлассификаторТНВЭД.ПустаяСсылка");
КонецФункции  // Функция НайтиСоздатьКодТНВЭД(КодТНВЭД)

#КонецОбласти // СозданиеПакетовДокументов

#Область ПечатныеФормы

&НаКлиенте
Процедура ПечатнаяФормаОтчетаАгентаМарлин(Команда)
	ТекДанные = ЭтаФорма.Элементы.ДТДляГрупповогоСоздания.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ИдентификаторСтроки = ТекДанные.ПолучитьИдентификатор();
	ТабличныйДокумент = ПечатнаяФормаОтчетаАгентаМарлинНаСервере(ИдентификаторСтроки);
	ТабличныйДокумент.Показать("Отчет агента");
КонецПроцедуры

&НаКлиенте
Функция СозданиеИЗаписьВПрисоединенныеФайлыОтчетовАгента()
	ФайлыУспешноСозданыЗаписаны = Истина;
	Для каждого СтрокаДТ из ЭтаФорма.ДТДляГрупповогоСоздания Цикл  // перебор ЭтаФорма.ДТДляГрупповогоСоздания для создания файлов отчетов агента
		Если НЕ СтрокаДТ.Пометка Тогда
			Продолжить;			
		КонецЕсли;
		Если СтрокаДТ.Ошибка Тогда 
			// не создавать печатные формы если были ошибки создания документов
			Продолжить;			
		КонецЕсли;
		Если СтрокаДТ.IDКонтрактодержателя <> 3295 Тогда
			Продолжить; // 	Контрактодержатель - не Сармант
		КонецЕсли;	
		Если ПустаяСтрока(СтрокаДТ.ОтчетАгента) ИЛИ ПустаяСтрока(СтрокаДТ.ОтчетАгентаGUID) Тогда
			Продолжить; // ОтчетАгента не создан (н-р Контрактодержатель - это не Сармант)
		КонецЕсли;	
		
		// СтрокаДТ.Пометка = Ложь; // не понятно нужно ли снимать пометку
	
		Окончание = "Отчет агента № " + ОчиститьСтрокуНаименованияНаКлиенте(СокрЛП(СтрокаДТ.НомерОтчетаАгента)) + 
		" от " +Формат(СтрокаДТ.ДатаДокументов,"ДФ=yyyy-MM-dd")+".docx";
		// ИмяВременногоФайла например "C:\Users\vchekalov\AppData\Local\Temp\3\v8_9954_e.docx"	
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("docx");
		ПолноеИмяФайлаОтчетаАгента = "";
	
		Поз = СтрНайти(ИмяВременногоФайла, "\", НаправлениеПоиска.СКонца,,1);
		Если Поз > 0 Тогда
			ПолноеИмяФайлаОтчетаАгента = Лев(ИмяВременногоФайла, Поз) + Окончание;
		Иначе
			ПолноеИмяФайлаОтчетаАгента = ИмяВременногоФайла;
		КонецЕсли;	
	
		ОбъектФайл = Новый Файл(ПолноеИмяФайлаОтчетаАгента);
		Если ОбъектФайл.Существует() Тогда
			Попытка
				УдалитьФайлы(ПолноеИмяФайлаОтчетаАгента);
				// чистая перестраховка
			Исключение
				ФайлыУспешноСозданыЗаписаны = Ложь;				
				СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Отчет агента НЕ сформирован, т.к. не удалось удалить временный файл (1).";
				СтрокаДТ.Ошибка = Истина;
				Продолжить;
			КонецПопытки;	
			ОбъектФайл = Новый Файл(ПолноеИмяФайлаОтчетаАгента);			
			Если ОбъектФайл.Существует() Тогда
				ФайлыУспешноСозданыЗаписаны = Ложь;												
				СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Отчет агента НЕ сформирован, т.к. не удалось удалить временный файл (2).";
				СтрокаДТ.Ошибка = Истина;
				Продолжить;
			КонецЕсли;	
		КонецЕсли;  // ОбъектФайл.Существует()
		ИдентификаторСтроки = СтрокаДТ.ПолучитьИдентификатор();
		ТабличныйДокумент = ПечатнаяФормаОтчетаАгентаМарлинНаСервере(ИдентификаторСтроки);
		Попытка
			ТабличныйДокумент.Записать(ПолноеИмяФайлаОтчетаАгента, ТипФайлаТабличногоДокумента.DOCX);		
		Исключение
			СтрокаДТ.Ошибка = Истина;	
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Ошибка записи печ.формы ОтчетАгента во временный файл.";
			ФайлыУспешноСозданыЗаписаны = Ложь;	
			Продолжить;
		КонецПопытки;
		
		РасширениеФайла = ".docx";
		РасширениеФайлаБезТочки = "docx";
		ИмяФайлаБезРасширения = Лев(Окончание, СтрДлина(Окончание)-5); // УдалитьРасширениеИмениФайла(ОбъектФайл.Имя, "docx");
		// в поля регистра сведений ОтчетыАгента_ПрисоединенныеФайлы:
		// - ИмяФайла - только имя файла без пути и расширения
		// - Расширение - без точки
		ОтчетАгентаСсылка = ПолучитьСсылку_ОтчетыАгента_ОтчетАгента(СтрокаДТ.ОтчетАгентаGUID);		
		
		НомерФайла = СоздатьЗапись_ОтчетыАгента_ПрисоединенныеФайлы(ОтчетАгентаСсылка, ИмяФайлаБезРасширения, РасширениеФайлаБезТочки, 
			"TEMP", Истина, Ложь,, ВернутьПеречислениеТипФайла("ОтчетАгента"));
		ПутьКФайлу = ВернутьДопКаталогПутиКФайлу(ОтчетАгентаСсылка);
		// создана запись в регистре присоединенных файлов, но сам файл еще не скопирован
		// ОтчетАгентаСсылка только что программно создан и еще не может быть открыт, оповещение не имеет смысла
		// Оповестить("РегистрСведений_ОтчетыАгента_ПрисоединенныеФайлы_ДобавленыЗаписи", ОтчетАгентаСсылка); 		
		Если НомерФайла = 0 Тогда
			СтрокаДТ.Ошибка = Истина;	
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Файл ОтчетАгента: Ошибка записи в регистр присоединенных файлов.";
			ФайлыУспешноСозданыЗаписаны = Ложь;	
			Продолжить;
		КонецЕсли;	
	
		КаталогРазмещения = ВернутьИмяБазовогоКаталогаФизическогоРазмещения() + ПутьКФайлу;
		// пример КаталогРазмещения	 "\\192.168.2.30\1c\principal_report\2022_03\12\"	Строка
		// КаталогРазмещения будет иметь "\" последним символом	
		КаталогРазмещенияПодрезанный = Лев(КаталогРазмещения, СтрДлина(КаталогРазмещения) - 1);
		Попытка
			ОбъектФайл = Новый Файл(КаталогРазмещенияПодрезанный);
			Если НЕ ОбъектФайл.Существует() Тогда
				Попытка
					СоздатьКаталог(КаталогРазмещенияПодрезанный)
				Исключение
					СтрокаДТ.Ошибка = Истина;	
					СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Файл ОтчетАгента: Ошибка создания каталога промежуточного размещения.";
					ФайлыУспешноСозданыЗаписаны = Ложь;	
					// при возникновении ошибки работы с файловой системой будут понятные сообщения об ошибке
					//Сообщить(ОписаниеОшибки());
					//ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					//Сообщить(ТекстОшибки);
					Продолжить;					
				КонецПопытки;	
			Иначе // т.е. ОбъектФайл.Существует() == Истина
				Если НЕ ОбъектФайл.ЭтоКаталог() Тогда
					СтрокаДТ.Ошибка = Истина;	
					СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Файл ОтчетАгента: Путь промежуточного размещения не является каталогом.";
					ФайлыУспешноСозданыЗаписаны = Ложь;						
					Продолжить;					
				КонецЕсли;	
			КонецЕсли;	
		Исключение
			СтрокаДТ.Ошибка = Истина;	
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Файл ОтчетАгента: Ошибка поиска/создания каталога промежуточного размещения.";
			ФайлыУспешноСозданыЗаписаны = Ложь;				
			Продолжить;
		КонецПопытки;	
	
		// скопировать файл	в общий каталог		
		КонечныйПуть = КаталогРазмещения + ВернутьПравильнуюСтрокуID(НомерФайла) + "." + СокрЛП(РасширениеФайлаБезТочки);
		Попытка
			КопироватьФайл(ПолноеИмяФайлаОтчетаАгента, КонечныйПуть);
			СтрокаДТ.Пометка = Ложь; // не понятно нужно ли снимать пометку	??		
		Исключение
			СтрокаДТ.Ошибка = Истина;	
			СтрокаДТ.Примечания = СтрокаДТ.Примечания + "Файл ОтчетАгента: Ошибка копирования файла в каталог присоединенных файлов.";
			ФайлыУспешноСозданыЗаписаны = Ложь;				
			// Продолжить; // не нужно, попытаться ниже удалить временный файл
		КонецПопытки;	

		Попытка
			УдалитьФайлы(ПолноеИмяФайлаОтчетаАгента);
		Исключение
			// ошибка удаления файла, но временный файл будет удален при закрытии сеанса
			// не буду записывать в регистр сведений ОтчетыАгента_ВременныеФайлы для последующего удаления
		КонецПопытки;
	КонецЦикла;  // перебор ЭтаФорма.ДТДляГрупповогоСоздания для создания файлов отчетов агента		
	Возврат ФайлыУспешноСозданыЗаписаны;
КонецФункции // СозданиеИЗаписьВПрисоединенныеФайлыОтчетовАгента()

&НаСервере
Функция СоздатьЗапись_ОтчетыАгента_ПрисоединенныеФайлы(ОтчетАгента, ИмяФайла, Расширение, Первоисточник, 
	ЭтоОтчетАгента = Неопределено, ЭтоФайлИзНЛ = Неопределено, ID = Неопределено, ТипФайла = Неопределено)
	// будет создана запись регистра и возвращен НомерФайла
	// реплика из Документы.ОтчетыАгента_ОтчетАгента.ФормаДокумента
	Возврат РегистрыСведений.ОтчетыАгента_ПрисоединенныеФайлы.СоздатьЗапись_ОтчетыАгента_ПрисоединенныеФайлы(ОтчетАгента, ИмяФайла, Расширение, 
		Первоисточник, ЭтоОтчетАгента, ЭтоФайлИзНЛ, ID, ТипФайла);
КонецФункции
	
&НаСервере
Функция ВернутьДопКаталогПутиКФайлу(ОтчетАгента)
	// реплика из Документы.ОтчетыАгента_ОтчетАгента.ФормаДокумента	
	Возврат Документы.ОтчетыАгента_ОтчетАгента.ПолучитьКаталогРазмещенияФайлов(ОтчетАгента);
КонецФункции

&НаСервере
Функция ВернутьИмяБазовогоКаталогаФизическогоРазмещения()
	// реплика из Документы.ОтчетыАгента_ОтчетАгента.ФормаДокумента	
	Возврат РегистрыСведений.ОтчетыАгента_ПрисоединенныеФайлы.ВернутьИмяБазовогоКаталогаФизическогоРазмещения();
КонецФункции	

&НаСервере
Функция ВернутьПеречислениеТипФайла(ИмяТипаФайла)
	//"АктПриемаПередачи", "АктОбОказанииУслуг", "СчетФактура", "СчетНаОплату", "ПлатежноеПоручение", "БанковскийОрдер", "ОтчетКомитенту",
	//"ТоварнаяНакладная", "ОтчетАгента", "Прочее", "БухгалтерскаяСправка"
	// реплика из Документы.ОтчетыАгента_ОтчетАгента.ФормаДокумента	
	Попытка
		Возврат Перечисления.ОтчетыАгента_ТипФайла[ИмяТипаФайла];
	Исключение
		Возврат Перечисления.ОтчетыАгента_ТипФайла.ПустаяСсылка();
	КонецПопытки;	
КонецФункции	

&НаКлиенте  
Функция ОчиститьСтрокуНаименованияНаКлиенте(ИсходнаяСтрока)
	// для кодовой страницы cp866 нужно чистить 160, 159, 9, 10, 13	
	// очистка имени файла
	РезСтрока = СтрЗаменить(ИсходнаяСтрока, Символы.ВК ," ");
	РезСтрока = СтрЗаменить(РезСтрока, Символы.ПС ," ");
	РезСтрока = СтрЗаменить(РезСтрока, Символы.Таб  ," ");
	РезСтрока = СтрЗаменить(РезСтрока, Символы.ВТаб ," ");
	РезСтрока = СтрЗаменить(РезСтрока, Символы.НПП ," ");
	РезСтрока = СтрЗаменить(РезСтрока, Символы.ПФ ," ");
	РезСтрока = СтрЗаменить(РезСтрока, "&" ," ");
	РезСтрока = СтрЗаменить(РезСтрока, "'" ," ");
	РезСтрока = СтрЗаменить(РезСтрока, """" ," ");
	РезСтрока = СтрЗаменить(РезСтрока, "  " ," ");
	РезСтрока = СтрЗаменить(РезСтрока, ">" ," "); // часть строки JSON
	РезСтрока = СтрЗаменить(РезСтрока, "<" ," ");	
	// используется и для получения допустимого имени файла
	РезСтрока = СтрЗаменить(РезСтрока, "/" ," ");		
	РезСтрока = СтрЗаменить(РезСтрока, "\" ," ");			
	РезСтрока = СтрЗаменить(РезСтрока, "*" ," ");				
	Возврат СокрЛП(РезСтрока);
КонецФункции  // ОчиститьСтрокуНаименованияНаКлиенте(ИсходнаяСтрока)

&НаСервере
Функция ПечатнаяФормаОтчетаАгентаМарлинНаСервере(ИдентификаторСтроки)
	ТекДанные = ЭтаФорма.ДТДляГрупповогоСоздания.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	КонтрактодержательЭтоСармант = Ложь; // выше создана только Операция и НЕ СОЗДАНЫ ПоступлениеТоваровУслуг,
	// ОтчетКомитентуОПродажах, ПередачаТоваров, ОтчетАгента (самописные не типовой документ)
	Если ТекДанные.IDКонтрактодержателя = 3295 Тогда
		КонтрактодержательЭтоСармант = Истина; // выше должен быть создан полный комплект документов 1с
	КонецЕсли;	
	
	// ТекДанные = ЭтаФорма.ДТДляГрупповогоСоздания[2]; // для целей тестирования
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.РазмерСтраницы = "A4";
	ТабличныйДокумент.АвтоМасштаб			= Истина;  // == ПоШиринеСтраницы
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОтчетАгента_Марлин";
	ТабличныйДокумент.ПолеСверху = 10;
	ТабличныйДокумент.ПолеСлева = 10;
	ТабличныйДокумент.ПолеСправа = 10;
	ТабличныйДокумент.ПолеСнизу = 10;
	ТабличныйДокумент.РазмерКолонтитулаСверху = 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу = 0;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет; // .Ландшафт;
	ПервыйДокумент = Истина;

//	МассивПроверкиПостраничногоВывода = Новый Массив;
//  пока некогда связываться с корректным постраничным выводом и итогами по страницам
//  например как в печатной форме СпецификацияДляДоговораБ2

	МакетОбработки = ЭтаФорма.РеквизитФормыВЗначение("Объект").ПолучитьМакет("МакетМарлин");		

	ОбластьПустаяСтрока = МакетОбработки.ПолучитьОбласть("ПустаяСтрока");			
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);		
	
	ОбластьШапка = МакетОбработки.ПолучитьОбласть("Шапка");	
	ОбластьШапка.Параметры.НомерОтчетаАгента = ТекДанные.НомерОтчетаАгента;
	ОбластьШапка.Параметры.ДатаОтчетаАгентаСтрока = Формат(ТекДанные.ДатаДокументов, "ДЛФ=DD");
	ТабличныйДокумент.Вывести(ОбластьШапка);
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);			
	
	Агент_Наименование = 
		?(ПустаяСтрока(ЭтаФорма.Организация.НаименованиеПолное), ЭтаФорма.Организация.Наименование, ЭтаФорма.Организация.НаименованиеПолное);
	Агент_Наименование_Кратко = 
		?(ПустаяСтрока(ЭтаФорма.Организация.Наименование), ЭтаФорма.Организация.НаименованиеПолное, ЭтаФорма.Организация.Наименование);	
		
	Агент_Должность_Руководителя = ""; // в родительном падеже 
	Если НЕ ПустаяСтрока(ТекДанные.Договор.ДолжностьРуководителя.Наименование) Тогда
		Агент_Должность_Руководителя = НРег(СклонениеПредставленийОбъектов.ПросклонятьПредставление(ТекДанные.Договор.ДолжностьРуководителя.Наименование, 2));
	Иначе
		Агент_Должность_Руководителя = "генерального директора";
	КонецЕсли;	
		
	Агент_Руководитель = "???????????"; // в родительном падеже 
	Агент_Руководитель_Кратко = "????????????"; 	
	Если НЕ ПустаяСтрока(ТекДанные.Договор.Руководитель.Наименование) Тогда
		Агент_Руководитель = СклонениеПредставленийОбъектов.ПросклонятьПредставление(ТекДанные.Договор.Руководитель.Наименование, 2);
		Агент_Руководитель_Кратко = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ТекДанные.Договор.Руководитель.ФИО);
	КонецЕсли;	
	
	Агент_Действующего = "действующего";
	Если ТекДанные.Договор.Руководитель.Пол = Перечисления.ПолФизическогоЛица.Женский Тогда
		Агент_Действующего = "действующей";
	КонецЕсли;
	
	// с 10.12.18 Калинина Е.А. попросила, чтобы всегда подписывал Ушаков О.А., а не представитель организации из договора
	Агент_Руководитель =  "Ушакова Олега Алексеевича";
	Агент_Руководитель_Кратко = "Ушаков О.А.";		
	Агент_Действующего = "действующего";	
	
	МодКонтрНаименование = ?(ПустаяСтрока(ЭтаФорма.Контрагент1C.НаименованиеПолное),ЭтаФорма.Контрагент1C.Наименование,ЭтаФорма.Контрагент1C.НаименованиеПолное);
	Если ВРЕГ(Лев(МодКонтрНаименование ,4)) = "ООО " Тогда
		МодКонтрНаименование = СокрЛП(Сред(МодКонтрНаименование, 5));	
		ЛевСимвол = Лев(МодКонтрНаименование,1); 
		Если ЛевСимвол <> """" Тогда
			МодКонтрНаименование = """" + МодКонтрНаименование;
		КонецЕсли;	
		ПравСимвол = Прав(МодКонтрНаименование, 1);
		Если ПравСимвол <> """" Тогда
			МодКонтрНаименование = МодКонтрНаименование + """";
		КонецЕсли;	
		МодКонтрНаименование = "Общество с ограниченной ответственностью " + МодКонтрНаименование;				
	КонецЕсли;	
	Принципал_Наименование = МодКонтрНаименование;
		
	МодКонтрНаименование = ?(ПустаяСтрока(ЭтаФорма.Контрагент1C.Наименование),ЭтаФорма.Контрагент1C.НаименованиеПолное,ЭтаФорма.Контрагент1C.Наименование);	
	Если ВРег(Прав(МодКонтрНаименование,4)) = " ООО" Тогда
		// преобразование странно подрезанных наименований типа ВИТАНД" ООО в нормальное ООО "ВИТАНД"
		МодКонтрНаименование = СокрЛП(Лев(МодКонтрНаименование, СтрДлина(МодКонтрНаименование) - 4));
		ЛевСимвол = Лев(МодКонтрНаименование,1); 
		Если ЛевСимвол <> """" Тогда
			МодКонтрНаименование = """" + МодКонтрНаименование;
		КонецЕсли;	
		ПравСимвол = Прав(МодКонтрНаименование, 1);
		Если ПравСимвол <> """" Тогда
			МодКонтрНаименование = МодКонтрНаименование + """";
		КонецЕсли;	
		МодКонтрНаименование = "ООО " + МодКонтрНаименование;		
	КонецЕсли;	
	Принципал_Наименование_Кратко = МодКонтрНаименование;		
		
	Принципал_Должность_Руководителя = "???????????"; // в родительном падеже
	Если НЕ ПустаяСтрока(ТекДанные.Договор.ДолжностьРуководителяКонтрагента) Тогда
		Принципал_Должность_Руководителя = НРег(СклонениеПредставленийОбъектов.ПросклонятьПредставление(ТекДанные.Договор.ДолжностьРуководителяКонтрагента, 2));
	Иначе
		Принципал_Должность_Руководителя = "генерального директора";
	КонецЕсли;	
		
	Принципал_Руководитель = "??????????";		
	Принципал_Руководитель_Кратко = "??????????";		
	// Объект.Договор.ДолжностьРуководителя, Объект.Договор.ДолжностьРуководителяКонтрагента
	Если НЕ ПустаяСтрока(ТекДанные.Договор.РуководительКонтрагента) Тогда
		Принципал_Руководитель = СклонениеПредставленийОбъектов.ПросклонятьПредставление(ТекДанные.Договор.РуководительКонтрагента,2);
		Принципал_Руководитель_Кратко = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(ТекДанные.Договор.РуководительКонтрагента);		
	КонецЕсли;			
	
	Принципал_Действующего = "действующего";
	Если ТекДанные.Договор.ПолРуководителяКонтрагента = Перечисления.ПолФизическогоЛица.Женский Тогда
		Принципал_Действующего = "действующей";
	КонецЕсли;
	
	СведенияОбАгенте = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ЭтаФорма.Организация, ТекДанные.ДатаДокументов, 
		ЭтаФорма.ОрганизацияБанковскийСчет);
		
	Агент_ЮридическийАдрес = "Юридический адрес: "+СведенияОбАгенте.ЮридическийАдрес;
	
	// ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(..., "ОГРН") выдает в виде строки цифр без ОГРН спереди 
	Агент_ИНН_КПП_ОГРН_ОКПО = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбАгенте, "ИНН,КПП") + 
		?(ПустаяСтрока(СведенияОбАгенте.ОГРН), "", ", ОГРН " + СведенияОбАгенте.ОГРН) +
		?(ПустаяСтрока(СведенияОбАгенте.КодПоОКПО),"",", ОКПО " + СведенияОбАгенте.КодПоОКПО);
	
	Агент_Счет_Банк_БИК_КоррСчет = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбАгенте, "НомерСчета,Банк,БИК,КоррСчет");		
	Агент_Телефоны_EMail = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбАгенте, "Телефоны,EMail");
	
	СведенияОбПринципале = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ЭтаФорма.Контрагент1C, ТекДанные.ДатаДокументов, 
		ЭтаФорма.КонтрагентБанковскийСчет);
	Принципал_ЮридическийАдрес = "Юридический адрес: "+СведенияОбПринципале.ЮридическийАдрес;		
	
	Принципал_ИНН_КПП_ОГРН_ОКПО = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбПринципале, "ИНН,КПП") + 
		?(ПустаяСтрока(СведенияОбПринципале.ОГРН), "", ", ОГРН " + СведенияОбПринципале.ОГРН) +
		?(ПустаяСтрока(СведенияОбПринципале.КодПоОКПО),"",", ОКПО " + СведенияОбПринципале.КодПоОКПО);
		
	Принципал_Счет_Банк_БИК_КоррСчет = 
		ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбПринципале, "НомерСчета,Банк,БИК,КоррСчет");		
	Принципал_Телефоны_EMail = 				
		ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбПринципале, "Телефоны,EMail");
	
	ОбластьСтрока = МакетОбработки.ПолучитьОбласть("Строка");			
	ОбластьСтрока.Параметры.ТекстСтроки = 
Агент_Наименование + ",  именуемое  далее «Агент», в лице " + Агент_Должность_Руководителя + " " + Агент_Руководитель + 
", " + Агент_Действующего + " на основании Устава, представляет, а " + Принципал_Наименование + ", именуемое  далее «Принципал», в лице " + 
Принципал_Должность_Руководителя + " " + Принципал_Руководитель + ", " + Принципал_Действующего + " на основании Устава, " + 
"вместе далее именуемые «Стороны», принимает настоящий отчет об исполнении агентского поручения (далее – «Отчет») по " + 
"Агентскому договору № " + СокрЛП(ТекДанные.Договор.Номер) + " от " + Формат(ТекДанные.Договор.Дата, "ДФ=dd.MM.yyyy") + " (далее – «Договор»)"; 
	ТабличныйДокумент.Вывести(ОбластьСтрока);	
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);		
	
	ОбластьСтрокаСОтступом = МакетОбработки.ПолучитьОбласть("СтрокаСОтступом");
	ОбластьСтрокаСОтступом.Параметры.НумераторСтрокиСОтступом = "1.";
	ОбластьСтрокаСОтступом.Параметры.ТекстСтрокиСОтступом = 
"По Договору Агент по поручению Принципала от своего имени и за счет Принципала обязуется осуществлять все действия, " + 
"необходимые для приобретения Товаров, в том числе за границей Российской Федерации, их экспорта из страны приобретения, " + 
"транспортировки, ввоза на территорию Российской Федерации, таможенной очистки и передачи Товаров Принципалу";
	ТабличныйДокумент.Вывести(ОбластьСтрокаСОтступом);	
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);		
	
	ОбластьСтрокаСОтступом = МакетОбработки.ПолучитьОбласть("СтрокаСОтступом");
	ОбластьСтрокаСОтступом.Параметры.НумераторСтрокиСОтступом = "2.";
	ОбластьСтрокаСОтступом.Параметры.ТекстСтрокиСОтступом = 
"В соответствии с п.п.1.1, 2.1 Договора и Поручением Агент обязался совершить следующие действия необходимые для " + 
"приобретения, доставки, таможенной очистки и передачи Принципалу товары, а именно:";
	ТабличныйДокумент.Вывести(ОбластьСтрокаСОтступом);	
	
	ОбластьСтрокаСОтступом = МакетОбработки.ПолучитьОбласть("СтрокаСОтступом");
	ОбластьСтрокаСОтступом.Параметры.НумераторСтрокиСОтступом = "";
	ОбластьСтрокаСОтступом.Параметры.ТекстСтрокиСОтступом = 
"-	организовать приобретение Товара;" + Символы.ПС + 
"-	организовать таможенную очистку Товара;" + Символы.ПС + 
"-	организовать передачу Товара Принципалу. ";
	ТабличныйДокумент.Вывести(ОбластьСтрокаСОтступом);	
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);		
	
	ОбластьСтрокаСОтступом = МакетОбработки.ПолучитьОбласть("СтрокаСОтступом");
	ОбластьСтрокаСОтступом.Параметры.НумераторСтрокиСОтступом = "3.";
	ОбластьСтрокаСОтступом.Параметры.ТекстСтрокиСОтступом = 
"Агентом за отчетный промежуток времени были совершены следующие действия:" + Символы.ПС + 
"а) организовано приобретение Товара;" + Символы.ПС + 
"б) организована таможенная очистка Товара;" + Символы.ПС + 
"в) организована передача Товара Принципалу в месте и в срок, согласованный Сторонами в Поручении." + Символы.ПС + 
"Таким образом, Агентом выполнены обязательства, обусловленные Договором и Поручением.";
	ТабличныйДокумент.Вывести(ОбластьСтрокаСОтступом);	
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);		
	
	ОбластьСтрокаСОтступом = МакетОбработки.ПолучитьОбласть("СтрокаСОтступом");
	ОбластьСтрокаСОтступом.Параметры.НумераторСтрокиСОтступом = "4.";
	ОбластьСтрокаСОтступом.Параметры.ТекстСтрокиСОтступом =
"В соответствии с п. 3.1. Договора Принципал обязан в полном объеме компенсировать Агенту расходы, " +
"понесенные последним в связи с исполнением Поручений Принципала. При выполнении Поручения Агент понес следующие расходы:";	
	ТабличныйДокумент.Вывести(ОбластьСтрокаСОтступом);	
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);		
	
// таблица	
	СуммаВсегоВРуб = ТекДанные.Сумма1010 + ТекДанные.Сумма2010 + ТекДанные.Сумма5010;	
	ОбластьТаблица = Неопределено;
	Если КонтрактодержательЭтоСармант Тогда
		ОбластьТаблица = МакетОбработки.ПолучитьОбласть("Таблица");	
	Иначе
		ОбластьТаблица = МакетОбработки.ПолучитьОбласть("ТаблицаМин");			
	КонецЕсли;	
	ОбластьТаблица.Параметры.Сумма1010 = Формат(ТекДанные.Сумма1010, "ЧДЦ=2");	
	ОбластьТаблица.Параметры.Сумма2010 = Формат(ТекДанные.Сумма2010, "ЧДЦ=2");	
	ОбластьТаблица.Параметры.Сумма5010 = Формат(ТекДанные.Сумма5010, "ЧДЦ=2");		
	ОбластьТаблица.Параметры.СуммаВсегоВРуб = Формат(СуммаВсегоВРуб, "ЧДЦ=2") + " РУБ";	
	
	Если КонтрактодержательЭтоСармант Тогда
		ОбластьТаблица.Параметры.СуммаПокупки = Формат(ТекДанные.СуммаПокупки, "ЧДЦ=2");		
		ОбластьТаблица.Параметры.ВалютаПокупки = Строка(ТекДанные.ВалютаПокупки);
		ОбластьТаблица.Параметры.ВсегоСуммаПокупки = Формат(ТекДанные.СуммаПокупки, "ЧДЦ=2") + " " + Строка(ТекДанные.ВалютаПокупки);		
	КонецЕсли;	
	ТабличныйДокумент.Вывести(ОбластьТаблица);	
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);			

	ОбластьСтрока = МакетОбработки.ПолучитьОбласть("Строка");			
	ОбластьСтрока.Параметры.ТекстСтроки =
"Указанные выше суммы подлежат перечислению на расчетный счет Агента в Рублях.";	
	ТабличныйДокумент.Вывести(ОбластьСтрока);	
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);		

	ОбластьСтрока = МакетОбработки.ПолучитьОбласть("Строка");			
	ОбластьСтрока.Параметры.ТекстСтроки =
"За исполнение Поручения (включая суммы понесенных расходов и сумму агентского вознаграждения) на расчетный счет " +
"Агента подлежит перечислению " + Формат(СуммаВсегоВРуб, "ЧДЦ=2")+ " руб. (" + 
ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(СуммаВсегоВРуб, Справочники.Валюты.НайтиПоКоду("643"))	+ ").";	
	ТабличныйДокумент.Вывести(ОбластьСтрока);	
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);		
	
	ОбластьСтрока = МакетОбработки.ПолучитьОбласть("Строка");			
	ОбластьСтрока.Параметры.ТекстСтроки =
"Приложение (копии документов, подтверждающие факт выполнения Поручения):";	
	ТабличныйДокумент.Вывести(ОбластьСтрока);	

	ОбластьСтрока = МакетОбработки.ПолучитьОбласть("Строка");			
	ОбластьСтрока.Параметры.ТекстСтроки = "Декларация на товары № " + ТекДанные.НомерДТ;	
	ТабличныйДокумент.Вывести(ОбластьСтрока);	

	Если КонтрактодержательЭтоСармант Тогда
		ОбластьСтрока = МакетОбработки.ПолучитьОбласть("Строка");			
		ОбластьСтрока.Параметры.ТекстСтроки = "Инвойс № " + ТекДанные.СтрокаИнвойсов;	
		ТабличныйДокумент.Вывести(ОбластьСтрока);	
		
		ОбластьСтрока = МакетОбработки.ПолучитьОбласть("Строка");			
		ОбластьСтрока.Параметры.ТекстСтроки = "Акт приема-передачи № " + 
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ТекДанные.ПередачаТоваров.Номер, Истина, Истина) + 
			" от " + Формат(ТекДанные.ПередачаТоваров.Дата, "ДФ=dd.MM.yyyy");
		ТабличныйДокумент.Вывести(ОбластьСтрока);	
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);		
	
	ОбластьСтрока = МакетОбработки.ПолучитьОбласть("Строка");			
	ОбластьСтрока.Параметры.ТекстСтроки = "Отчет принят: : «______» ______________________ 20 _____ г. ";
	ТабличныйДокумент.Вывести(ОбластьСтрока);	
	
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);			

	ОбластьПодвал = МакетОбработки.ПолучитьОбласть("Подвал");	
	ОбластьПодвал.Параметры.Агент_Наименование_Кратко = Агент_Наименование_Кратко;
	ОбластьПодвал.Параметры.Агент_ЮридическийАдрес = Агент_ЮридическийАдрес;	
	ОбластьПодвал.Параметры.Агент_ИНН_КПП_ОГРН_ОКПО = Агент_ИНН_КПП_ОГРН_ОКПО;
	ОбластьПодвал.Параметры.Агент_Счет_Банк_БИК_КоррСчет = Агент_Счет_Банк_БИК_КоррСчет;
	ОбластьПодвал.Параметры.Агент_Телефоны_EMail = Агент_Телефоны_EMail;			
	ОбластьПодвал.Параметры.Агент_Руководитель_Кратко = Агент_Руководитель_Кратко;
	ОбластьПодвал.Параметры.Принципал_Наименование_Кратко = Принципал_Наименование_Кратко;
	ОбластьПодвал.Параметры.Принципал_ЮридическийАдрес = Принципал_ЮридическийАдрес;	
	ОбластьПодвал.Параметры.Принципал_ИНН_КПП_ОГРН_ОКПО = Принципал_ИНН_КПП_ОГРН_ОКПО;
	ОбластьПодвал.Параметры.Принципал_Счет_Банк_БИК_КоррСчет = Принципал_Счет_Банк_БИК_КоррСчет;
	ОбластьПодвал.Параметры.Принципал_Телефоны_EMail = Принципал_Телефоны_EMail;
	ОбластьПодвал.Параметры.Принципал_Руководитель_Кратко = Принципал_Руководитель_Кратко;	
	ТабличныйДокумент.Вывести(ОбластьПодвал);
	
	Возврат ТабличныйДокумент;
КонецФункции  // ПечатнаяФормаОтчетаАгентаМарлинНаСервере()	

#КонецОбласти // ПечатныеФормы

#Область ОКОП_Комиссионное_вознаграждение

&НаКлиенте
Процедура ВтчКомиссионноеВознаграждениеПриИзменении(Элемент)
	ЭтаФорма.Элементы.КомиссионноеВознагражениеСумма.Доступность = ЭтаФорма.ВтчКомиссионноеВознаграждение;	
	ЭтаФорма.Элементы.КомиссионноеВознагражениеСумма.ТолькоПросмотр = НЕ ЭтаФорма.ВтчКомиссионноеВознаграждение;
	ЭтаФорма.Элементы.УслугаПоВознаграждению.Доступность = ЭтаФорма.ВтчКомиссионноеВознаграждение;	
	ЭтаФорма.Элементы.УслугаПоВознаграждению.ТолькоПросмотр = НЕ ЭтаФорма.ВтчКомиссионноеВознаграждение;
	ЭтаФорма.Элементы.СчетУчетаДоходов.Доступность = ЭтаФорма.ВтчКомиссионноеВознаграждение;
	ЭтаФорма.Элементы.СчетУчетаДоходов.ТолькоПросмотр = НЕ ЭтаФорма.ВтчКомиссионноеВознаграждение;
	ЭтаФорма.Элементы.СчетУчетаНДС.Доступность = ЭтаФорма.ВтчКомиссионноеВознаграждение;	
	ЭтаФорма.Элементы.СчетУчетаНДС.ТолькоПросмотр = НЕ ЭтаФорма.ВтчКомиссионноеВознаграждение;
	ЭтаФорма.Элементы.СтавкаНДСВознагражения.Доступность = ЭтаФорма.ВтчКомиссионноеВознаграждение;	
	ЭтаФорма.Элементы.СтавкаНДСВознагражения.ТолькоПросмотр = НЕ ЭтаФорма.ВтчКомиссионноеВознаграждение;	
КонецПроцедуры

#КонецОбласти // ОКОП_Комиссионное_вознаграждение

#Область ПередачаДанныхИзОперацииВозмещенияТаможПлатежейПоФайлуExcel
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)                     
	// см. обработку ОперацииВозмещенияТаможПлатежейПоФайлуExcel   ПодготовитьДанныеДляОтправкиНаСервере()
	Если ИмяСобытия <> "ПередачаДанных_ОперацииВозмещенияТаможПлатежейПоФайлуExcel_ГрупповоеСозданиеОтчетовАгентаНаОсновеДТ" Тогда 
		Возврат;
	КонецЕсли;
	
	// Параметр - это Структура с полями МассивВозврата, МассивВозвратаИтоги, МассивВозвратаНомерДТОперация (каждый элемент это Структура с полями НомерДТ, Операция)	
	Если Параметр.МассивВозврата.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Объект.ПереданныеДанные.Очистить(); 
	Объект.ПереданныеДанныеИтоги.Очистить(); 
	Объект.ПереданныеНомерДТОперация.Очистить();
	Для каждого СтрокаПараметра из Параметр.МассивВозврата Цикл
		СтрокаПД = Объект.ПереданныеДанные.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПД, СтрокаПараметра);
	КонецЦикла;	                
	Для каждого СтрокаПараметра из Параметр.МассивВозвратаИтоги Цикл
		СтрокаПД = Объект.ПереданныеДанныеИтоги.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПД, СтрокаПараметра);
	КонецЦикла;	                
	Для каждого СтрокаПараметра из Параметр.МассивВозвратаНомерДТОперация Цикл
		СтрокаПД = Объект.ПереданныеНомерДТОперация.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПД, СтрокаПараметра);		
	КонецЦикла;	
	
	ДополнительнаяОбработкаПолученныхДанных();
	
	ЭтаФорма.ОбновитьОтображениеДанных();
	ЭтаФорма.Элементы.ГруппаСтраницы.ТекущаяСтраница = ЭтаФорма.Элементы.СтраницаПереданныеДанные;
	
КонецПроцедуры  // ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

&НаКлиенте
Функция ВернутьТекстЗапросаДанныеКлиентовЗаказовПоIDДТ(СтрокаIDДТ)
	//в декларации на закладке грузовые единицы могут быть грузовые единицы из декларации у которых не указан номер груз ед из заказа
	// для таких грузовых единиц list_order.id_company_client (или id_company_principal) будет NULL
	// Для принципала: COALESCE — если id_company_principal пуст (NULL/0), берём id_company_client.
	// Покрывает прямые контракты (ПП), где принципал = клиент заказа.
	// ОТКЛЮЧЕНО 26.02.2026: см. комментарий в ПолучитьТекстЗапроса_ЗаполнитьСписокДТ
	//Если ЭтаФорма.ИспользоватьПринципала Тогда
	//	ВыражениеКомпании = "COALESCE(NULLIF(list_order.id_company_principal, 0), list_order.id_company_client)";
	//Иначе
		ВыражениеКомпании = "list_order.id_company_client";
	//КонецЕсли;
Возврат "
|SELECT distinct
|   " + ВыражениеКомпании + " as id_company_value,
|   COALESCE(case
|	        when list_company.list_company_name IS NULL then list_company.list_company_name_small
|	        else list_company.list_company_name
|	        end,'') as contractor_name,
|   COALESCE(list_company.inn,'') as contractor_inn
|FROM public.list_gtd list_gtd
|   LEFT JOIN declaration.unit AS dec_unit ON dec_unit.id_list_gtd = list_gtd.id
|   LEFT JOIN declaration.dec_unit_oper_unit_link AS link ON link.id_declaration_unit = dec_unit.id
|   LEFT JOIN operation.unit unit ON link.id_operation_unit = unit.id
|   LEFT JOIN public.order list_order ON unit.id_order = list_order.id
|   LEFT JOIN public.list_company on " + ВыражениеКомпании + " = list_company.id
|WHERE
|   list_gtd.deleted = 0
|   and list_gtd.date_recall is NULL -- дата отзыва
|   and list_gtd.date_refuse is NULL -- дата отказа в выпуске
|   and list_gtd.gtd_number is not null
|   and " + ВыражениеКомпании + " is not null
|   and list_gtd.id in
|( " + СтрокаIDДТ + " )  ";
КонецФункции	// ВернутьТекстЗапросаДанныеКлиентовЗаказовПоIDДТ(СтрокаIDДТ)

&НаКлиенте
Процедура ДополнительнаяОбработкаПолученныхДанных();
	// в Объект.ПереданныеДанныеИтоги заполнить поля IDКонтрагентаЗаказа, НаименованиеКонтрагентаНЛ, СтрокаIDДТ 
	ConRSCom = ВернутьСтруктуруConnectionRecordSetCommand();  // Connection, RecordSet, Command;
	Если ConRSCom = Неопределено Тогда
		Сообщить("Дальнейшая обработка переданных данных невозможна.");
		Возврат; // сообщения об ошибках уже выведены
	КонецЕсли;	

	МассивБудетДобавлено = Новый Массив;
	Для каждого СтрокаИтоги из Объект.ПереданныеДанныеИтоги Цикл
		СтрокаIDДТ = "";
		ОбработаноДТ = 0;
		
		СтруктураПоиска = Новый Структура("Контрагент, Договор, Организация", 
			СтрокаИтоги.Контрагент, СтрокаИтоги.Договор, СтрокаИтоги.Организация);
		НайденныеСтрокиПД = Объект.ПереданныеДанные.НайтиСтроки(СтруктураПоиска);		
		Для каждого СтрокаПД из НайденныеСтрокиПД Цикл
			СтрокаIDДТ = СтрокаIDДТ + "," + ВернутьПравильнуюСтрокуID(СтрокаПД.IDДТ);
			ОбработаноДТ = ОбработаноДТ + 1;
			Если ОбработаноДТ / 11 = Цел(ОбработаноДТ / 11) Тогда
				СтрокаIDДТ = СтрокаIDДТ + Символы.ПС;
			КонецЕсли;	
		КонецЦикла;	
		СтрокаIDДТ = Сред(СтрокаIDДТ, 2);
		Если ПустаяСтрока(СтрокаIDДТ) Тогда
			Продолжить;
		КонецЕсли;	
		// запросы в цикле - это плохо
		Попытка    
			ConRSCom.Command.CommandText = ВернутьТекстЗапросаДанныеКлиентовЗаказовПоIDДТ(СтрокаIDДТ); 
			ConRSCom.RecordSet = ConRSCom.Command.Execute();    //  Данные получены
		Исключение    
			Сообщить("Ошибка поиска контрагентов заказов по  ##ДТ в базе Логос"); 
			Сообщить(ОписаниеОшибки());				
			ЗакрытьПодключениеКPostgres(ConRSCom);				
			Возврат;
		КонецПопытки;		
		// для одной кучки ДТ может быть несколько контрагентов, тогда придется добавлять строки
		// в Объект.ПереданныеДанныеИтоги для элементов МассивКонтрагентовОднойКучкиДТ начиная со второго
		МассивКонтрагентовОднойКучкиДТ = Новый Массив;
		Пока НЕ ConRSCom.RecordSet.EOF Цикл 
			НаименованиеКонтрагентаНЛ = ConRSCom.RecordSet.fields("contractor_name").Value + 
				" ИНН " + ConRSCom.RecordSet.fields("contractor_inn").Value;		
			IDКонтрагентаЗаказа = ConRSCom.RecordSet.fields("id_company_value").Value;
			МассивКонтрагентовОднойКучкиДТ.Добавить(Новый Структура("НаименованиеКонтрагентаНЛ, IDКонтрагентаЗаказа", 
				НаименованиеКонтрагентаНЛ, IDКонтрагентаЗаказа));
			ConRSCom.RecordSet.MoveNext();
		КонецЦикла; 
		ConRSCom.RecordSet.Close();    // RecordSet.State = 1 == Open	
		
		НомерЭлемента = 1;
		Для каждого ЭлементКучки из МассивКонтрагентовОднойКучкиДТ Цикл
			Если НомерЭлемента = 1 Тогда
				СтрокаИтоги.IDКонтрагентаЗаказа = ЭлементКучки.IDКонтрагентаЗаказа;				
				СтрокаИтоги.НаименованиеКонтрагентаНЛ = ЭлементКучки.НаименованиеКонтрагентаНЛ;
				СтрокаИтоги.СтрокаIDДТ = СтрокаIDДТ;				
			Иначе                   
				СтрокаБудетДобавлено = Новый Структура("Контрагент, Договор, Организация, IDКонтрагентаЗаказа, " + 
					"НаименованиеКонтрагентаНЛ, СтрокаIDДТ", 
					СтрокаИтоги.Контрагент, СтрокаИтоги.Договор, СтрокаИтоги.Организация,
					ЭлементКучки.IDКонтрагентаЗаказа, ЭлементКучки.НаименованиеКонтрагентаНЛ, СтрокаIDДТ); 
				МассивБудетДобавлено.Добавить(СтрокаБудетДобавлено);
			КонецЕсли;	
			НомерЭлемента = НомерЭлемента + 1;
		КонецЦикла;
	КонецЦикла;	                
	ЗакрытьПодключениеКPostgres(ConRSCom);
	
	Если МассивБудетДобавлено.Количество() > 0 Тогда
		Для каждого СтрокаБудетДобавлено из МассивБудетДобавлено Цикл
			СтрокаИтоги = Объект.ПереданныеДанныеИтоги.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаИтоги, СтрокаБудетДобавлено);
		КонецЦикла;	
		Объект.ПереданныеДанныеИтоги.Сортировать("Контрагент Возр, Договор Возр, Организация Возр");
	КонецЕсли;	

КонецПроцедуры  // ДополнительнаяОбработкаПолученныхДанных()

&НаКлиенте
Процедура ОбъектПереданныеДанныеИтогиПриАктивизацииСтроки(Элемент)
	ТекДанные = ЭтаФорма.Элементы.ОбъектПереданныеДанныеИтоги.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	ЭтаФорма.Элементы.ОбъектПереданныеДанные.ОтборСтрок = 
		Новый ФиксированнаяСтруктура("Контрагент, Договор, Организация", 
			ТекДанные.Контрагент, ТекДанные.Договор, ТекДанные.Организация);
КонецПроцедуры  // ОбъектПереданныеДанныеИтогиПриАктивизацииСтроки(Элемент)

&НаКлиенте
Процедура ОбработатьПереданныеДанные(Команда)
	ОчиститьСообщения();
	//Если (ЭтаФорма.НачальнаяДата = Дата(1,1,1)) ИЛИ (ЭтаФорма.КонечнаяДата = Дата(1,1,1)) ИЛИ
	//	(ЭтаФорма.НачальнаяДата > ЭтаФорма.КонечнаяДата) Тогда
	//	Сообщить("Проверьте даты");
	//	Возврат;
	//КонецЕсли;	
	//Если ЭтаФорма.IDКлиентаЗаказа = 0 Тогда
	//	Сообщить("Укажите #клиента заказа");
	//	Возврат;
	//КонецЕсли;	
	//Если НЕ ЗначениеЗаполнено(ЭтаФорма.Организация) ИЛИ НЕ ЗначениеЗаполнено(ЭтаФорма.Контрагент1C) Тогда
	//	Сообщить("Укажите организацию и контрагента 1С");
	//	Возврат;
	//КонецЕсли;	
	Если НЕ ЗначениеЗаполнено(ЭтаФорма.ГруппаПоискаПоставщика) Тогда
		Сообщить("Укажите группу контрагентов для поиска поставщика в 1С (закладка 'Главное')");
		Возврат;
	КонецЕсли;              
	Если Объект.ПереданныеДанныеИтоги.Количество() = 0 Тогда
		Сообщить("Таблицы переданных данных пусты.");
		Возврат;
	КонецЕсли;	
	
	КилограммСсылка = НайтиКилограммНаСервере();			
	Если НЕ ЗначениеЗаполнено(КилограммСсылка) Тогда
		Сообщить("В базе 1С не найдена единица измерения с кодом 166 (килограмм). Обработка прервана.");
		Возврат;
	КонецЕсли;
	
	ОбработанаОднаСтрока = Ложь;
	ОсталосьОбработать = 0;
	Для каждого СтрокаИтоги из Объект.ПереданныеДанныеИтоги Цикл
		Если НЕ СтрокаИтоги.Обработано Тогда
			Если НЕ ОбработанаОднаСтрока Тогда  

				ЭтаФорма.IDКлиентаЗаказа = СтрокаИтоги.IDКонтрагентаЗаказа;
				ЭтаФорма.НаименованиеКонтрагентаНЛ = СтрокаИтоги.НаименованиеКонтрагентаНЛ;  
				ЭтаФорма.Организация = СтрокаИтоги.Организация;
				ЭтаФорма.Контрагент1C = СтрокаИтоги.Контрагент;
				ЭтаФорма.ДоговорКонтрагент1СПоУмолчанию = СтрокаИтоги.Договор;
				ЭтаФорма.КонтрагентБанковскийСчет = ПредопределенноеЗначение("Справочник.БанковскиеСчета.ПустаяСсылка");
				// нужно проверить что ОрганизацияБанковскийСчет принадлежит Организация
				// нужно заполнить КонтрагентБанковскийСчет н-р как ЭтаФорма.Контрагент1C.ОсновнойБанковскийСчет 
				ПроверитьЗаполнитьБанковскиеСчетаНаСервере();
				ЗаполнитьСписокДТФактическоеВыполнение("ЗакладкаПереданныеДанные", КилограммСсылка, СтрокаИтоги.СтрокаIDДТ);
				СтрокаИтоги.Обработано = Истина;
				ОбработанаОднаСтрока = Истина; 
				// чтобы не выдумывать и не переписывать уже существующий функционал
				УстановитьПометку(ЭтаФорма.Команды.УстановитьПометку);
			Иначе
				ОсталосьОбработать = ОсталосьОбработать + 1;
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	                         
	Если ОсталосьОбработать = 0 Тогда
		Сообщить("Обработаны все переданные данные.");
	Иначе
		Сообщить("Переданные данные - осталось обработать строк: " + ВернутьПравильнуюСтрокуID(ОсталосьОбработать));
	КонецЕсли;	
	ЭтаФорма.ОбновитьОтображениеДанных();
	
КонецПроцедуры  // ОбработатьПереданныеДанные(Команда)

&НаСервере
Процедура ПроверитьЗаполнитьБанковскиеСчетаНаСервере()
	Если ЭтаФорма.ОрганизацияБанковскийСчет.Владелец <> ЭтаФорма.Организация Тогда
		ЭтаФорма.ОрганизацияБанковскийСчет = Справочники.БанковскиеСчета.ПустаяСсылка();
	КонецЕсли;	
	Если ЗначениеЗаполнено(ЭтаФорма.Контрагент1C.ОсновнойБанковскийСчет) И НЕ
		ЭтаФорма.Контрагент1C.ОсновнойБанковскийСчет.Валютный Тогда
		ЭтаФорма.КонтрагентБанковскийСчет = ЭтаФорма.Контрагент1C.ОсновнойБанковскийСчет;
	КонецЕсли;	
КонецПроцедуры // ПроверитьЗаполнитьБанковскиеСчета()	

#КонецОбласти // ПередачаДанныхИзОперацииВозмещенияТаможПлатежейПоФайлуExcel

//ДополнительнаяОбработкаПолученныхДанных() - не проверено
// потом для обработки строки из ПереданныеДанныеИтоги подставить на закладку главное
// значения организации, контрагента, Договора, #Клиента заказа, Контрагент НЛ и можно будет использовать 	
// ЗаполнитьСписокДТФактическоеВыполнение("ЗакладкаПереданныеДанные", КилограммСсылка), но текст запроса
// данных ДТ несколько другой, приведен ниже

//-- в декларации на закладке грузовые единицы могут быть грузовые единицы из декларации у которых не указан номер груз ед из заказа
//-- для таких запией list_order.id_company_client будет NULL
//SELECT distinct
//   list_gtd.id as gtd_id, 
//   list_gtd.gtd_number,
//   COALESCE(list_gtd.date_clear + interval '3 hour','1000-01-01'::timestamp) as date_clear,  -- дата выпуска
//   COALESCE(list_gtd.date_conditional_clear + interval '3 hour','1000-01-01'::timestamp) as date_conditional_clear, -- дата условного выпуска
//   COALESCE(list_gtd.date_release_for_procuring + interval '3 hour','1000-01-01'::timestamp) as date_release_for_procuring, -- дата выпуска под обеспечение
//   list_order.id as order_id, 
//   COALESCE(list_order.sys_serial_number, '') as sys_serial_number,
//   COALESCE(list_gtd.id_list_client, 0) as id_list_client,  -- контрактодержатель
//   COALESCE(LIST_GTD.sum_curs, 0) as sum_curs, -- курс валюты графа 23, курс взаиморасчетов
//   COALESCE(LIST_GTD.seller_name, '') as seller_name,  -- наименование контрагента для документа ПоступлениеТоваровУслуг
//   COALESCE(list_order.order_date + interval '3 hour','1000-01-01'::timestamp) as order_date,  -- дата заказа
//   COALESCE(LIST_GTD.contract_number, '') as contract_number,
//   COALESCE(LIST_GTD.contract_date + interval '3 hour','1000-01-01'::timestamp) as contract_date,
//   COALESCE(list_order.number_by_client, '') as number_by_client
//FROM public.list_gtd list_gtd
// LEFT JOIN declaration.unit AS dec_unit ON dec_unit.id_list_gtd = list_gtd.id
// LEFT JOIN declaration.dec_unit_oper_unit_link AS link ON link.id_declaration_unit = dec_unit.id
// LEFT JOIN operation.unit unit ON link.id_operation_unit = unit.id
// LEFT JOIN public.order list_order ON unit.id_order = list_order.id
//WHERE --list_gtd.id >= 89000 and list_gtd.id <= 89500
//   list_gtd.deleted = 0
//   and list_gtd.date_recall is NULL -- дата отзыва
//   and list_gtd.date_refuse is NULL -- дата отказа в выпуске 
//   and list_gtd.gtd_number is not null
//   and list_order.id_company_client = " + ВернутьПравильнуюСтрокуID(ЭтаФорма.IDКлиентаЗаказа) + "
//   --and (LEAST(list_gtd.date_clear + interval '3 hour', list_gtd.date_conditional_clear + interval '3 hour', list_gtd.date_release_for_procuring + interval '3 hour') 
//   --   BETWEEN Cast('" + ДатаВФорматеЗапросаPostgres(ЭтаФорма.НачальнаяДата) + "' as timestamp) + interval '3 hour' 
//   --          and Cast('" + ДатаВФорматеЗапросаPostgres(КонецДня(ЭтаФорма.КонечнаяДата)) + "' as timestamp) + interval '3 hour')
//and list_gtd.id in 
//(126462,126006,128620,128783,125719,125718,125722,125759,125802,125824,125827,125832,125829,125831,125836,
//125844,125849,125884,125925,125921,125927,125977,125979,125990,125987,125980,125986,125981,125994,125988,125993,
//125995,126001,126000,126003,126005,126017,126018,126021,126019,126066,126070,126109,126145,126108,126113,126147,
//126148,126156,126198,126200,126161,126203,126199,126207,126211,126214,126217,126223,126229,126231)
//order by list_gtd.id 
